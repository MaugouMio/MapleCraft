import 121 as virtual
import 122 as virtual

namespace 12() as virtual
{
	folder 1() from 121();
	folder 2() from 122();
	
	func install()
	{
		scoreboard objectives add 1200001_level dummy
		scoreboard objectives add 1200003_level dummy
		scoreboard objectives add blunt_final_atk dummy
		scoreboard objectives add blunt_final_rate dummy
		
		scoreboard objectives add 1201005_level dummy
		scoreboard objectives add 1201005_mp dummy
		scoreboard objectives add 1201005_time dummy
		
		scoreboard objectives add 1201006_level dummy
		scoreboard objectives add 1201006_mp dummy
		scoreboard objectives add 1201006_time dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder transfer()
		{
			func check()
			{
				scoreboard players operation #GLOBAL temp = @s level
				scoreboard players remove #GLOBAL temp 30
				scoreboard players operation #GLOBAL temp *= #3 const
				execute if score @s[tag=warrior] job_level matches 1 if score @s level matches 30.. unless score @s skill_point > #GLOBAL temp at @s run function ARG(_PATH)run
			}
			
			func run()
			{
				tag @s add page
				function stat:job/hp_mp/warrior/page_spearman
					
				scoreboard players set @s 1100000_level 0
				scoreboard players set @s 1200001_level 0
				scoreboard players set @s 1100002_level 0
				scoreboard players set @s 1200003_level 0
				scoreboard players set @s 1101004_level 0
				scoreboard players set @s 1201005_level 0
				scoreboard players set @s 1201006_level 0
				scoreboard players set @s 1101007_level 0
			}
		}
		
		# 1200000 shares the same functions with 1100000
		
		folder 0001()
		{
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 1200001_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					```
					for i in range(1, 21):
						print(f"execute if score @s 1200001_level matches {i} run function ARG(_PATH){i}")
					```
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 1200001_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s blunt_mastery
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s blunt_color
					
					execute if score @s 1200001_level matches ..19 run function ARG(_PATH)get_next_level
					execute if score @s 1200001_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.1200001","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[0]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 1200001_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 1200001_level
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s blunt_mastery
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[2]","storage":"ui:main"}]},{"translate":"skill.description.level.1200001","with":[{"nbt":"temp[3]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 1200001_level 1
					scoreboard players remove @s blunt_accuracy 1
					execute store result score @s blunt_mastery run data get storage ui:main temp[1]
					execute store result score @s blunt_color run data get storage ui:main temp[4]
					execute if score @s 1200001_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			```
			for i in range(18):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s blunt_mastery {i//2*5+15}
					scoreboard players set @s blunt_accuracy {i+1}
					scoreboard players set @s blunt_color {mastery_colors[i//2]}
				}}
				''')
			for i in range(18, 20):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s blunt_mastery {i//2*5+15}
					scoreboard players set @s blunt_accuracy {i+1}
				}}
				''')
			```
			}
		}
		
		# 1200002 shares the same functions with 1100002
		
		folder 0003()
		{
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 1200003_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					execute store result score @s blunt_final_atk run scoreboard players operation @s blunt_final_rate = @s 1200003_level
					scoreboard players operation @s blunt_final_atk *= #5 const
					scoreboard players add @s blunt_final_atk 100
					scoreboard players operation @s blunt_final_rate *= #2 const
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 1200003_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s blunt_final_rate
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s blunt_final_atk
					
					execute if score @s 1200003_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 1200003_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.1200003","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 1200003_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 1200003_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s blunt_final_rate
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s blunt_final_atk
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.1200003","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 1200003_level 1
					execute store result score @s blunt_final_rate run data get storage ui:main temp[1]
					execute store result score @s blunt_final_atk run data get storage ui:main temp[2]
					execute if score @s 1200003_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			}
		}
		
		# 1200004 shares the same functions with 1100004
		
		folder 1005() from booster_skill_template(1201005, blunt, k)
		{
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:25}
				give @s minecraft:carrot_on_a_stick{CustomModelData:25,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.1201005","bold":true,"italic":false}'}}
			}
		}
		
		folder 1006()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 1201006_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 1201006_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"#efb7ab"}
				execute if score @s mp >= @s 1201006_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				
				scoreboard players operation #THREATEN_LEVEL temp = @s 1201006_level
				scoreboard players operation #THREATEN_TIME temp = @s 1201006_time
				
				execute positioned ~ ~0.5 ~ run function skill:summon_font_effect/1201006effect
				playsound minecraft:skill.1201006 player @a ~ ~ ~ 1 1 0
				
				scoreboard players set #FOUND_ENEMY temp 0
				scoreboard players reset #PAIR_NUMBER temp
				execute positioned ~-4 ~-3 ~-4 as @e[type=#skill:enemy,tag=enemy,tag=!dead,dx=7,dy=5.5,dz=7] unless score @s weaken_from matches 2 run function ARG(_PATH)try_debuff
				
				scoreboard players operation @s mp -= @s 1201006_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func try_debuff()
			{
				execute if score #FOUND_ENEMY temp matches ..5 run function ARG(_PATH)type_check
			}
			
			func type_check()
			{
				execute if entity @s[type=zombified_piglin] run function ARG(_PATH)set_debuff
				execute if entity @s[type=magma_cube] unless score @s number = #PAIR_NUMBER temp run function ARG(_PATH)set_owner
			}
			
			func set_debuff()
			{
				scoreboard players operation @s weaken_time = #THREATEN_TIME temp
				scoreboard players set @s threat_particle 1
				tag @s add mob_effect
				scoreboard players set @s weaken_from 2
				
				# weapon attack drop
				execute store result score @s temp run attribute @s minecraft:generic.attack_damage base get 10
				scoreboard players remove @s temp 1
				execute store result entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"disorder"}].Amount float -0.1 run scoreboard players operation @s temp < #THREATEN_LEVEL temp
				scoreboard players operation #GLOBAL temp = @s temp
				execute if entity @s[tag=paired_mob] unless score @s number = #PAIR_NUMBER temp run function ARG(_PATH)debuff_pair
				
				# weapon defense drop
				scoreboard players operation @s wdef_drop = @s weapon_defense
				scoreboard players operation @s wdef_drop < #THREATEN_LEVEL temp
				
				scoreboard players add #FOUND_ENEMY temp 1
			}
			
			func set_owner()
			{
				scoreboard players operation #PAIR_NUMBER temp = @s number
				execute as @e[type=zombified_piglin,tag=paired_mob] if score @s number = #PAIR_NUMBER temp run function ARG(_PATH)set_debuff
				function ARG(_PATH)pair_effect
			}
			
			func debuff_pair()
			{
				scoreboard players operation #PAIR_NUMBER temp = @s number
				execute as @e[type=magma_cube,tag=enemy] if score @s number = #PAIR_NUMBER temp run function ARG(_PATH)pair_effect
			}
			
			func pair_effect()
			{
				execute store result entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"disorder"}].Amount float -0.1 run scoreboard players get #GLOBAL temp
				scoreboard players set @s weaken_from 2
			}
			
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 1201006_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 1201006_mp 12
					scoreboard players add @s[scores={1201006_level=11..}] 1201006_mp 8
					scoreboard players operation @s 1201006_time = @s 1201006_level
					scoreboard players operation @s 1201006_time *= #6 const
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 1201006_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 1201006_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 1201006_time
					
					execute if score @s 1201006_level matches ..19 run function ARG(_PATH)get_next_level
					execute if score @s 1201006_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.1201006","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[0]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 1201006_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 1201006_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 1201006_mp
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 1201006_time
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.1201006","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 1201006_level 1
					execute store result score @s 1201006_mp run data get storage ui:main temp[1]
					execute store result score @s 1201006_time run data get storage ui:main temp[2]
					execute if score @s 1201006_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:26}
				give @s minecraft:carrot_on_a_stick{CustomModelData:26,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.1201006","bold":true,"italic":false}'}}
			}
		}
		
		# 1201007 shares the same functions with 1101007
	}
}
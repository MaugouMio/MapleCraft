namespace 0() as virtual
{
	func install()
	{
		scoreboard objectives add 0001000_mp dummy
		scoreboard objectives add 0001000_attack dummy
		
		scoreboard objectives add 0001001_level dummy
		scoreboard objectives add 0001001_mp dummy
		scoreboard objectives add 0001001_cd dummy
		
		scoreboard objectives add 0001002_level dummy
		scoreboard objectives add 0001002_mp dummy
		scoreboard objectives add 0001002_time dummy
		scoreboard objectives add 0001002_cd dummy
	}
	
	folder 0()
	{
		folder 0()
		{
			folder 1000() from bullet_skill_template(0001000)
			{
				func check()
				{
					execute unless score @s action_time matches 0.. unless score @s invis_buff_time matches 1.. if score @s[nbt={OnGround:1b}] 0001000_level matches 1.. run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					function skill:check_weapon/single_hand/run
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 0001000_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 0001000_mp run function ARG(_PATH)run
				}
				
				func mark_target()
				{
					scoreboard players set #DISTANCE temp 9
					scoreboard players set #SEARCH_HEIGHT temp 0
					scoreboard players set #MAX_ENEMY temp 1
					function skill:enemy_search/search_front/search
				}
				
				func summon_bullet()
				{
					# max_distance should be set to (#DISTANCE temp - 1)
					execute if score @s 0001000_level matches 1 rotated ~ 0 run summon minecraft:item_display ^ ^ ^1 {Tags:["first_bullet","new_bullet","bullet"],item:{id:"minecraft:potion",Count:1,tag:{appear_delay:11,max_distance:8,skill_id:8,bullet_model:6000,CustomModelData:1}},item_display:"head"}
					execute if score @s 0001000_level matches 2 rotated ~ 0 run summon minecraft:item_display ^ ^ ^1 {Tags:["first_bullet","new_bullet","bullet"],item:{id:"minecraft:potion",Count:1,tag:{appear_delay:11,max_distance:8,skill_id:8,bullet_model:6001,CustomModelData:1}},item_display:"head"}
					execute if score @s 0001000_level matches 3 rotated ~ 0 run summon minecraft:item_display ^ ^ ^1 {Tags:["first_bullet","new_bullet","bullet"],item:{id:"minecraft:potion",Count:1,tag:{appear_delay:11,max_distance:8,skill_id:8,bullet_model:6002,CustomModelData:1}},item_display:"head"}
				}
				
				func set_numeric()
				{
					scoreboard players operation #SNAIL_DAMAGE temp = @s 0001000_attack
					
					scoreboard players operation @s mp -= @s 0001000_mp
					function bar_display:change/mp/dropped
					
					scoreboard players set @s action_time 16
				}
				
				func cast_effect()
				{
					playsound minecraft:skill.1001004 player @a ~ ~ ~ 1 1 0
				}
				
				func effect()
				{
					scoreboard players operation #SNAIL_DAMAGE temp = @s display_number
					execute summon item_display run function ARG(_PATH)effect_model_set
					function skill:mob_effect/play_hurt_sound/run
				}

				func effect_model_set()
				{
					data merge entity @s {Tags:["model_animation"],item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:1}},item_display:"head",billboard:"center"}
					scoreboard players set @s max_life 11
					execute if score #SNAIL_DAMAGE temp matches 10 store result entity @s item.tag.CustomModelData int 1 run scoreboard players set @s base_model 100018
					execute if score #SNAIL_DAMAGE temp matches 25 store result entity @s item.tag.CustomModelData int 1 run scoreboard players set @s base_model 100029
					execute if score #SNAIL_DAMAGE temp matches 40 store result entity @s item.tag.CustomModelData int 1 run scoreboard players set @s base_model 100040

					tp @s ~ ~ ~ ~ 0
					function skill:get_horizon_offset_color
					execute store result entity @s item.tag.CustomPotionColor int 1 run scoreboard players add #MODEL_COLOR temp 12
				}
				
				func append_scheduler()
				{
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
				}
				
				# override
				func hit()
				{
					scoreboard players operation #BIND_PLAYER number = @s number
					execute on passengers if entity @s[type=interaction] store result score #HORIZON_OFFSET temp run data get entity @s width 50
					execute as @e[type=item_display,tag=new_bullet] run function skill:bullet/setting
					
					################################################# replace calculate damage
					scoreboard players operation @s hp -= #SNAIL_DAMAGE temp
					scoreboard players set #SILENT_UPDATE_HP temp 0
					function skill:calculate_damage/update_hp_display/run
					execute if score @s hp matches ..0 run function skill:calculate_damage/record_dead
					#################################################
					# mp eater
					execute if score #ABSORB_RATE temp matches 1.. if score @s[tag=!boss] mp matches 1.. run function skill:2/1/0/0000/try_absorb
					
					data modify entity @s ArmorItems[3].tag.bullet_list append value {}
					execute store result entity @s ArmorItems[3].tag.bullet_list[-1].bullet_id int 1 run scoreboard players get #BULLET_ID number
					################################################# replace record damage from damage_copy
					data modify entity @s ArmorItems[3].tag.bullet_list[-1].damage_record set value [{number:0,crit:0,height:0}]
					execute store result entity @s ArmorItems[3].tag.bullet_list[-1].damage_record[-1].number int 1 run scoreboard players get #SNAIL_DAMAGE temp
					execute on passengers if entity @s[type=interaction] store result score #NUMBER_HEIGHT temp run data get entity @s height 100
					# calculate hit height
					scoreboard players operation #MAX_HEIGHT temp = #NUMBER_HEIGHT temp
					scoreboard players operation #MAX_HEIGHT temp /= #10 const
					scoreboard players add #MAX_HEIGHT temp 2
					scoreboard players operation #OFFSET_Y temp = #POSY temp
					execute store result score #POSY2 temp run data get entity @s Pos[1] 10
					scoreboard players operation #OFFSET_Y temp -= #POSY2 temp
					scoreboard players operation #OFFSET_Y temp > #0 const
					scoreboard players operation #OFFSET_Y temp < #MAX_HEIGHT temp
					execute store result entity @s ArmorItems[3].tag.bullet_list[-1].damage_record[-1].height int 1 run scoreboard players get #NUMBER_HEIGHT temp
					execute store result entity @s ArmorItems[3].tag.bullet_list[-1].damage_record[-1].offset int 1 run scoreboard players get #OFFSET_Y temp
					#################################################
					data modify entity @s ArmorItems[3].tag.bullet_list[-1].damage_info set from storage skill:main damage_info
					
					scoreboard players add @s queuing_effect 1
					tag @s remove target
					tag @s remove first_target
				}
				
				folder levels()
				{
					func add()
					{
						scoreboard players add @s 0001000_level 1
						function ARG(_PATH)check
						function skill:window/after_addition
					}
					
					func check()
					{
						execute if score @s 0001000_level matches 1 run function ARG(_PATH)1
						execute if score @s 0001000_level matches 2 run function ARG(_PATH)2
						execute if score @s 0001000_level matches 3 run function ARG(_PATH)3
					}
					
					func set_description()
					{
						data modify block 250 0 0 front_text.messages[0] set value '""'
						data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
						execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 0001000_level
						execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 0001000_mp
						execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 0001000_attack
						
						execute if score @s 0001000_level matches ..2 run function ARG(_PATH)get_next_level
						execute if score @s 0001000_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.0001000","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
						data modify storage ui:main skill_level_info append from block 250 0 0 front_text.messages[0]
					}
					
					func get_next_level()
					{
						scoreboard players add @s 0001000_level 1
						
						function ARG(_PATH)check
						execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 0001000_level
						execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 0001000_mp
						execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 0001000_attack
						data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.0001000","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
						
						scoreboard players remove @s 0001000_level 1
						execute store result score @s 0001000_mp run data get storage ui:main temp[1]
						execute store result score @s 0001000_attack run data get storage ui:main temp[2]
						execute if score @s 0001000_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '["\\n",{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
					}
				```
				for i in range(3):
					print(f'''
					func {i+1}()
					{{
						scoreboard players set @s 0001000_mp {i*5+10}
						scoreboard players set @s 0001000_attack {i*15+10}
					}}
					''')
				```
				}
				
				func get()
				{
					clear @s minecraft:carrot_on_a_stick{CustomModelData:1}
					give @s minecraft:carrot_on_a_stick{CustomModelData:1,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.0001000","font":"minecraft:uniform","bold":true,"italic":false}'}}
				}
			}
			
			folder 1001()
			{
				func check()
				{
					execute if score @s 0001001_level matches 1.. unless score @s invis_buff_time matches 1.. run function ARG(_PATH)cd_check
				}
				
				func cd_check()
				{
					execute if score @s 0001001_cd matches 1.. run tellraw @s {"translate":"warning.skill.in_cd","font":"minecraft:uniform","color":"#efb7ab","with":[{"score":{"name":"@s","objective":"0001001_cd"}}]}
					execute unless score @s 0001001_cd matches 1.. run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					execute if score @s mp < @s 0001001_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
					execute if score @s mp >= @s 0001001_mp run function ARG(_PATH)cast_effect
				}
				
				func cast_effect()
				{
					function ARG(_PATH)recov/run
					
					# set this buff values
					scoreboard players set @s recov_buff_time 30
					```
					print(f"attribute @s minecraft:generic.luck modifier add {simple_buff_info_dict['recov']['uuid']} g 0 add")
					```
					# VVV since we got mp change below, no need to update the bar display VVV
					# tag @s add hp_mp_varying
					
					scoreboard players operation #BIND_PLAYER number = @s number
					execute summon text_display run function skill:summon_font_effect/0001001effect
					playsound minecraft:skill.2301004 player @a ~ ~ ~ 1 1 0
					
					scoreboard players operation @s mp -= @s 0001001_mp
					function bar_display:change/mp/dropped
					
					scoreboard players set @s 0001001_cd 120
					function ARG(_PATH)cd_counter/add
				}
				
				folder cd_counter() from cd_counter(0001001);
				
				folder recov() from simple_buff_template("recov")
				{
					func execute()
					{
						scoreboard players operation #GLOBAL temp = @s recov_buff_time
						scoreboard players operation #GLOBAL temp %= #5 const
						execute if score #GLOBAL temp matches 0 run function ARG(_PATH)recover
					}
					
					func recover()
					{
						scoreboard players operation @s display_number = @s 0001001_level
						scoreboard players operation @s display_number *= #4 const
						
						execute store result score #GLOBAL temp if score @s hp < @s hp_max
						scoreboard players operation @s hp += @s display_number
						scoreboard players operation @s hp < @s hp_max
						
						scoreboard players set @s display_num_type 3
						execute positioned ~ ~2 ~ run function number_display:show
						
						execute if score #GLOBAL temp matches 1 run function bar_display:change/hp/recovered
					}
				}
				
				folder levels()
				{
					func add()
					{
						scoreboard players add @s 0001001_level 1
						function ARG(_PATH)check
						function skill:window/after_addition
					}
					
					func check()
					{
						execute if score @s 0001001_level matches 1 run scoreboard players set @s 0001001_mp 10
						execute if score @s 0001001_level matches 2 run scoreboard players set @s 0001001_mp 20
						execute if score @s 0001001_level matches 3 run scoreboard players set @s 0001001_mp 30
					}
					
					func set_description()
					{
						data modify block 250 0 0 front_text.messages[0] set value '""'
						data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
						execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 0001001_level
						execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 0001001_mp
						execute store result storage ui:main temp[2] int 24 run scoreboard players get @s 0001001_level
						
						execute if score @s 0001001_level matches ..2 run function ARG(_PATH)get_next_level
						execute if score @s 0001001_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.0001001","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
						data modify storage ui:main skill_level_info append from block 250 0 0 front_text.messages[0]
					}
					
					func get_next_level()
					{
						scoreboard players add @s 0001001_level 1
						
						function ARG(_PATH)check
						execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 0001001_level
						execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 0001001_mp
						execute store result storage ui:main temp[5] int 24 run scoreboard players get @s 0001001_level
						data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.0001001","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
						
						scoreboard players remove @s 0001001_level 1
						execute store result score @s 0001001_mp run data get storage ui:main temp[1]
						execute if score @s 0001001_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '["\\n",{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
					}
				}
				
				func get()
				{
					clear @s minecraft:carrot_on_a_stick{CustomModelData:2}
					give @s minecraft:carrot_on_a_stick{CustomModelData:2,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.0001001","font":"minecraft:uniform","bold":true,"italic":false}'}}
				}
			}
			
			folder 1002()
			{
				func check()
				{
					execute if score @s 0001002_level matches 1.. unless score @s invis_buff_time matches 1.. run function ARG(_PATH)cd_check
				}
				
				func cd_check()
				{
					execute if score @s 0001002_cd matches 1.. run tellraw @s {"translate":"warning.skill.in_cd","font":"minecraft:uniform","color":"#efb7ab","with":[{"score":{"name":"@s","objective":"0001002_cd"}}]}
					execute unless score @s 0001002_cd matches 1.. run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					execute if score @s mp < @s 0001002_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
					execute if score @s mp >= @s 0001002_mp run function ARG(_PATH)cast_effect
				}
				
				func cast_effect()
				{
					function skill:buff/speed/run
					
					# set sub buff values
					scoreboard players operation @s speed -= @s speed_buff_num
					execute if score @s 0001000_level matches 1 run scoreboard players set @s speed_buff_num 10
					execute if score @s 0001000_level matches 2 run scoreboard players set @s speed_buff_num 15
					execute if score @s 0001000_level matches 3 run scoreboard players set @s speed_buff_num 20
					scoreboard players operation @s speed += @s speed_buff_num
					function stat:speed_changed/check
					
					# set this buff values
					scoreboard players operation @s speed_buff_time = @s 0001002_time
					```
					print(f"attribute @s minecraft:generic.luck modifier add {buff_info_dict['speed']['uuid']} 2 0 add")
					```
					# VVV since we got mp change below, no need to update the bar display VVV
					# tag @s add hp_mp_varying
					
					scoreboard players operation #BIND_PLAYER number = @s number
					execute summon text_display run function skill:summon_font_effect/0001002effect
					playsound minecraft:skill.1111005 player @a ~ ~ ~ 1 1 0
					
					scoreboard players operation @s mp -= @s 0001002_mp
					function bar_display:change/mp/dropped
					
					scoreboard players set @s 0001002_cd 60
					function ARG(_PATH)cd_counter/add
				}
				
				folder cd_counter() from cd_counter(0001002);
				
				folder levels()
				{
					func add()
					{
						scoreboard players add @s 0001002_level 1
						function ARG(_PATH)check
						function skill:window/after_addition
					}
					
					func check()
					{
						execute if score @s 0001002_level matches 1 run function ARG(_PATH)1
						execute if score @s 0001002_level matches 2 run function ARG(_PATH)2
						execute if score @s 0001002_level matches 3 run function ARG(_PATH)3
					}
					
					func set_description()
					{
						data modify block 250 0 0 front_text.messages[0] set value '""'
						data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
						execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 0001002_level
						execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 0001002_mp
						execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 0001002_time
						
						scoreboard players operation #GLOBAL temp = @s 0001002_level
						scoreboard players operation #GLOBAL temp *= #5 const
						scoreboard players add #GLOBAL temp 5
						
						execute if score @s 0001002_level matches ..2 run function ARG(_PATH)get_next_level
						execute if score @s 0001002_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.0001002","with":[{"nbt":"temp[1]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
						data modify storage ui:main skill_level_info append from block 250 0 0 front_text.messages[0]
					}
					
					func get_next_level()
					{
						scoreboard players add @s 0001002_level 1
						scoreboard players add #GLOBAL temp 5
						
						function ARG(_PATH)check
						execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 0001002_level
						execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 0001002_mp
						execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 0001002_time
						data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.0001002","with":[{"nbt":"temp[4]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}},{"nbt":"temp[5]","storage":"ui:main"}]}]'
						
						scoreboard players remove @s 0001002_level 1
						scoreboard players remove #GLOBAL temp 5
						execute store result score @s 0001002_mp run data get storage ui:main temp[1]
						execute store result score @s 0001002_time run data get storage ui:main temp[2]
						execute if score @s 0001002_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '["\\n",{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
					}
				```
				for i in range(3):
					print(f'''
					func {i+1}()
					{{
						scoreboard players set @s 0001002_mp {i*5+10}
						scoreboard players set @s 0001002_time {i*4+4}
					}}
					''')
				```
				}
				
				func get()
				{
					clear @s minecraft:carrot_on_a_stick{CustomModelData:3}
					give @s minecraft:carrot_on_a_stick{CustomModelData:3,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.0001002","font":"minecraft:uniform","bold":true,"italic":false}'}}
				}
			}
		}
	}
}
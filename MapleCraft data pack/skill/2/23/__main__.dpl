import 231 as virtual
import 232 as virtual

namespace 23() as virtual
{
	folder 1() from 231();
	folder 2() from 232();
	
	func install()
	{
		scoreboard objectives add 2301002_mp dummy
		
		scoreboard objectives add 2301003_level dummy
		scoreboard objectives add 2301003_mp dummy
		scoreboard objectives add 2301003_rate dummy
		scoreboard objectives add 2301003_time dummy
		
		scoreboard objectives add 2301004_level dummy
		scoreboard objectives add 2301004_mp dummy
		scoreboard objectives add 2301004_time dummy
		
		scoreboard objectives add 2301005_mp dummy
		scoreboard objectives add 2301005_mastery dummy
		scoreboard objectives add 2301005_attack dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder transfer()
		{
			func check()
			{
				scoreboard players operation #GLOBAL temp = @s level
				scoreboard players remove #GLOBAL temp 30
				scoreboard players operation #GLOBAL temp *= #3 const
				execute if score @s[tag=magician] job_level matches 1 if score @s level matches 30.. unless score @s skill_point > #GLOBAL temp at @s run function ARG(_PATH)run
			}
			
			func run()
			{
				tag @s add cleric
				function stat:job/hp_mp/magician/second
					
				scoreboard players set @s 2100000_level 0
				scoreboard players set @s 2101002_level 0
				scoreboard players set @s 2301002_level 0
				scoreboard players set @s 2301003_level 0
				scoreboard players set @s 2301004_level 0
				scoreboard players set @s 2301005_level 0
			}
		}
		
		# 2300000 shares the same functions with 2100000
		# 2301001 shares the same functions with 2101002
		
		folder 1002() from attack_skill_template(2301002)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s 2301002_level matches 1.. run function ARG(_PATH)rope_check
			}
			
			func rope_check()
			{
				scoreboard players set #GLOBAL temp 0
				execute if block ~0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~0.3 ~-0.4 ~-0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~-0.3 #minecraft:climbable run scoreboard players set #GLOBAL temp 1
				execute if score #GLOBAL temp matches 0 unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2301002_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score @s mp >= @s 2301002_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				tag @s add target
				execute positioned ~-4 ~-3 ~-4 store result score #FOUND_ALLY temp run tag @a[tag=!dead,tag=!target,dx=7,dy=5.5,dz=7,limit=5,sort=random] add target
				scoreboard players add #FOUND_ALLY temp 1
				execute if score #FOUND_ALLY temp matches ..5 run function ARG(_PATH)mark_enemy/run
				
				tag @s add caster
			}
			
			folder mark_enemy()
			{
				func run()
				{
					data modify storage skill:main temp set value []
					scoreboard players set #MAX_ENEMY temp 6
					scoreboard players set #FOUND_ENEMY temp 0
					scoreboard players operation #MAX_ENEMY temp -= #FOUND_ALLY temp
					
					execute positioned ~-4 ~-3 ~-4 as @e[type=interaction,tag=heal_target,dx=7,dy=5.5,dz=7] run function ARG(_PATH)try_mark
					scoreboard players operation #FOUND_ENEMY temp += #FOUND_ALLY temp
				}
			
				func try_mark()
				{
					execute if score #FOUND_ENEMY temp < #MAX_ENEMY temp on vehicle run function ARG(_PATH)mark
				}
				
				func mark()
				{
					tag @s add target
					
					scoreboard players operation @s hit_distance = #FOUND_ENEMY temp
					data modify storage skill:main temp append value 0
					execute store result storage skill:main temp[-1] int 1 run scoreboard players get @s hit_distance
					
					scoreboard players add #FOUND_ENEMY temp 1
				}
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 1
				function skill:damage_formula/heal
				
				scoreboard players operation @s mp -= @s 2301002_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func cast_effect()
			{
				scoreboard players operation #BIND_PLAYER number = @s number
				execute summon text_display run function skill:summon_font_effect/2301002effect
				playsound minecraft:skill.2301002 player @a ~ ~ ~ 1 1 0
				
				execute as @a[tag=target] at @s run function ARG(_PATH)heal
				tag @s remove caster
			}
			
			func heal()
			{
				# 給文字動畫吃的高度設定
				scoreboard players set #DEFAULT_SIZE temp 22
				scoreboard players operation #BIND_PLAYER number = @s number
				execute if entity @s[tag=!caster] summon text_display run function ARG(_PATH)player_effect
				execute if score @s hp < @s hp_max run function ARG(_PATH)did_need_heal
				
				tag @s remove target
			}

			func player_effect()
			{
				function skill:summon_font_effect/2301002hit
				function skill:bind_player
			}
			
			func did_need_heal()
			{
				function ARG(_PATH)rand_recovery
				scoreboard players operation @s hp += #FINAL_HEAL temp
				scoreboard players operation @s hp < @s hp_max
				
				function bar_display:change/hp/recovered
			}
			
			func rand_recovery() from random().generate("#MIN_HEAL_AMOUNT temp", "#MAX_HEAL_AMOUNT temp", "#FINAL_HEAL temp");
			
			# override
			folder forward_effect()
			{
				func run()
				{
					execute if score @s display_number matches 1.. run function ARG(_PATH)effect
					function skill:mob_effect/play_hurt_sound/run
				}
				
				func effect()
				{
					scoreboard players operation #BIND_PLAYER number = @s number
					execute store result score #DEFAULT_SIZE temp run data get entity @s ArmorItems[3].tag.bind_point
					execute positioned ~ ~0.2625 ~ summon text_display run function ARG(_PATH)bind_effect
				}

				func bind_effect()
				{
					function skill:summon_font_effect/2301002hit
					function skill:bind_mob
				}
			}
				
			```
			# delay_range = max possible value of enemy amount
			append_scheduler(delay_range = 5, base_delay = 1, skill_path = "skill:2/3/0/1002/")
			```
				
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2301002_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 2301002_mp 12
					execute if score @s 2301002_level matches 16.. run scoreboard players add @s 2301002_mp 12
				}
				
				func set_description()
				{
					data modify block 250 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2301002_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2301002_mp
					execute store result storage ui:main temp[2] int 10 run scoreboard players get @s 2301002_level
					
					execute if score @s 2301002_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 2301002_level matches 1.. run data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2301002","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2301002_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2301002_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2301002_mp
					execute store result storage ui:main temp[5] int 10 run scoreboard players get @s 2301002_level
					data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.2301002","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2301002_level 1
					execute store result score @s 2301002_mp run data get storage ui:main temp[1]
					execute if score @s 2301002_level matches 1.. run data modify block 250 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"250 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:84}
				give @s minecraft:carrot_on_a_stick{CustomModelData:84,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2301002","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
		
		folder 1003()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 2301003_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2301003_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score @s mp >= @s 2301003_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				
				function ARG(_PATH)invin/run
				
				# set this buff values
				scoreboard players operation @s invin_buff_time = @s 2301003_time
				scoreboard players operation @s invin_buff_num = @s 2301003_rate
				```
				print(f"attribute @s minecraft:generic.luck modifier add {simple_buff_info_dict['invin']['uuid']} t 0 add")
				```
				# VVV since we got mp change below, no need to update the bar display VVV
				# tag @s add hp_mp_varying
				
				scoreboard players operation #BIND_PLAYER number = @s number
				execute summon text_display run function skill:summon_font_effect/2301003effect
				playsound minecraft:skill.2301003 player @a ~ ~ ~ 1 1 0
				
				scoreboard players operation @s mp -= @s 2301003_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func guard()
			{
				scoreboard players operation @s damage_taken *= @s invin_buff_num
				scoreboard players operation @s damage_taken /= #100 const
			}
			
			folder invin() from simple_buff_template("invin");
			
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2301003_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 2301003_mp 15
					scoreboard players add @s[scores={2301003_level=11..}] 2301003_mp 15
					scoreboard players set @s 2301003_rate 90
					scoreboard players operation @s 2301003_rate -= @s 2301003_level
					scoreboard players operation @s 2301003_time = @s 2301003_level
					scoreboard players operation @s 2301003_time *= #15 const
				}
				
				func set_description()
				{
					data modify block 250 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2301003_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2301003_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2301003_time
					execute store result storage ui:main temp[6] int 1 run scoreboard players get @s 2301003_rate
					
					execute if score @s 2301003_level matches ..19 run function ARG(_PATH)get_next_level
					scoreboard players set #GLOBAL temp 100
					scoreboard players operation #GLOBAL temp -= @s 2301003_rate
					execute if score @s 2301003_level matches 1.. run data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2301003","with":[{"nbt":"temp[1]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2301003_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2301003_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2301003_mp
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2301003_time
					scoreboard players set #GLOBAL temp 100
					scoreboard players operation #GLOBAL temp -= @s 2301003_rate
					data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.2301003","with":[{"nbt":"temp[4]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2301003_level 1
					execute store result score @s 2301003_mp run data get storage ui:main temp[1]
					execute store result score @s 2301003_time run data get storage ui:main temp[2]
					execute store result score @s 2301003_rate run data get storage ui:main temp[6]
					execute if score @s 2301003_level matches 1.. run data modify block 250 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"250 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:85}
				give @s minecraft:carrot_on_a_stick{CustomModelData:85,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2301003","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
		
		folder 1004()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 2301004_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2301004_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score @s mp >= @s 2301004_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				
				# set sub buff values
				scoreboard players operation #BLESS_LEVEL temp = @s 2301004_level
				scoreboard players operation #BLESS_TIME temp = @s 2301004_time
				
				function ARG(_PATH)setting
				tag @s add caster
				execute as @a[tag=!dead,tag=!caster,limit=5,distance=..5,sort=nearest] at @s run function ARG(_PATH)take_effect
				tag @s remove caster
				
				# VVV since we got mp change below, no need to update the bar display VVV
				# tag @s add hp_mp_varying
				
				scoreboard players operation #BIND_PLAYER number = @s number
				execute summon text_display run function skill:summon_font_effect/2301004effect
				playsound minecraft:skill.2301004 player @a ~ ~ ~ 1 1 0
				
				scoreboard players operation @s mp -= @s 2301004_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func take_effect()
			{
				function ARG(_PATH)setting
				tag @s add hp_mp_varying
				
				scoreboard players operation #BIND_PLAYER number = @s number
				execute summon text_display run function skill:summon_font_effect/2301004affected
			}
			
			func setting()
			{
				function skill:buff/angel/run
				
				# set sub buff values
				scoreboard players operation @s wdef_buff_num = #BLESS_LEVEL temp
				scoreboard players operation @s mdef_buff_num = #BLESS_LEVEL temp
				scoreboard players operation @s accu_buff_num = #BLESS_LEVEL temp
				scoreboard players operation @s avoid_buff_num = #BLESS_LEVEL temp
				function stat:calc_avoid/run
				
				# set this buff values
				scoreboard players operation @s angel_buff_time = #BLESS_TIME temp
				```
				print(f"attribute @s minecraft:generic.luck modifier add {buff_info_dict['angel']['uuid']} a 0 add")
				```
			}
			
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2301004_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 2301004_mp 12
					scoreboard players add @s[scores={2301004_level=11..}] 2301004_mp 12
					scoreboard players operation @s 2301004_time = @s 2301004_level
					scoreboard players operation @s 2301004_time *= #10 const
				}
				
				func set_description()
				{
					data modify block 250 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2301004_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2301004_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2301004_time
					
					execute if score @s 2301004_level matches ..19 run function ARG(_PATH)get_next_level
					execute if score @s 2301004_level matches 1.. run data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2301004","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[0]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2301004_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2301004_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2301004_mp
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2301004_time
					data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.2301004","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2301004_level 1
					execute store result score @s 2301004_mp run data get storage ui:main temp[1]
					execute store result score @s 2301004_time run data get storage ui:main temp[2]
					execute if score @s 2301004_level matches 1.. run data modify block 250 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"250 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:86}
				give @s minecraft:carrot_on_a_stick{CustomModelData:86,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2301004","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
		
		folder 1005() from bullet_skill_template(2301005)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s[nbt={OnGround:1b}] 2301005_level matches 1.. run function ARG(_PATH)rope_check
			}
			
			func rope_check()
			{
				scoreboard players set #GLOBAL temp 0
				execute if block ~0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~0.3 ~-0.4 ~-0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~-0.3 #minecraft:climbable run scoreboard players set #GLOBAL temp 1
				execute if score #GLOBAL temp matches 0 unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				scoreboard players operation #USED_WEAPON temp = @s weapon_type
				execute if score #USED_WEAPON temp matches 0 run tellraw @s {"translate":"warning.skill.no_weapon","font":"minecraft:uniform","color":"#efb7ab"}
				
				execute if score #USED_WEAPON temp matches 1.. if score @s mp < @s 2301005_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score #USED_WEAPON temp matches 1.. if score @s mp >= @s 2301005_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				scoreboard players set #DISTANCE temp 9
				scoreboard players set #SEARCH_HEIGHT temp 0
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func effect_model_set()
			{
				data merge entity @s {Tags:["model_animation"],item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:230000}},transformation:{scale:[0.625f,0.625f,0.625f],translation:[0.0f,1.6885f,0.0f]},item_display:"head"}
				scoreboard players set @s max_life 14
				scoreboard players set @s base_model 230000
				
				data modify entity @s transformation.left_rotation set from storage main:main rotation
				function skill:bind_player
			}
			
			func summon_bullet()
			{
				# max_distance should be set to (#DISTANCE temp - 1)
				execute rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^1 {Tags:["first_bullet","new_bullet","bullet"],CustomName:'{"text":"2","font":"space:default"}',ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1,tag:{appear_delay:11,max_distance:8,skill_id:5,bullet_model:2300,CustomModelData:1}}],Marker:1,Invisible:1,Invulnerable:1,NoGravity:1,Fire:32767s,CustomNameVisible:0}
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 6
				scoreboard players operation #SKILL_MASTERY temp = @s 2301005_mastery
				scoreboard players operation #SPELL_ATTACK temp = @s 2301005_attack
				function skill:damage_formula/spell
				
				scoreboard players operation @s mp -= @s 2301005_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 16
			}
				
			func cast_effect()
			{
				scoreboard players operation #BIND_PLAYER number = @s number
				# angle to PI
				execute store result storage main:main rotation.angle float 0.0001745329 run data get entity @s Rotation[0] 100
				execute rotated ~ 0 summon minecraft:item_display run function ARG(_PATH)effect_model_set
				playsound minecraft:skill.2301005 player @a ~ ~ ~ 1 1 0
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. positioned ^ ^ ^0.5 summon text_display run function skill:summon_font_effect/2301005hit
				function skill:mob_effect/play_hurt_sound/run
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
			}
				
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2301005_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 2301005_mp 12
					scoreboard players add @s[scores={2301005_level=16..}] 2301005_mp 12
					execute store result score @s 2301005_attack run scoreboard players operation @s 2301005_mastery = @s 2301005_level
					scoreboard players operation @s 2301005_attack *= #2 const
					scoreboard players add @s 2301005_attack 20
					scoreboard players remove @s 2301005_mastery 1
					scoreboard players operation @s 2301005_mastery /= #3 const
					scoreboard players operation @s 2301005_mastery *= #5 const
					scoreboard players add @s 2301005_mastery 15
				}
				
				func set_description()
				{
					data modify block 250 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2301005_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2301005_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2301005_attack
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2301005_mastery
					
					execute if score @s 2301005_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 2301005_level matches 1.. run data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2301005","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"}]},{"nbt":"Text1","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2301005_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2301005_level
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2301005_mp
					execute store result storage ui:main temp[6] int 1 run scoreboard players get @s 2301005_attack
					execute store result storage ui:main temp[7] int 1 run scoreboard players get @s 2301005_mastery
					data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[4]","storage":"ui:main"}]},{"translate":"skill.description.level.2301005","with":[{"nbt":"temp[5]","storage":"ui:main"},{"nbt":"temp[6]","storage":"ui:main"},{"nbt":"temp[7]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2301005_level 1
					execute store result score @s 2301005_mp run data get storage ui:main temp[1]
					execute store result score @s 2301005_attack run data get storage ui:main temp[2]
					execute store result score @s 2301005_mastery run data get storage ui:main temp[3]
					execute if score @s 2301005_level matches 1.. run data modify block 250 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"250 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:87}
				give @s minecraft:carrot_on_a_stick{CustomModelData:87,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2301005","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
	}
}
import 231 as virtual
import 232 as virtual

namespace 23() as virtual
{
	folder 1() from 231();
	folder 2() from 232();
	
	func install()
	{
		scoreboard objectives add 2301002_mp dummy
		
		scoreboard objectives add 2301003_level dummy
		scoreboard objectives add 2301003_mp dummy
		scoreboard objectives add 2301003_rate dummy
		scoreboard objectives add 2301003_time dummy
		
		scoreboard objectives add 2301004_level dummy
		scoreboard objectives add 2301004_mp dummy
		scoreboard objectives add 2301004_time dummy
		
		scoreboard objectives add 2301005_mp dummy
		scoreboard objectives add 2301005_mastery dummy
		scoreboard objectives add 2301005_attack dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		# 2300000 shares the same functions with 2100000
		# 2301001 shares the same functions with 2101002
		
		folder 1002() from attack_skill_template(2301002)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s 2301002_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2301002_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score @s mp >= @s 2301002_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				tag @s add target
				execute positioned ~ ~-2.5 ~ store result score #FOUND_ALLY temp run tag @a[tag=!dead,tag=!target,distance=..5,limit=5,sort=random] add target
				scoreboard players add #FOUND_ALLY temp 1
				execute if score #FOUND_ALLY temp matches ..5 run function ARG(_PATH)mark_enemy/run
				
				tag @s add caster
			}
			
			folder mark_enemy()
			{
				func run()
				{
					data modify storage skill:main temp set value []
					scoreboard players set #MAX_ENEMY temp 6
					scoreboard players set #FOUND_ENEMY temp 0
					scoreboard players operation #MAX_ENEMY temp -= #FOUND_ALLY temp
					
					execute positioned ~ ~-2.5 ~ as @e[type=zombified_piglin,tag=heal_target,tag=!dead,distance=..5,limit=5,sort=random] run function ARG(_PATH)try_mark
					scoreboard players operation #FOUND_ENEMY temp += #FOUND_ALLY temp
				}
			
				func try_mark()
				{
					execute if score #FOUND_ENEMY temp < #MAX_ENEMY temp run function ARG(_PATH)mark
				}
				
				func mark()
				{
					tag @s add target
					
					scoreboard players operation @s hit_distance = #FOUND_ENEMY temp
					data modify storage skill:main temp append value 0
					execute store result storage skill:main temp[-1] int 1 run scoreboard players get @s hit_distance
					
					scoreboard players add #FOUND_ENEMY temp 1
				}
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 6
				function skill:damage_formula/heal
				
				scoreboard players operation @s mp -= @s 2301002_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func cast_effect()
			{
				execute positioned ~ ~-0.4 ~ run function skill:summon_font_effect/2301002effect
				playsound minecraft:skill.2301002 player @a ~ ~ ~ 1 1 0
				
				execute as @a[tag=target] at @s run function ARG(_PATH)heal
				tag @s remove caster
			}
			
			func heal()
			{
				execute if entity @s[tag=!caster] positioned ~ ~0.8 ~ run function skill:summon_font_effect/2301002hit
				execute if score @s hp < @s hp_max run function ARG(_PATH)did_need_heal
				
				tag @s remove target
			}
			
			func did_need_heal()
			{
				function ARG(_PATH)rand_recovery
				scoreboard players operation @s hp += #FINAL_HEAL temp
				scoreboard players operation @s hp < @s hp_max
				
				function bar_display:change/hp/recovered
			}
			
			func rand_recovery() from random().generate("#MIN_HEAL_AMOUNT temp", "#MAX_HEAL_AMOUNT temp", "#FINAL_HEAL temp");
			
			func effect()
			{
				execute if score @s display_number matches 1.. positioned ~ ~0.8 ~ run function skill:summon_font_effect/2301002hit
				function skill:mob_effect/play_hurt_sound/run
			}
				
			```
			# delay_range = max possible value of enemy amount
			append_scheduler(delay_range = 5, base_delay = 1, skill_path = "skill:2/3/0/1002/")
			```
				
			folder levels()
			{
			```
			for i in range(30):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2301002_level {i+1}
					scoreboard players set @s 2301002_mp {i//15*12+12}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:84}
				give @s minecraft:carrot_on_a_stick{CustomModelData:84,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2301002","bold":true,"italic":false}'}}
			}
		}
		
		folder 1003()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 2301003_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2301003_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score @s mp >= @s 2301003_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				
				function ARG(_PATH)invin/run
				
				# set this buff values
				scoreboard players operation @s invin_buff_time = @s 2301003_time
				scoreboard players operation @s invin_buff_num = @s 2301003_rate
				```
				print(f"attribute @s minecraft:generic.luck modifier add {simple_buff_info_dict['invin']['uuid']} t 0 add")
				```
				# VVV since we got mp change below, no need to update the bar display VVV
				# execute if entity @s[tag=!hp_mp_varying] run function bar_display:update_bar/run
				
				execute positioned ~ ~-0.4 ~ run function skill:summon_font_effect/2301003effect
				playsound minecraft:skill.2301003 player @a ~ ~ ~ 1 1 0
				
				scoreboard players operation @s mp -= @s 2301003_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func guard()
			{
				scoreboard players operation @s damage_taken *= @s invin_buff_num
				scoreboard players operation @s damage_taken /= #100 const
			}
			
			folder invin() from simple_buff_template("invin");
			
			folder levels()
			{
			```
			for i in range(20):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2301003_level {i+1}
					scoreboard players set @s 2301003_mp {i//10*15+15}
					scoreboard players set @s 2301003_rate {89-i}
					scoreboard players set @s 2301003_time {i*15+15}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:85}
				give @s minecraft:carrot_on_a_stick{CustomModelData:85,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2301003","bold":true,"italic":false}'}}
			}
		}
		
		folder 1004()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 2301004_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2301004_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score @s mp >= @s 2301004_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				
				# set sub buff values
				scoreboard players operation #BLESS_LEVEL temp = @s 2301004_level
				scoreboard players operation #BLESS_TIME temp = @s 2301004_time
				
				function ARG(_PATH)setting
				tag @s add caster
				execute as @a[tag=!dead,tag=!caster,limit=5,distance=..5,sort=nearest] at @s run function ARG(_PATH)take_effect
				tag @s remove caster
				
				# VVV since we got mp change below, no need to update the bar display VVV
				# execute if entity @s[tag=!hp_mp_varying] run function bar_display:update_bar/run
				
				execute positioned ~ ~-0.3 ~ run function skill:summon_font_effect/2301004effect
				playsound minecraft:skill.2301004 player @a ~ ~ ~ 1 1 0
				
				scoreboard players operation @s mp -= @s 2301004_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func take_effect()
			{
				function ARG(_PATH)setting
				execute if entity @s[tag=!hp_mp_varying] run function bar_display:update_bar/run
				
				execute positioned ~ ~1.7 ~ run function skill:summon_font_effect/2301004affected
			}
			
			func setting()
			{
				function skill:buff/angel/run
				
				# set sub buff values
				scoreboard players operation @s wdef_buff_num = #BLESS_LEVEL temp
				scoreboard players operation @s mdef_buff_num = #BLESS_LEVEL temp
				scoreboard players operation @s accu_buff_num = #BLESS_LEVEL temp
				scoreboard players operation @s avoid_buff_num = #BLESS_LEVEL temp
				
				# set this buff values
				scoreboard players operation @s angel_buff_time = #BLESS_TIME temp
				```
				print(f"attribute @s minecraft:generic.luck modifier add {buff_info_dict['angel']['uuid']} a 0 add")
				```
			}
			
			folder levels()
			{
			```
			for i in range(20):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2301004_level {i+1}
					scoreboard players set @s 2301004_mp {i//10*12+12}
					scoreboard players set @s 2301004_time {i*10+10}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:86}
				give @s minecraft:carrot_on_a_stick{CustomModelData:86,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2301004","bold":true,"italic":false}'}}
			}
		}
		
		folder 1005() from bullet_skill_template(2301005)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s[nbt={OnGround:1b}] 2301005_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				scoreboard players operation #USED_WEAPON temp = @s weapon_type
				execute if score #USED_WEAPON temp matches 0 run tellraw @s {"translate":"warning.skill.no_weapon","color":"red"}
				
				execute if score #USED_WEAPON temp matches 1.. if score @s mp < @s 2301005_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score #USED_WEAPON temp matches 1.. if score @s mp >= @s 2301005_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				scoreboard players set #DISTANCE temp 9
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func effect_model_set()
			{
				execute positioned as @s run tp @s ~ ~ ~ ~ ~
				scoreboard players set @s max_life 14
				scoreboard players set @s base_model 230000
				tag @s remove new
			}
			
			func summon_bullet()
			{
				# max_distance should be set to (#DISTANCE temp - 3)
				execute rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^1 {Tags:["first_bullet","new_bullet","bullet"],CustomName:'{"text":"2","font":"space:default"}',ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1,tag:{appear_delay:11,max_distance:6,skill_id:5,bullet_model:2300,CustomModelData:1}}],Marker:1,Invisible:1,Invulnerable:1,NoGravity:1,Fire:32767s,CustomNameVisible:0}
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 6
				scoreboard players operation #SKILL_MASTERY temp = @s 2301005_mastery
				scoreboard players operation #SPELL_ATTACK temp = @s 2301005_attack
				function skill:damage_formula/spell
				
				scoreboard players operation @s mp -= @s 2301005_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 16
			}
				
			func cast_effect()
			{
				execute rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^1 {Tags:["model_animation","new"],Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,Fire:32767s,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:230000}}]}
				execute as @e[type=armor_stand,tag=new,limit=1] run function ARG(_PATH)effect_model_set
				playsound minecraft:skill.2301005 player @a ~ ~ ~ 1 1 0
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. positioned ^ ^ ^0.5 run function skill:summon_font_effect/2301005hit
				function skill:mob_effect/play_hurt_sound/run
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
			}
				
			folder levels()
			{
			```
			for i in range(30):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2301005_level {i+1}
					scoreboard players set @s 2301005_mp {i//15*12+12}
					scoreboard players set @s 2301005_attack {i*2+22}
					scoreboard players set @s 2301005_mastery {i//3*5+15}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:87}
				give @s minecraft:carrot_on_a_stick{CustomModelData:87,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2301005","bold":true,"italic":false}'}}
			}
		}
	}
}
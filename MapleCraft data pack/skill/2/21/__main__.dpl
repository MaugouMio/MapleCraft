import 211 as virtual
import 212 as virtual

namespace 21() as virtual
{
	folder 1() from 211();
	folder 2() from 212();
	
	func install()
	{
		scoreboard objectives add 2100000_level dummy
		scoreboard objectives add 2100000_rate dummy
		scoreboard objectives add 2100000_absorb dummy
		
		scoreboard objectives add 2101001_level dummy
		scoreboard objectives add 2101001_mp dummy
		scoreboard objectives add 2101001_time dummy
		
		scoreboard objectives add 2101002_level dummy
		scoreboard objectives add 2101002_mp dummy
		scoreboard objectives add teleport_time dummy
		
		scoreboard objectives add 2101003_level dummy
		scoreboard objectives add 2101003_mp dummy
		scoreboard objectives add 2101003_time dummy
		
		scoreboard objectives add 2101004_mp dummy
		scoreboard objectives add 2101004_mastery dummy
		scoreboard objectives add 2101004_attack dummy
		
		scoreboard objectives add 2101005_mp dummy
		scoreboard objectives add 2101005_mastery dummy
		scoreboard objectives add 2101005_attack dummy
		# poison time should be (sec)
		scoreboard objectives add 2101005_rate dummy
		scoreboard objectives add 2101005_poison dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder 0000()
		{
			func set_numeric()
			{
				scoreboard players set #TOTAL_ABSORB temp 0
				scoreboard players operation #ABSORB_RATE temp = @s 2100000_rate
				scoreboard players operation #ABSORB_AMOUNT temp = @s 2100000_absorb
			}
			
			func cast_effect()
			{
				scoreboard players operation @s mp += #TOTAL_ABSORB temp
				scoreboard players operation @s mp < @s mp_max
				function bar_display:change/mp/recovered
				
				execute positioned ~ ~-0.4 ~ run function skill:summon_font_effect/2100000effect
			}
				
			func try_absorb()
			{
				function main:rand_rate
				execute if score #RAND_RESULT temp < #ABSORB_RATE temp run function ARG(_PATH)absorb
			}
			
			func absorb()
			{
				scoreboard players operation @s 2100000_absorb = #ABSORB_AMOUNT temp
				scoreboard players operation @s 2100000_absorb *= @s mp_max
				scoreboard players operation @s 2100000_absorb /= #100 const
				scoreboard players operation @s 2100000_absorb < @s mp
				
				scoreboard players operation @s mp -= @s 2100000_absorb
				scoreboard players operation #TOTAL_ABSORB temp += @s 2100000_absorb
			}
			
			folder levels()
			{
			```
			for i in range(20):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2100000_level {i+1}
					scoreboard players set @s 2100000_rate {i+11}
					scoreboard players set @s 2100000_absorb {i*2+2}
				}}
				''')
			```
			}
		}
		
		folder 1001()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 2101001_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2101001_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score @s mp >= @s 2101001_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				
				# set sub buff values
				scoreboard players operation #MEDITATE_LEVEL temp = @s 2101001_level
				scoreboard players operation #MEDITATE_TIME temp = @s 2101001_time
				
				function ARG(_PATH)setting
				tag @s add caster
				execute as @a[tag=!dead,tag=!caster,limit=5,distance=..5,sort=nearest] at @s run function ARG(_PATH)take_effect
				tag @s remove caster
				
				# VVV since we got mp change below, no need to update the bar display VVV
				# execute if entity @s[tag=!hp_mp_varying] run function bar_display:update_bar/run
				
				execute positioned ~ ~-0.4 ~ run function skill:summon_font_effect/2101001effect
				playsound minecraft:skill.2101001 player @a ~ ~ ~ 1 1 0
				
				scoreboard players operation @s mp -= @s 2101001_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func take_effect()
			{
				function ARG(_PATH)setting
				execute if entity @s[tag=!hp_mp_varying] run function bar_display:update_bar/run
				
				execute positioned ~ ~1.7 ~ run function skill:summon_font_effect/2101001affected
			}
			
			func setting()
			{
				function skill:buff/matk/run
				
				# set sub buff values
				scoreboard players operation @s matk_buff_num = #MEDITATE_LEVEL temp
				
				# set this buff values
				scoreboard players operation @s matk_buff_time = #MEDITATE_TIME temp
				```
				print(f"attribute @s minecraft:generic.luck modifier add {buff_info_dict['matk']['uuid']} 3 0 add")
				```
			}
			
			folder levels()
			{
			```
			for i in range(20):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2101001_level {i+1}
					scoreboard players set @s 2101001_mp {i//10*10+10}
					scoreboard players set @s 2101001_time {i*10+10}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:58}
				give @s minecraft:carrot_on_a_stick{CustomModelData:58,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101001","bold":true,"italic":false}'}}
			}
		}
		
		folder 1002()
		{
			func check()
			{
				execute unless score @s teleport_time matches 1.. if score @s[tag=!delay_tp] 2101002_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2101002_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score @s mp >= @s 2101002_mp run function ARG(_PATH)move/up/check
			}
			
			folder move()
			{
				func check_position()
				{
					scoreboard players set #GLOBAL temp 0
					execute if block ~ ~ ~ #skill:passable if block ~ ~1 ~ #skill:passable unless block ~ ~-1 ~ #skill:passable run scoreboard players set #GLOBAL temp 1
				}
			
				func tp()
				{
					tp @s ~ ~ ~
					# remove motion
					tp @s @s
					
					# cast restriction
					scoreboard players operation @s next_hp_recover = @s hp_recover_time
					
					execute positioned ~ ~0.3 ~ run function skill:summon_font_effect/2101002effect
					playsound minecraft:skill.2101002 player @a ~ ~ ~ 1 1 0
					
					scoreboard players operation @s mp -= @s 2101002_mp
					function bar_display:change/mp/dropped
					
					scoreboard players set @s teleport_time 11
				}
				
				folder up()
				{
					func check()
					{
						execute if score @s just_jump matches 1.. positioned ~ ~5 ~ run function ARG(_PATH)run
						execute unless score @s just_jump matches 1.. if data entity @s {OnGround:1b} run function ARG(__PATH)down/check
					}
					
					func run()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~-1 ~ run function ARG(_PATH)run
					}
				}
				
				folder down()
				{
					func check()
					{
						execute if predicate main:sneaking positioned ~ ~-5 ~ run function ARG(_PATH)run
						execute unless predicate main:sneaking run function ARG(__PATH)horizon/tag
					}
					
					func run()
					{
						scoreboard players set #DOWN_CHECK_TIMES temp 5
						
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~-1 ~ run function ARG(_PATH)loop_down
					}
					
					func loop_down()
					{
						scoreboard players remove #DOWN_CHECK_TIMES temp 1
						
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 if score #DOWN_CHECK_TIMES temp matches 0 positioned ~ ~6 ~ run function ARG(_PATH)loop_up
						execute if score #GLOBAL temp matches 0 if score #DOWN_CHECK_TIMES temp matches 1.. positioned ~ ~-1 ~ run function ARG(_PATH)loop_down
					}
					
					func loop_up()
					{
						scoreboard players remove #DOWN_CHECK_TIMES temp 1
						
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 if score #DOWN_CHECK_TIMES temp matches -2.. positioned ~ ~1 ~ run function ARG(_PATH)loop_up
					}
				}
				
				folder horizon()
				{
					func tag()
					{
						tag @s add delay_tp
						scoreboard players operation #GLOBAL temp = @s number
						
						summon minecraft:area_effect_cloud ~ ~ ~ {Tags:["tp_check","new"],Duration:2}
						execute as @e[type=area_effect_cloud,tag=tp_check,tag=new] run function ARG(_PATH)set_point
					}
					
					func set_point()
					{
						scoreboard players operation @s number = #GLOBAL temp
						tag @s remove new
					}
					
					func run()
					{
						tag @s remove delay_tp
						
						tag @s add tp_player
						scoreboard players operation #GLOBAL temp = @s number
						execute as @e[type=area_effect_cloud,tag=tp_check] if score @s number = #GLOBAL temp run function ARG(_PATH)tp_player
						tag @s remove tp_player
					}
					
					func tp_player()
					{
						execute unless entity @s[distance=..0.00001] at @s facing entity @a[tag=tp_player,limit=1] eyes rotated ~ 0 positioned ^ ^ ^5 run function ARG(_PATH)first_check
						execute if entity @s[distance=..0.00001] run kill @s
					}
					
					func vertical_check()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 as @a[tag=tp_player,limit=1] run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~-2 ~ run function ARG(_PATH)final_check
					}
					
					func final_check()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 as @a[tag=tp_player,limit=1] run function ARG(__PATH)tp
					}
					
					func first_check()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 as @a[tag=tp_player,limit=1] run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~1 ~ run function ARG(_PATH)vertical_check
						execute if score #GLOBAL temp matches 0 positioned ^ ^ ^-1 run function ARG(_PATH)second_check
					}
					
					func second_check()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 as @a[tag=tp_player,limit=1] run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~1 ~ run function ARG(_PATH)vertical_check
					}
				}
			}
			
			folder levels()
			{
			```
			for i in range(20):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2101002_level {i+1}
					scoreboard players set @s 2101002_mp {50-i*2}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:59}
				give @s minecraft:carrot_on_a_stick{CustomModelData:59,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101002","bold":true,"italic":false}'}}
			}
		}
		
		folder 1003()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 2101003_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2101003_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score @s mp >= @s 2101003_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				
				scoreboard players operation #SLOW_TIME temp = @s 2101003_time
				
				execute positioned ~ ~0.3 ~ run function skill:summon_font_effect/2101003effect
				playsound minecraft:skill.2101003_use player @a ~ ~ ~ 1 1 0
				
				execute positioned ~ ~-2 ~ as @e[type=minecraft:zombified_piglin,tag=enemy,tag=!dead,distance=..5,sort=random,limit=6] unless score @s 2101003_time matches 1.. at @s run function ARG(_PATH)set_debuff
				
				scoreboard players operation @s mp -= @s 2101003_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func set_debuff()
			{
				execute positioned ~ ~0.8 ~ run function skill:summon_font_effect/2101003mob
				playsound minecraft:skill.2101003_hit player @a ~ ~ ~ 1 1 0
				
				scoreboard players operation @s 2101003_time = #SLOW_TIME temp
				execute store result entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"slow"}].Amount float -0.002 run scoreboard players get #SLOW_TIME temp
			}
			
			folder mob()
			{
				func countdown()
				{
					scoreboard players remove @s 2101003_time 1
					execute if score @s 2101003_time matches 0 run function ARG(_PATH)reset
				}
				
				func reset()
				{
					scoreboard players reset @s 2101003_time
					data modify entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"slow"}].Amount set value 0
				}
			}
			
			folder levels()
			{
			```
			for i in range(20):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2101003_level {i+1}
					scoreboard players set @s 2101003_mp {i//10*8+8}
					scoreboard players set @s 2101003_time {i*2+2}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:60}
				give @s minecraft:carrot_on_a_stick{CustomModelData:60,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101003","bold":true,"italic":false}'}}
			}
		}
		
		folder 1004() from bullet_skill_template(2101004)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s[nbt={OnGround:1b}] 2101004_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				scoreboard players operation #USED_WEAPON temp = @s weapon_type
				execute if score #USED_WEAPON temp matches 0 run tellraw @s {"translate":"warning.skill.no_weapon","color":"red"}
				
				execute if score #USED_WEAPON temp matches 1.. if score @s mp < @s 2101004_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score #USED_WEAPON temp matches 1.. if score @s mp >= @s 2101004_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				scoreboard players set #DISTANCE temp 9
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func effect_model_set()
			{
				execute positioned as @s run tp @s ~ ~ ~ ~ ~
				scoreboard players set @s max_life 20
				scoreboard players set @s base_model 210000
				tag @s remove new
			}
			
			func summon_bullet()
			{
				# max_distance should be set to (#DISTANCE temp - 3)
				execute rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^1 {Tags:["first_bullet","new_bullet","bullet"],CustomName:'{"text":"2","font":"space:default"}',ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1,tag:{appear_delay:12,max_distance:6,skill_id:4,bullet_model:2101,CustomModelData:1}}],Marker:1,Invisible:1,Invulnerable:1,NoGravity:1,Fire:32767s,CustomNameVisible:0}
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 2
				scoreboard players operation #SKILL_MASTERY temp = @s 2101004_mastery
				scoreboard players operation #SPELL_ATTACK temp = @s 2101004_attack
				function skill:damage_formula/spell
				
				scoreboard players operation @s mp -= @s 2101004_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 16
			}
				
			func cast_effect()
			{
				execute rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^1 {Tags:["model_animation","new"],Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,Fire:32767s,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:210000}}]}
				execute as @e[type=armor_stand,tag=new,limit=1] run function ARG(_PATH)effect_model_set
				playsound minecraft:skill.2101004 player @a ~ ~ ~ 1 1 0
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. positioned ^ ^ ^0.5 run function skill:summon_font_effect/2101004hit
				function skill:mob_effect/play_hurt_sound/run
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
			}
				
			folder levels()
			{
			```
			for i in range(30):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2101004_level {i+1}
					scoreboard players set @s 2101004_mp {i//15*14+14}
					scoreboard players set @s 2101004_attack {i*3+33}
					scoreboard players set @s 2101004_mastery {i//3*5+15}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:61}
				give @s minecraft:carrot_on_a_stick{CustomModelData:61,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101004","bold":true,"italic":false}'}}
			}
		}
		
		folder 1005() from bullet_skill_template(2101005)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s[nbt={OnGround:1b}] 2101005_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				scoreboard players operation #USED_WEAPON temp = @s weapon_type
				execute if score #USED_WEAPON temp matches 0 run tellraw @s {"translate":"warning.skill.no_weapon","color":"red"}
				
				execute if score #USED_WEAPON temp matches 1.. if score @s mp < @s 2101005_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score #USED_WEAPON temp matches 1.. if score @s mp >= @s 2101005_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				scoreboard players set #DISTANCE temp 9
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func summon_bullet()
			{
				# max_distance should be set to (#DISTANCE temp - 3)
				execute rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^1 {Tags:["first_bullet","new_bullet","bullet"],CustomName:'{"text":"2","font":"space:default"}',ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1,tag:{appear_delay:11,max_distance:6,skill_id:3,bullet_model:2100,CustomModelData:1}}],Marker:1,Invisible:1,Invulnerable:1,NoGravity:1,Fire:32767s,CustomNameVisible:0}
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 3
				scoreboard players operation #SKILL_MASTERY temp = @s 2101005_mastery
				scoreboard players operation #SPELL_ATTACK temp = @s 2101005_attack
				
				scoreboard players operation #POISON_RATE temp = @s 2101005_rate
				scoreboard players set #POISON_HP_RATE temp 70
				scoreboard players operation #POISON_HP_RATE temp -= @s 2101005_level
				scoreboard players operation #POISON_TIME temp = @s 2101005_poison
				
				function skill:damage_formula/spell
				
				scoreboard players operation @s mp -= @s 2101005_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 16
			}
			
			func cast_effect()
			{
				execute positioned ~ ~0.3 ~ run function skill:summon_font_effect/2101005effect
				playsound minecraft:skill.2101005_use player @a ~ ~ ~ 1 1 0
			}
			
			func add_debuff()
			{
				execute if entity @s[tag=!dead,tag=!poison_immune] run function ARG(_PATH)try_poison
			}
			
			func try_poison()
			{
				function main:rand_rate
				execute if score #RAND_RESULT temp < #POISON_RATE temp run function skill:mob_effect/debuff/poison
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. positioned ^ ^ ^0.5 run function skill:summon_font_effect/2101005hit
				playsound minecraft:skill.2101005_hit hostile @a ~ ~ ~ 1 1 0
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
			}
				
			folder levels()
			{
			```
			for i in range(30):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2101005_level {i+1}
					scoreboard players set @s 2101005_mp {i//15*10+10}
					scoreboard players set @s 2101005_attack {i*2+12}
					scoreboard players set @s 2101005_mastery {i//3*5+15}
					scoreboard players set @s 2101005_rate {i+31}
					scoreboard players set @s 2101005_poison {i//3*4+4}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:62}
				give @s minecraft:carrot_on_a_stick{CustomModelData:62,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101005","bold":true,"italic":false}'}}
			}
		}
	}
}
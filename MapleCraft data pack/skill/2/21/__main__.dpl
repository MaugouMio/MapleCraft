import ../../../math
import 211 as virtual
import 212 as virtual

namespace 21() as virtual
{
	folder 1() from 211();
	folder 2() from 212();
	
	func install()
	{
		scoreboard objectives add 2100000_level dummy
		scoreboard objectives add 2100000_rate dummy
		scoreboard objectives add 2100000_absorb dummy
		
		scoreboard objectives add 2101001_level dummy
		scoreboard objectives add 2101001_mp dummy
		scoreboard objectives add 2101001_time dummy
		
		scoreboard objectives add 2101002_level dummy
		scoreboard objectives add 2101002_mp dummy
		scoreboard objectives add teleport_time dummy
		
		scoreboard objectives add 2101003_level dummy
		scoreboard objectives add 2101003_mp dummy
		scoreboard objectives add 2101003_time dummy
		
		scoreboard objectives add 2101004_mp dummy
		scoreboard objectives add 2101004_mastery dummy
		scoreboard objectives add 2101004_attack dummy
		
		scoreboard objectives add 2101005_mp dummy
		scoreboard objectives add 2101005_mastery dummy
		scoreboard objectives add 2101005_attack dummy
		# poison time should be (sec)
		scoreboard objectives add 2101005_rate dummy
		scoreboard objectives add 2101005_poison dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder transfer()
		{
			func check()
			{
				scoreboard players operation #GLOBAL temp = @s level
				scoreboard players remove #GLOBAL temp 30
				scoreboard players operation #GLOBAL temp *= #3 const
				execute if score @s[tag=magician] job_level matches 1 if score @s level matches 30.. unless score @s skill_point > #GLOBAL temp at @s run function ARG(_PATH)run
			}
			
			func run()
			{
				tag @s add wizard_fp
				function stat:job/hp_mp/magician/second
					
				scoreboard players set @s 2100000_level 0
				scoreboard players set @s 2101001_level 0
				scoreboard players set @s 2101002_level 0
				scoreboard players set @s 2101003_level 0
				scoreboard players set @s 2101004_level 0
				scoreboard players set @s 2101005_level 0
			}
		}
		
		folder 0000()
		{
			func set_numeric()
			{
				scoreboard players set #TOTAL_ABSORB temp 0
				scoreboard players operation #ABSORB_RATE temp = @s 2100000_rate
				scoreboard players operation #ABSORB_AMOUNT temp = @s 2100000_absorb
			}
			
			func cast_effect()
			{
				scoreboard players operation @s mp += #TOTAL_ABSORB temp
				scoreboard players operation @s mp < @s mp_max
				function bar_display:change/mp/recovered
				
				scoreboard players operation #BIND_PLAYER number = @s number
				execute summon text_display run function skill:summon_font_effect/2100000effect
			}
				
			func try_absorb()
			{
				function random:rand_rate
				execute if score #RAND_RESULT number < #ABSORB_RATE temp run function ARG(_PATH)absorb
			}
			
			func absorb()
			{
				scoreboard players operation @s 2100000_absorb = #ABSORB_AMOUNT temp
				scoreboard players operation @s 2100000_absorb *= @s mp_max
				scoreboard players operation @s 2100000_absorb /= #100 const
				scoreboard players operation @s 2100000_absorb < @s mp
				
				scoreboard players operation @s mp -= @s 2100000_absorb
				scoreboard players operation #TOTAL_ABSORB temp += @s 2100000_absorb
			}
			
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2100000_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					execute store result score @s 2100000_rate run scoreboard players operation @s 2100000_absorb = @s 2100000_level
					scoreboard players add @s 2100000_rate 10
					scoreboard players operation @s 2100000_absorb *= #2 const
				}
				
				func set_description()
				{
					data modify block 250 0 0 front_text.messages[0] set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2100000_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2100000_rate
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2100000_absorb
					
					execute if score @s 2100000_level matches ..19 run function ARG(_PATH)get_next_level
					execute if score @s 2100000_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2100000","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 front_text.messages[0]
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2100000_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2100000_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2100000_rate
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2100000_absorb
					data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.2100000","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2100000_level 1
					execute store result score @s 2100000_rate run data get storage ui:main temp[1]
					execute store result score @s 2100000_absorb run data get storage ui:main temp[2]
					execute if score @s 2100000_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '["\\n",{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
				}
			}
		}
		
		folder 1001()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 2101001_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2101001_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score @s mp >= @s 2101001_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				
				# set sub buff values
				scoreboard players operation #MEDITATE_LEVEL temp = @s 2101001_level
				scoreboard players operation #MEDITATE_TIME temp = @s 2101001_time
				
				function ARG(_PATH)setting
				tag @s add caster
				execute as @a[tag=!dead,tag=!caster,limit=5,distance=..5,sort=nearest] at @s run function ARG(_PATH)take_effect
				tag @s remove caster
				
				# VVV since we got mp change below, no need to update the bar display VVV
				# tag @s add hp_mp_varying
				
				scoreboard players operation #BIND_PLAYER number = @s number
				execute summon text_display run function skill:summon_font_effect/2101001effect
				playsound minecraft:skill.2101001 player @a ~ ~ ~ 1 1 0
				
				scoreboard players operation @s mp -= @s 2101001_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func take_effect()
			{
				function ARG(_PATH)setting
				tag @s add hp_mp_varying
				
				scoreboard players operation #BIND_PLAYER number = @s number
				execute summon text_display run function skill:summon_font_effect/2101001affected
			}
			
			func setting()
			{
				function skill:buff/matk/run
				
				# set sub buff values
				scoreboard players operation @s matk_buff_num = #MEDITATE_LEVEL temp
				
				# set this buff values
				scoreboard players operation @s matk_buff_time = #MEDITATE_TIME temp
				```
				print(f"attribute @s minecraft:generic.luck modifier add {buff_info_dict['matk']['uuid']} 3 0 add")
				```
			}
			
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2101001_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 2101001_mp 10
					scoreboard players add @s[scores={2101001_level=11..}] 2101001_mp 10
					scoreboard players operation @s 2101001_time = @s 2101001_level
					scoreboard players operation @s 2101001_time *= #10 const
				}
				
				func set_description()
				{
					data modify block 250 0 0 front_text.messages[0] set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2101001_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2101001_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2101001_time
					
					execute if score @s 2101001_level matches ..19 run function ARG(_PATH)get_next_level
					execute if score @s 2101001_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2101001","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[0]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 front_text.messages[0]
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2101001_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2101001_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2101001_mp
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2101001_time
					data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.2101001","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2101001_level 1
					execute store result score @s 2101001_mp run data get storage ui:main temp[1]
					execute store result score @s 2101001_time run data get storage ui:main temp[2]
					execute if score @s 2101001_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '["\\n",{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:58}
				give @s minecraft:carrot_on_a_stick{CustomModelData:58,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101001","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
		
		folder 1002()
		{
			func check()
			{
				execute unless score @s teleport_time matches 1.. if score @s[tag=!delay_tp] 2101002_level matches 1.. run function ARG(_PATH)rope_check
			}
			
			func rope_check()
			{
				scoreboard players set #GLOBAL temp 0
				execute if block ~0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~0.3 ~-0.4 ~-0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~-0.3 #minecraft:climbable run scoreboard players set #GLOBAL temp 1
				execute if score #GLOBAL temp matches 0 unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2101002_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score @s mp >= @s 2101002_mp run function ARG(_PATH)move/up/check
			}
			
			folder move()
			{
				func check_position()
				{
					scoreboard players set #GLOBAL temp 0
					execute if block ~ ~ ~ #skill:passable if block ~ ~1 ~ #skill:passable unless block ~ ~-1 ~ #skill:passable run scoreboard players set #GLOBAL temp 1
				}
			
				func tp()
				{
					tp @s ~ ~ ~
					# remove motion
					tp @s @s
					
					# cast restriction
					scoreboard players operation @s next_hp_recover = @s hp_recover_time
					function ARG(_PATH)tp_effect
					
					scoreboard players operation @s mp -= @s 2101002_mp
					function bar_display:change/mp/dropped
					
					scoreboard players set @s teleport_time 11
				}
				
				func tp_effect()
				{
					scoreboard players operation #BIND_PLAYER number = @s number
					execute summon text_display run function skill:summon_font_effect/2101002effect
					playsound minecraft:skill.2101002 player @a ~ ~ ~ 1 1 0
				}
				
				folder up()
				{
					func check()
					{
						execute if score @s just_jump matches 1.. positioned ~ ~5 ~ run function ARG(_PATH)run
						execute unless score @s just_jump matches 1.. if data entity @s {OnGround:1b} run function ARG(__PATH)down/check
					}
					
					func run()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~-1 ~ run function ARG(_PATH)run
					}
				}
				
				folder down()
				{
					func check()
					{
						execute if predicate main:sneaking positioned ~ ~-5 ~ run function ARG(_PATH)run
						execute unless predicate main:sneaking run function ARG(__PATH)horizon/tag
					}
					
					func run()
					{
						scoreboard players set #DOWN_CHECK_TIMES temp 5
						
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~-1 ~ run function ARG(_PATH)loop_down
					}
					
					func loop_down()
					{
						scoreboard players remove #DOWN_CHECK_TIMES temp 1
						
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 if score #DOWN_CHECK_TIMES temp matches 0 positioned ~ ~6 ~ run function ARG(_PATH)loop_up
						execute if score #GLOBAL temp matches 0 if score #DOWN_CHECK_TIMES temp matches 1.. positioned ~ ~-1 ~ run function ARG(_PATH)loop_down
					}
					
					func loop_up()
					{
						scoreboard players remove #DOWN_CHECK_TIMES temp 1
						
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 if score #DOWN_CHECK_TIMES temp matches -2.. positioned ~ ~1 ~ run function ARG(_PATH)loop_up
					}
				}
				
				folder horizon()
				{
					func tag()
					{
						tag @s add delay_tp
						scoreboard players operation #GLOBAL temp = @s number
						execute summon minecraft:marker run function ARG(_PATH)set_point
					}
					
					func set_point()
					{
						scoreboard players operation @s number = #GLOBAL temp
						tag @s add tp_check
					}
					
					func run()
					{
						tag @s remove delay_tp
						
						tag @s add tp_player
						scoreboard players operation #GLOBAL temp = @s number
						execute as @e[type=marker,tag=tp_check] if score @s number = #GLOBAL temp run function ARG(_PATH)tp_player
						tag @s remove tp_player
					}
					
					func tp_player()
					{
						execute unless entity @s[distance=..0.00001] at @s facing entity @a[tag=tp_player,limit=1] eyes rotated ~ 0 positioned ^ ^ ^5 run function ARG(_PATH)first_check
						kill @s
					}
					
					func vertical_check()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 as @a[tag=tp_player,limit=1] run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~-2 ~ run function ARG(_PATH)final_check
					}
					
					func final_check()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 as @a[tag=tp_player,limit=1] run function ARG(__PATH)tp
					}
					
					func first_check()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 as @a[tag=tp_player,limit=1] run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~1 ~ run function ARG(_PATH)vertical_check
						execute if score #GLOBAL temp matches 0 positioned ^ ^ ^-1 run function ARG(_PATH)second_check
					}
					
					func second_check()
					{
						function ARG(__PATH)check_position
						execute if score #GLOBAL temp matches 1 as @a[tag=tp_player,limit=1] run function ARG(__PATH)tp
						execute if score #GLOBAL temp matches 0 positioned ~ ~1 ~ run function ARG(_PATH)vertical_check
					}
				}
			}
			
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2101002_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					# 提示快捷施放
					execute if score @s 2101002_level matches 1 run function ARG(_PATH)hint
					
					scoreboard players operation @s 2101002_mp = @s 2101002_level
					scoreboard players operation @s 2101002_mp *= #-3 const
					scoreboard players add @s 2101002_mp 63
					scoreboard players operation @s 2101002_mp += @s 2101002_level
					scoreboard players remove @s 2101002_mp 10
				}
				
				func hint()
				{
					tellraw @s ["\n\n",{"translate":"skill.description.2101002_hint"},"\n\n"]
					playsound minecraft:game.dlg_notice player @s
				}
				
				func set_description()
				{
					data modify block 250 0 0 front_text.messages[0] set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2101002_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2101002_mp
					
					execute if score @s 2101002_level matches ..19 run function ARG(_PATH)get_next_level
					execute if score @s 2101002_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2101002","with":[{"nbt":"temp[1]","storage":"ui:main"}]},{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 front_text.messages[0]
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2101002_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2101002_level
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2101002_mp
					data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[2]","storage":"ui:main"}]},{"translate":"skill.description.level.2101002","with":[{"nbt":"temp[3]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2101002_level 1
					execute store result score @s 2101002_mp run data get storage ui:main temp[1]
					execute if score @s 2101002_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '["\\n",{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:59}
				give @s minecraft:carrot_on_a_stick{CustomModelData:59,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101002","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
		
		folder 1003()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 2101003_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 2101003_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score @s mp >= @s 2101003_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				
				scoreboard players operation #SLOW_TIME temp = @s 2101003_time
				
				scoreboard players operation #BIND_PLAYER number = @s number
				execute summon text_display run function skill:summon_font_effect/2101003effect
				playsound minecraft:skill.2101003_use player @a ~ ~ ~ 1 1 0
				```
				print("summon marker ~ ~ ~ {UUID:%s}" %UUID_LIST["face_effect"])
				```
				scoreboard players set #FOUND_ENEMY temp 0
				execute positioned ~-4 ~-3 ~-4 as @e[type=interaction,tag=enemy,tag=!boss,dx=7,dy=5.5,dz=7] unless score @s 2101003_time matches 1.. run function ARG(_PATH)try_debuff
				kill 00000000-0000-0004-0000-000000000004
				
				scoreboard players operation @s mp -= @s 2101003_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 12
			}
			
			func try_debuff()
			{
				execute if score #FOUND_ENEMY temp matches ..5 run function ARG(_PATH)set_owner
			}
			
			func set_owner()
			{
				execute on vehicle at @s run function ARG(_PATH)set_debuff
				scoreboard players set @s 2101003_time 1
			}
			
			func set_debuff()
			{
				execute facing entity 00000000-0000-0004-0000-000000000004 feet rotated ~ 0 run function ARG(_PATH)forward_effect/start
				playsound minecraft:skill.2101003_hit player @a ~ ~ ~ 1 1 0
				
				scoreboard players operation @s 2101003_time = #SLOW_TIME temp
				scoreboard players operation @s 2101003_level = #SLOW_TIME temp

				# calculate new speed rate ( sqrt(new speed / original speed) )
				scoreboard players operation #SPEED_RATE temp = @s speed
				scoreboard players operation #SPEED_RATE temp -= #SLOW_TIME temp
				execute if score #SPEED_RATE temp < #10 const run scoreboard players set #SPEED_RATE temp 10
				scoreboard players operation #SPEED_RATE temp *= #1000000 const
				scoreboard players operation #SPEED_RATE temp /= @s speed
				function ARG(_PATH)sqrt_speed/run
				scoreboard players operation #SPEED_RATE temp -= #1000 const

				execute store result entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"slow"}].Amount float 0.001 run scoreboard players get #SPEED_RATE temp
				
				scoreboard players add #FOUND_ENEMY temp 1
			}
			folder sqrt_speed() from math().sqrt("#SPEED_RATE temp", "#SPEED_RATE temp");
	
			folder forward_effect()
			{
				func start()
				{
					execute on passengers if entity @s[type=interaction] run function ARG(_PATH)get_offset
					execute summon item_display run function ARG(_PATH)effect_model_set
				}

				func get_offset()
				{
					# 以 0.1 格為單位紀錄水平距離 * 256 (方便顏色計算，要算座標的再除回去)
					execute store result score #HORIZON_OFFSET temp run data get entity @s width 5
					scoreboard players operation #HORIZON_OFFSET temp *= #256 const

					execute store result score #VERTICAL_OFFSET temp run data get entity @s height 5
				}

				func effect_model_set()
				{
					data merge entity @s {Tags:["model_animation"],item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:210020}},item_display:"head",billboard:"center"}
					scoreboard players set @s max_life 10
					scoreboard players set @s base_model 210020

					tp @s ~ ~ ~ ~ 0
					function skill:get_horizon_offset_color
					execute store result entity @s item.tag.CustomPotionColor int 1 run scoreboard players operation #MODEL_COLOR temp += #VERTICAL_OFFSET temp
				}
			}
			
			folder mob()
			{
				func countdown()
				{
					scoreboard players remove @s 2101003_time 1
					execute if score @s 2101003_time matches 0 run function ARG(_PATH)reset
				}
				
				func reset()
				{
					scoreboard players reset @s 2101003_time
					data modify entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"slow"}].Amount set value 0
					execute on passengers run scoreboard players reset @s[type=interaction] 2101003_time
				}
			}
			
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2101003_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 2101003_mp 8
					scoreboard players add @s[scores={2101003_level=11..}] 2101003_mp 8
					scoreboard players operation @s 2101003_time = @s 2101003_level
					scoreboard players operation @s 2101003_time *= #2 const
				}
				
				func set_description()
				{
					data modify block 250 0 0 front_text.messages[0] set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2101003_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2101003_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2101003_time
					
					execute if score @s 2101003_level matches ..19 run function ARG(_PATH)get_next_level
					execute if score @s 2101003_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2101003","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 front_text.messages[0]
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2101003_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2101003_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2101003_mp
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2101003_time
					data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.2101003","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2101003_level 1
					execute store result score @s 2101003_mp run data get storage ui:main temp[1]
					execute store result score @s 2101003_time run data get storage ui:main temp[2]
					execute if score @s 2101003_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '["\\n",{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:60}
				give @s minecraft:carrot_on_a_stick{CustomModelData:60,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101003","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
		
		folder 1004() from bullet_skill_template(2101004)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s[nbt={OnGround:1b}] 2101004_level matches 1.. run function ARG(_PATH)rope_check
			}
			
			func rope_check()
			{
				scoreboard players set #GLOBAL temp 0
				execute if block ~0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~0.3 ~-0.4 ~-0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~-0.3 #minecraft:climbable run scoreboard players set #GLOBAL temp 1
				execute if score #GLOBAL temp matches 0 unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				scoreboard players operation #USED_WEAPON temp = @s weapon_type
				execute if score #USED_WEAPON temp matches 0 run tellraw @s {"translate":"warning.skill.no_weapon","font":"minecraft:uniform","color":"#efb7ab"}
				
				execute if score #USED_WEAPON temp matches 1.. if score @s mp < @s 2101004_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score #USED_WEAPON temp matches 1.. if score @s mp >= @s 2101004_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				scoreboard players set #DISTANCE temp 9
				scoreboard players set #SEARCH_HEIGHT temp 0
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func summon_bullet()
			{
				# max_distance should be set to (#DISTANCE temp - 1)
				execute rotated ~ 0 run summon minecraft:item_display ^ ^ ^1 {Tags:["first_bullet","new_bullet","bullet"],item:{id:"minecraft:potion",Count:1,tag:{appear_delay:12,max_distance:8,skill_id:4,bullet_model:2101,CustomModelData:1}},item_display:"head"}
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 2
				scoreboard players operation #SKILL_MASTERY temp = @s 2101004_mastery
				scoreboard players operation #SPELL_ATTACK temp = @s 2101004_attack
				function skill:damage_formula/spell
				
				scoreboard players operation @s mp -= @s 2101004_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 16
			}
				
			func cast_effect()
			{
				scoreboard players operation #BIND_PLAYER number = @s number
				# angle to PI
				execute store result storage main:main rotation.angle float 0.0001745329 run data get entity @s Rotation[0] 100
				execute rotated ~ 0 summon minecraft:item_display run function ARG(_PATH)effect_model_set
				playsound minecraft:skill.2101004 player @a ~ ~ ~ 1 1 0
			}
			
			func effect_model_set()
			{
				data merge entity @s {Tags:["model_animation"],item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:210000}},transformation:{scale:[0.625f,0.625f,0.625f],translation:[0.0f,1.6885f,0.0f]},item_display:"head"}
				scoreboard players set @s max_life 20
				scoreboard players set @s base_model 210000
				
				data modify entity @s transformation.left_rotation set from storage main:main rotation
				function skill:bind_player
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. summon item_display run function ARG(_PATH)hit_effect_model_set
				function skill:mob_effect/play_hurt_sound/run
			}
			
			func hit_effect_model_set()
			{
				data merge entity @s {Tags:["model_animation"],item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:210030}},item_display:"head",billboard:"center"}
				scoreboard players set @s max_life 9
				scoreboard players set @s base_model 210030

				tp @s ~ ~ ~ ~ 0
				function skill:get_horizon_offset_color
				execute store result entity @s item.tag.CustomPotionColor int 1 run scoreboard players operation #MODEL_COLOR temp += #OFFSET_Y temp
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
			}
				
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2101004_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 2101004_mp 14
					scoreboard players add @s[scores={2101004_level=16..}] 2101004_mp 14
					execute store result score @s 2101004_attack run scoreboard players operation @s 2101004_mastery = @s 2101004_level
					scoreboard players operation @s 2101004_attack *= #3 const
					scoreboard players add @s 2101004_attack 30
					scoreboard players remove @s 2101004_mastery 1
					scoreboard players operation @s 2101004_mastery /= #3 const
					scoreboard players operation @s 2101004_mastery *= #5 const
					scoreboard players add @s 2101004_mastery 15
				}
				
				func set_description()
				{
					data modify block 250 0 0 front_text.messages[0] set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2101004_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2101004_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2101004_attack
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2101004_mastery
					
					execute if score @s 2101004_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 2101004_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2101004","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"}]},{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 front_text.messages[0]
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2101004_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2101004_level
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2101004_mp
					execute store result storage ui:main temp[6] int 1 run scoreboard players get @s 2101004_attack
					execute store result storage ui:main temp[7] int 1 run scoreboard players get @s 2101004_mastery
					data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[4]","storage":"ui:main"}]},{"translate":"skill.description.level.2101004","with":[{"nbt":"temp[5]","storage":"ui:main"},{"nbt":"temp[6]","storage":"ui:main"},{"nbt":"temp[7]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2101004_level 1
					execute store result score @s 2101004_mp run data get storage ui:main temp[1]
					execute store result score @s 2101004_attack run data get storage ui:main temp[2]
					execute store result score @s 2101004_mastery run data get storage ui:main temp[3]
					execute if score @s 2101004_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '["\\n",{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:61}
				give @s minecraft:carrot_on_a_stick{CustomModelData:61,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101004","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
		
		folder 1005() from bullet_skill_template(2101005)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s[nbt={OnGround:1b}] 2101005_level matches 1.. run function ARG(_PATH)rope_check
			}
			
			func rope_check()
			{
				scoreboard players set #GLOBAL temp 0
				execute if block ~0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~0.3 ~-0.4 ~-0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~-0.3 #minecraft:climbable run scoreboard players set #GLOBAL temp 1
				execute if score #GLOBAL temp matches 0 unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				scoreboard players operation #USED_WEAPON temp = @s weapon_type
				execute if score #USED_WEAPON temp matches 0 run tellraw @s {"translate":"warning.skill.no_weapon","font":"minecraft:uniform","color":"#efb7ab"}
				
				execute if score #USED_WEAPON temp matches 1.. if score @s mp < @s 2101005_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score #USED_WEAPON temp matches 1.. if score @s mp >= @s 2101005_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				scoreboard players set #DISTANCE temp 9
				scoreboard players set #SEARCH_HEIGHT temp 0
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func summon_bullet()
			{
				# max_distance should be set to (#DISTANCE temp - 1)
				execute rotated ~ 0 run summon minecraft:item_display ^ ^ ^1 {Tags:["first_bullet","new_bullet","bullet"],item:{id:"minecraft:potion",Count:1,tag:{appear_delay:11,max_distance:8,skill_id:3,bullet_model:2100,CustomModelData:1}},item_display:"head"}
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 3
				scoreboard players operation #SKILL_MASTERY temp = @s 2101005_mastery
				scoreboard players operation #SPELL_ATTACK temp = @s 2101005_attack
				
				scoreboard players operation #POISON_RATE temp = @s 2101005_rate
				scoreboard players set #POISON_HP_RATE temp 70
				scoreboard players operation #POISON_HP_RATE temp -= @s 2101005_level
				scoreboard players operation #POISON_TIME temp = @s 2101005_poison
				
				function skill:damage_formula/spell
				
				scoreboard players operation @s mp -= @s 2101005_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 16
			}
			
			func cast_effect()
			{
				scoreboard players operation #BIND_PLAYER number = @s number
				execute summon text_display run function skill:summon_font_effect/2101005effect
				playsound minecraft:skill.2101005_use player @a ~ ~ ~ 1 1 0
			}
			
			func add_debuff()
			{
				execute if entity @s[tag=!dead,tag=!poison_immune,tag=!boss] run function ARG(_PATH)try_poison
			}
			
			func try_poison()
			{
				function random:rand_rate
				execute if score #RAND_RESULT number < #POISON_RATE temp run function skill:mob_effect/debuff/poison
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. summon item_display run function ARG(_PATH)effect_model_set
				playsound minecraft:skill.2101005_hit player @a ~ ~ ~ 1 1 0
			}
			
			func effect_model_set()
			{
				data merge entity @s {Tags:["model_animation"],item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:210039}},item_display:"head",billboard:"center"}
				scoreboard players set @s max_life 12
				scoreboard players set @s base_model 210039

				tp @s ~ ~ ~ ~ 0
				function skill:get_horizon_offset_color
				execute store result entity @s item.tag.CustomPotionColor int 1 run scoreboard players operation #MODEL_COLOR temp += #OFFSET_Y temp
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
			}
				
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2101005_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					```
					for i in range(1, 31):
						print(f"execute if score @s 2101005_level matches {i} run function ARG(_PATH){i}")
					```
				}
				
				func set_description()
				{
					data modify block 250 0 0 front_text.messages[0] set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2101005_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2101005_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2101005_attack
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2101005_mastery
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2101005_rate
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2101005_poison
					
					execute if score @s 2101005_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 2101005_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2101005","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"},{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]},{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 front_text.messages[0]
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2101005_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[6] int 1 run scoreboard players get @s 2101005_level
					execute store result storage ui:main temp[7] int 1 run scoreboard players get @s 2101005_mp
					execute store result storage ui:main temp[8] int 1 run scoreboard players get @s 2101005_attack
					execute store result storage ui:main temp[9] int 1 run scoreboard players get @s 2101005_mastery
					execute store result storage ui:main temp[10] int 1 run scoreboard players get @s 2101005_rate
					execute store result storage ui:main temp[11] int 1 run scoreboard players get @s 2101005_poison
					data modify block 250 0 0 front_text.messages[0] set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[6]","storage":"ui:main"}]},{"translate":"skill.description.level.2101005","with":[{"nbt":"temp[7]","storage":"ui:main"},{"nbt":"temp[8]","storage":"ui:main"},{"nbt":"temp[9]","storage":"ui:main"},{"nbt":"temp[10]","storage":"ui:main"},{"nbt":"temp[11]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2101005_level 1
					execute store result score @s 2101005_mp run data get storage ui:main temp[1]
					execute store result score @s 2101005_attack run data get storage ui:main temp[2]
					execute store result score @s 2101005_mastery run data get storage ui:main temp[3]
					execute store result score @s 2101005_rate run data get storage ui:main temp[4]
					execute store result score @s 2101005_poison run data get storage ui:main temp[5]
					execute if score @s 2101005_level matches 1.. run data modify block 250 0 0 front_text.messages[0] set value '["\\n",{"nbt":"front_text.messages[0]","block":"250 0 0","interpret":true}]'
				}
			```
			for i in range(30):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2101005_mp {i//15*10+10}
					scoreboard players set @s 2101005_attack {i*2+12}
					scoreboard players set @s 2101005_mastery {i//3*5+15}
					scoreboard players set @s 2101005_rate {i+31}
					scoreboard players set @s 2101005_poison {i//3*4+4}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:62}
				give @s minecraft:carrot_on_a_stick{CustomModelData:62,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2101005","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
	}
}
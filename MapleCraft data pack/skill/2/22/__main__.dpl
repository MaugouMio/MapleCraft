import 221 as virtual
import 222 as virtual

namespace 22() as virtual
{
	folder 1() from 221();
	folder 2() from 222();
	
	func install()
	{
		scoreboard objectives add 2201004_mp dummy
		scoreboard objectives add 2201004_mastery dummy
		scoreboard objectives add 2201004_attack dummy
		# freeze time should be (second * 20)
		# 2/4/6 seconds for different level intervals
		scoreboard objectives add 2201004_freeze dummy
		
		scoreboard objectives add 2201005_mp dummy
		scoreboard objectives add 2201005_mastery dummy
		scoreboard objectives add 2201005_attack dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder transfer()
		{
			func check()
			{
				scoreboard players operation #GLOBAL temp = @s level
				scoreboard players remove #GLOBAL temp 30
				scoreboard players operation #GLOBAL temp *= #3 const
				execute if score @s[tag=magician] job_level matches 1 if score @s level matches 30.. unless score @s skill_point > #GLOBAL temp at @s run function ARG(_PATH)run
			}
			
			func run()
			{
				tag @s add wizard_il
				function stat:job/hp_mp/magician/second
					
				scoreboard players set @s 2100000_level 0
				scoreboard players set @s 2101001_level 0
				scoreboard players set @s 2101002_level 0
				scoreboard players set @s 2101003_level 0
				scoreboard players set @s 2201004_level 0
				scoreboard players set @s 2201005_level 0
			}
		}
		
		# 2200000 shares the same functions with 2100000
		# 2201001 shares the same functions with 2101001
		# 2201002 shares the same functions with 2101002
		# 2201003 shares the same functions with 2101003
		
		folder 1004() from attack_skill_template(2201004)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s[nbt={OnGround:1b}] 2201004_level matches 1.. run function ARG(_PATH)rope_check
			}
			
			func rope_check()
			{
				scoreboard players set #GLOBAL temp 0
				execute if block ~0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~0.3 ~-0.4 ~-0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~-0.3 #minecraft:climbable run scoreboard players set #GLOBAL temp 1
				execute if score #GLOBAL temp matches 0 unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				function skill:check_weapon/single_hand/run
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 2201004_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 2201004_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				scoreboard players set #DISTANCE temp 9
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 4
				scoreboard players operation #SKILL_MASTERY temp = @s 2201004_mastery
				scoreboard players operation #SPELL_ATTACK temp = @s 2201004_attack
				scoreboard players operation #FREEZE_TIME temp = @s 2201004_freeze
				function skill:damage_formula/spell
				
				scoreboard players operation @s mp -= @s 2201004_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 16
			}
			
			func cast_effect()
			{
				execute rotated ~ 0 positioned ^ ^0.2 ^0.5 run function skill:summon_font_effect/2201004effect
				playsound minecraft:skill.2201004 player @a ~ ~ ~ 1 1 0
			}
			
			func add_debuff()
			{
				execute if entity @s[tag=!freeze_immune,tag=!boss] run function skill:mob_effect/debuff/freeze
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. positioned ^ ^ ^0.5 run function skill:summon_font_effect/2201004hit
				function skill:mob_effect/play_hurt_sound/run
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 8t append
			}
				
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2201004_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					```
					for i in range(1, 31):
						print(f"execute if score @s 2201004_level matches {i} run function ARG(_PATH){i}")
					```
				}
				
				func set_description()
				{
					data modify block 250 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2201004_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2201004_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2201004_attack
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2201004_mastery
					execute store result storage ui:main temp[8] int 1 run scoreboard players get @s 2201004_freeze
					
					execute if score @s 2201004_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 2201004_level matches 1.. run data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2201004","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"}]},{"nbt":"Text1","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2201004_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2201004_level
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2201004_mp
					execute store result storage ui:main temp[6] int 1 run scoreboard players get @s 2201004_attack
					execute store result storage ui:main temp[7] int 1 run scoreboard players get @s 2201004_mastery
					data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[4]","storage":"ui:main"}]},{"translate":"skill.description.level.2201004","with":[{"nbt":"temp[5]","storage":"ui:main"},{"nbt":"temp[6]","storage":"ui:main"},{"nbt":"temp[7]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2201004_level 1
					execute store result score @s 2201004_mp run data get storage ui:main temp[1]
					execute store result score @s 2201004_attack run data get storage ui:main temp[2]
					execute store result score @s 2201004_mastery run data get storage ui:main temp[3]
					execute store result score @s 2201004_freeze run data get storage ui:main temp[8]
					execute if score @s 2201004_level matches 1.. run data modify block 250 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"250 0 0","interpret":true}]'
				}
			```
			for i in range(30):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 2201004_mp {i//15*12+12}
					scoreboard players set @s 2201004_attack {i*3+13}
					scoreboard players set @s 2201004_mastery {i//3*5+15}
					scoreboard players set @s 2201004_freeze {i//10*40+40}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:75}
				give @s minecraft:carrot_on_a_stick{CustomModelData:75,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2201004","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
		
		folder 1005() from attack_skill_template(2201005)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s[nbt={OnGround:1b}] 2201005_level matches 1.. run function ARG(_PATH)rope_check
			}
			
			func rope_check()
			{
				scoreboard players set #GLOBAL temp 0
				execute if block ~0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~0.3 ~-0.4 ~-0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~0.3 #minecraft:climbable if block ~-0.3 ~-0.4 ~-0.3 #minecraft:climbable run scoreboard players set #GLOBAL temp 1
				execute if score #GLOBAL temp matches 0 unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				function skill:check_weapon/single_hand/run
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 2201005_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 2201005_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				data modify storage skill:main temp set value []
				scoreboard players set #FOUND_ENEMY temp 0
				scoreboard players reset #PAIR_NUMBER temp
				execute positioned ~-4 ~-3 ~-4 as @e[type=#skill:enemy,tag=enemy,tag=!dead,dx=7,dy=5.5,dz=7] run function ARG(_PATH)try_mark
			}
			
			func try_mark()
			{
				execute if score #FOUND_ENEMY temp matches ..5 run function ARG(_PATH)type_check
			}
			
			func type_check()
			{
				execute if entity @s[type=zombified_piglin] run function ARG(_PATH)mark
				execute if entity @s[type=magma_cube] unless score @s number = #PAIR_NUMBER temp run function ARG(_PATH)mark_owner
			}
			
			func mark()
			{
				tag @s add target
				
				scoreboard players operation @s hit_distance = #FOUND_ENEMY temp
				data modify storage skill:main temp append value 0
				execute store result storage skill:main temp[-1] int 1 run scoreboard players get @s hit_distance
				scoreboard players operation #PAIR_NUMBER temp = @s number
				
				scoreboard players add #FOUND_ENEMY temp 1
			}
			
			func mark_owner()
			{
				scoreboard players operation #PAIR_NUMBER temp = @s number
				execute as @e[type=zombified_piglin,tag=paired_mob] if score @s number = #PAIR_NUMBER temp run function ARG(_PATH)mark
			}
			
			func set_numeric()
			{
				data modify storage skill:main damage[0].attribute set value 5
				scoreboard players operation #SKILL_MASTERY temp = @s 2201005_mastery
				scoreboard players operation #SPELL_ATTACK temp = @s 2201005_attack
				function skill:damage_formula/spell
				
				scoreboard players operation @s mp -= @s 2201005_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 16
			}
			
			func cast_effect()
			{
				execute positioned ~ ~0.2 ~ run function skill:summon_font_effect/2201005effect
				playsound minecraft:skill.2201005 player @a ~ ~ ~ 1 1 0
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. run function skill:summon_font_effect/2201005hit
				function skill:mob_effect/play_hurt_sound/run
			}
				
			```
			# delay_range = max possible value of enemy amount
			append_scheduler(delay_range = 6, base_delay = 8, skill_path = "skill:2/2/0/1005/")
			```
				
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 2201005_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 2201005_mp 20
					scoreboard players add @s[scores={2201005_level=16..}] 2201005_mp 20
					execute store result score @s 2201005_attack run scoreboard players operation @s 2201005_mastery = @s 2201005_level
					scoreboard players operation @s 2201005_attack *= #2 const
					scoreboard players remove @s 2201005_mastery 1
					scoreboard players operation @s 2201005_mastery /= #3 const
					scoreboard players operation @s 2201005_mastery *= #5 const
					scoreboard players add @s 2201005_mastery 15
				}
				
				func set_description()
				{
					data modify block 250 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 2201005_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 2201005_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 2201005_attack
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 2201005_mastery
					
					execute if score @s 2201005_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 2201005_level matches 1.. run data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.2201005","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"}]},{"nbt":"Text1","block":"250 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 250 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 2201005_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 2201005_level
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 2201005_mp
					execute store result storage ui:main temp[6] int 1 run scoreboard players get @s 2201005_attack
					execute store result storage ui:main temp[7] int 1 run scoreboard players get @s 2201005_mastery
					data modify block 250 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[4]","storage":"ui:main"}]},{"translate":"skill.description.level.2201005","with":[{"nbt":"temp[5]","storage":"ui:main"},{"nbt":"temp[6]","storage":"ui:main"},{"nbt":"temp[7]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 2201005_level 1
					execute store result score @s 2201005_mp run data get storage ui:main temp[1]
					execute store result score @s 2201005_attack run data get storage ui:main temp[2]
					execute store result score @s 2201005_mastery run data get storage ui:main temp[3]
					execute if score @s 2201005_level matches 1.. run data modify block 250 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"250 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:76}
				give @s minecraft:carrot_on_a_stick{CustomModelData:76,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.2201005","font":"minecraft:uniform","bold":true,"italic":false}'}}
			}
		}
	}
}
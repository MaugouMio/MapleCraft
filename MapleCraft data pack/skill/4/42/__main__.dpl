import 421 as virtual
import 422 as virtual

namespace 42() as virtual
{
	folder 1() from 421();
	folder 2() from 422();
	
	func install()
	{
		scoreboard objectives add 4201002_level dummy
		scoreboard objectives add 4201002_mp dummy
		scoreboard objectives add 4201002_time dummy
		
		scoreboard objectives add 4201004_mp dummy
		scoreboard objectives add 4201004_attack dummy
		scoreboard objectives add 4201004_steal dummy
		
		scoreboard objectives add 4201005_mp dummy
		scoreboard objectives add 4201005_attack dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder 0000()
		{
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 4200000_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					```
					for i in range(1, 21):
						print(f"execute if score @s 4200000_level matches {i} run function ARG(_PATH){i}")
					```
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4200000_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s dagger_mastery
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s dagger_color
					
					execute if score @s 4200000_level matches ..19 run function ARG(_PATH)get_next_level
					execute if score @s 4200000_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4200000","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[0]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 4200000_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4200000_level
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s dagger_mastery
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[2]","storage":"ui:main"}]},{"translate":"skill.description.level.4200000","with":[{"nbt":"temp[3]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 4200000_level 1
					scoreboard players remove @s dagger_accuracy 1
					execute store result score @s dagger_mastery run data get storage ui:main temp[1]
					execute store result score @s dagger_color run data get storage ui:main temp[4]
					execute if score @s 4200000_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			```
			for i in range(18):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s dagger_mastery {i//2*5+15}
					scoreboard players set @s dagger_accuracy {i+1}
					scoreboard players set @s dagger_color {mastery_colors[i//2]}
				}}
				''')
			for i in range(18, 20):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s dagger_mastery {i//2*5+15}
					scoreboard players set @s dagger_accuracy {i+1}
				}}
				''')
			```
			}
		}
		
		# 4200001 shares the same functions with 4100002
		
		folder 1002() from booster_skill_template(4201002, dagger, q)
		{
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:148}
				give @s minecraft:carrot_on_a_stick{CustomModelData:148,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4201002","bold":true,"italic":false}'}}
			}
		}
		
		# 4201003 shares the same functions with 4101004
		
		folder 1004() from attack_skill_template(4201004)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s 4201004_level matches 1.. unless score @s invis_buff_time matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				function skill:check_weapon/check_empty
				execute if score #USED_WEAPON temp matches 1.. if score @s mp < @s 4201004_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"#efb7ab"}
				execute if score #USED_WEAPON temp matches 1.. if score @s mp >= @s 4201004_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				execute unless score #USED_WEAPON temp matches 24 run function ARG(_PATH)check_weapon_distance
				execute if score #USED_WEAPON temp matches 24 run scoreboard players set #DISTANCE temp 3
				
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func check_weapon_distance()
			{
				scoreboard players operation #DISTANCE temp = #USED_WEAPON temp
				# make single hand weapon %2 be 0
				scoreboard players add #DISTANCE temp 1
				scoreboard players operation #DISTANCE temp %= #2 const
				scoreboard players add #DISTANCE temp 4
			}
			
			func set_numeric()
			{
				# melee *RANDOM* swing damage formula and default attack speed is done by afterimage functions
				scoreboard players operation #STEAL_RATE temp = @s 4201004_steal
				
				scoreboard players operation @s mp -= @s 4201004_mp
				function bar_display:change/mp/dropped
				
				scoreboard players operation @s action_time = @s weapon_speed
				scoreboard players remove @s[scores={boost_buff_time=1..}] action_time 1
			}
			
			func cast_effect()
			{
				execute rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^0.7 {Tags:["model_animation","new"],Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,Fire:32767s,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:420000}}]}
				execute as @e[type=armor_stand,tag=new,limit=1] run function ARG(_PATH)effect_model_set
				
				execute unless score #USED_WEAPON temp matches 24 run function skill:afterimage/warrior_swing/check
				execute if score #USED_WEAPON temp matches 24 run function skill:afterimage/claw/stab
				playsound minecraft:skill.4201004 player @a ~ ~ ~ 1 1 0
				
				# hit_effect: 0 => None, 1 => mace, 2 => sword
				data modify storage skill:main damage_info.hit_effect set value 1
				execute unless score #USED_WEAPON temp matches ..4 unless score #USED_WEAPON temp matches 9..10 unless score #USED_WEAPON temp matches 16..21 unless score #USED_WEAPON temp matches 24 run data modify storage skill:main damage_info.hit_effect set value 2
			}
				
			func effect_model_set()
			{
				execute positioned as @s run tp @s ~ ~ ~ ~ ~
				scoreboard players set @s max_life 14
				scoreboard players set @s base_model 420000
				tag @s remove new
			}
			
			func effect()
			{
				function skill:mob_effect/play_hurt_sound/run
			}
			
			func add_debuff()
			{
				execute if entity @s[tag=!stealed,tag=!boss,tag=!dead] run function ARG(_PATH)try_steal
			}
			
			func try_steal()
			{
				function main:rand_rate
				execute if score #RAND_RESULT temp < #STEAL_RATE temp run function ARG(_PATH)steal
			}
			
			func steal()
			{
				data modify entity 00000000-0000-0001-0000-000000000001 DeathLootTable set from entity @s ArmorItems[3].tag.steal_loot
				loot spawn ~ ~1 ~ kill 00000000-0000-0001-0000-000000000001
				execute as @e[type=item,tag=!checked] at @s run function item:entity/check
				tag @s add stealed
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 7t append
			}
				
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 4201004_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 4201004_mp 12
					scoreboard players add @s[scores={4201004_level=16..}] 4201004_mp 12
					execute store result score @s 4201004_attack run scoreboard players operation @s 4201004_steal = @s 4201004_level
					scoreboard players operation @s 4201004_attack *= #2 const
					scoreboard players add @s 4201004_attack 40
					scoreboard players add @s 4201004_steal 10
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4201004_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 4201004_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4201004_steal
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 4201004_attack
					
					execute if score @s 4201004_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 4201004_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4201004","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 4201004_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 4201004_level
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 4201004_mp
					execute store result storage ui:main temp[6] int 1 run scoreboard players get @s 4201004_steal
					execute store result storage ui:main temp[7] int 1 run scoreboard players get @s 4201004_attack
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[4]","storage":"ui:main"}]},{"translate":"skill.description.level.4201004","with":[{"nbt":"temp[5]","storage":"ui:main"},{"nbt":"temp[6]","storage":"ui:main"},{"nbt":"temp[7]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 4201004_level 1
					execute store result score @s 4201004_mp run data get storage ui:main temp[1]
					execute store result score @s 4201004_steal run data get storage ui:main temp[2]
					execute store result score @s 4201004_attack run data get storage ui:main temp[3]
					execute if score @s 4201004_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:149}
				give @s minecraft:carrot_on_a_stick{CustomModelData:149,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4201004","bold":true,"italic":false}'}}
			}
		}
		
		folder 1005() from attack_skill_template(4201005)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s 4201005_level matches 1.. unless score @s invis_buff_time matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				function skill:check_weapon/dagger/run
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 4201005_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"#efb7ab"}
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 4201005_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				scoreboard players set #DISTANCE temp 4
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func set_numeric()
			{
				execute store result storage skill:main damage[0].base_percent int 1 run scoreboard players get @s 4201005_attack
				function skill:damage_formula/dagger/thief
				
				# 2 times
				data modify storage skill:main damage append from storage skill:main damage[0]
				# 4 times
				data modify storage skill:main damage append from storage skill:main damage[]
				# 5 and 6 times
				execute if score @s 4201005_level matches 11.. run data modify storage skill:main damage append from storage skill:main damage[0]
				execute if score @s 4201005_level matches 21.. run data modify storage skill:main damage append from storage skill:main damage[0]
				
				scoreboard players operation @s mp -= @s 4201005_mp
				function bar_display:change/mp/dropped
				
				scoreboard players operation @s action_time = @s weapon_speed
				scoreboard players remove @s[scores={boost_buff_time=1..}] action_time 1
				scoreboard players add @s action_time 18
			}
			
			func cast_effect()
			{
				execute positioned ~ ~0.3 ~ run function skill:summon_font_effect/4201005effect
				playsound minecraft:skill.4201005_use player @a ~ ~ ~ 1 1 0
				# borrow hit_effect variable to set custom hit effect
				scoreboard players set #RANGE_MAX temp 3
				function main:rand_range
				execute store result storage skill:main damage_info.hit_effect int 1 run scoreboard players add #RAND_RANGE_RESULT temp 3
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. positioned ^ ^0.3 ^0.5 run function ARG(_PATH)rand_hit_effect
				playsound minecraft:skill.4201005_hit hostile @a ~ ~ ~ 1 1 0
			}
			
			func rand_hit_effect()
			{
				execute if score #HIT_EFFECT temp matches 3 run function skill:summon_font_effect/4201005hit0
				execute if score #HIT_EFFECT temp matches 4 run function skill:summon_font_effect/4201005hit1
				execute if score #HIT_EFFECT temp matches 5 run function skill:summon_font_effect/4201005hit2
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 7t append
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 9t append
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 11t append
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 13t append
				
				execute if score @s 4201005_level matches 11.. run data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute if score @s 4201005_level matches 11.. run execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 15t append
				execute if score @s 4201005_level matches 21.. run data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute if score @s 4201005_level matches 21.. run execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 18t append
			}
				
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 4201005_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					```
					for i in range(1, 31):
						print(f"execute if score @s 4201005_level matches {i} run function ARG(_PATH){i}")
					```
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4201005_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 4201005_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4201005_attack
					
					execute if score @s 4201005_level matches ..29 run function ARG(_PATH)get_next_level
					scoreboard players set #GLOBAL temp 4
					execute if score @s 4201005_level matches 11.. run scoreboard players add #GLOBAL temp 1
					execute if score @s 4201005_level matches 21.. run scoreboard players add #GLOBAL temp 1
					execute if score @s 4201005_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4201005","with":[{"nbt":"temp[1]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 4201005_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 4201005_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 4201005_mp
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 4201005_attack
					scoreboard players set #GLOBAL temp 4
					execute if score @s 4201005_level matches 11.. run scoreboard players add #GLOBAL temp 1
					execute if score @s 4201005_level matches 21.. run scoreboard players add #GLOBAL temp 1
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.4201005","with":[{"nbt":"temp[4]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 4201005_level 1
					execute store result score @s 4201005_mp run data get storage ui:main temp[1]
					execute store result score @s 4201005_attack run data get storage ui:main temp[2]
					execute if score @s 4201005_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			```
			atk_list = [40,44,48,52,56,60,64,68,72,74,60,62,64,66,68,70,72,74,76,78,71,72,73,74,75,76,77,78,79,80]
			for i in range(30):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s 4201005_mp {i//10*3+i//20*6+15}
					scoreboard players set @s 4201005_attack {atk_list[i]}
				}}
				''')
			```
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:150}
				give @s minecraft:carrot_on_a_stick{CustomModelData:150,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4201005","bold":true,"italic":false}'}}
			}
		}
	}
}
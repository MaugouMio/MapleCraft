import 421 as virtual
import 422 as virtual

namespace 42() as virtual
{
	folder 1() from 421();
	folder 2() from 422();
	
	func install()
	{
		scoreboard objectives add 4201005_mp dummy
		scoreboard objectives add 4201005_attack dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder 1002()
		{
			
		}
		
		folder 1004()
		{
			
		}
		
		folder 1005() from attack_skill_template(4201005)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s 4201005_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				function skill:check_weapon/dagger/run
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 4201005_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 4201005_mp run function ARG(_PATH)run
			}
			
			func mark_target()
			{
				scoreboard players set #DISTANCE temp 2
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func cast_effect()
			{
				execute positioned ~ ~0.3 ~ run function skill:summon_font_effect/4201005effect
				playsound minecraft:skill.4201005_use player @a ~ ~ ~ 1 1 0
			}
			
			func set_numeric()
			{
				execute store result storage skill:main damage[0].base_percent int 1 run scoreboard players get @s 4201005_attack
				function skill:damage_formula/dagger/thief
				
				# 2 times
				data modify storage skill:main damage append from storage skill:main damage[0]
				# 4 times
				data modify storage skill:main damage append from storage skill:main damage[]
				# 5 and 6 times
				execute if score @s 4201005_level matches 11.. run data modify storage skill:main damage append from storage skill:main damage[0]
				execute if score @s 4201005_level matches 21.. run data modify storage skill:main damage append from storage skill:main damage[0]
				
				scoreboard players operation @s mp -= @s 4201005_mp
				function bar_display:change/mp/dropped
				
				scoreboard players set @s action_time 18
			}
			
			func effect()
			{
				execute if score @s display_number matches 1.. run function ARG(_PATH)rand_hit_effect
				playsound minecraft:skill.4201005_hit hostile @a ~ ~ ~ 1 1 0
			}
			
			func rand_hit_effect()
			{
				scoreboard players set #RANGE_MAX temp 3
				function main:rand_range
				execute if score #RAND_RANGE_RESULT temp matches 0 run function skill:summon_font_effect/4201005hit0
				execute if score #RAND_RANGE_RESULT temp matches 1 run function skill:summon_font_effect/4201005hit1
				execute if score #RAND_RANGE_RESULT temp matches 2 run function skill:summon_font_effect/4201005hit2
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value 0
				execute store result storage skill:main new_schedule[-1] int 1 run schedule function ARG(_PATH)hit_effect 7t append
				data modify storage skill:main new_schedule append value 0
				execute store result storage skill:main new_schedule[-1] int 1 run schedule function ARG(_PATH)hit_effect 9t append
				data modify storage skill:main new_schedule append value 0
				execute store result storage skill:main new_schedule[-1] int 1 run schedule function ARG(_PATH)hit_effect 11t append
				data modify storage skill:main new_schedule append value 0
				execute store result storage skill:main new_schedule[-1] int 1 run schedule function ARG(_PATH)hit_effect 13t append
				
				execute if score @s 4201005_level matches 11.. run data modify storage skill:main new_schedule append value 0
				execute if score @s 4201005_level matches 11.. run execute store result storage skill:main new_schedule[-1] int 1 run schedule function ARG(_PATH)hit_effect 15t append
				execute if score @s 4201005_level matches 21.. run data modify storage skill:main new_schedule append value 0
				execute if score @s 4201005_level matches 21.. run execute store result storage skill:main new_schedule[-1] int 1 run schedule function ARG(_PATH)hit_effect 18t append
			}
		}
	}
}
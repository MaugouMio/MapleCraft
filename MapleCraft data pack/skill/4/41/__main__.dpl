import 411 as virtual
import 412 as virtual

namespace 41() as virtual
{
	folder 1() from 411();
	folder 2() from 412();
	
	func install()
	{
		scoreboard objectives add 4100000_level dummy
		scoreboard objectives add 4100001_level dummy
		scoreboard objectives add 4100002_level dummy
		
		scoreboard objectives add 4101003_level dummy
		scoreboard objectives add 4101003_mp dummy
		scoreboard objectives add 4101003_time dummy
		
		scoreboard objectives add 4101004_level dummy
		scoreboard objectives add 4101004_mp dummy
		scoreboard objectives add 4101004_time dummy
		
		scoreboard objectives add 4101005_mp dummy
		scoreboard objectives add 4101005_attack dummy
		scoreboard objectives add 4101005_absorb dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder transfer()
		{
			func check()
			{
				scoreboard players operation #GLOBAL temp = @s level
				scoreboard players remove #GLOBAL temp 30
				scoreboard players operation #GLOBAL temp *= #3 const
				execute if score @s[tag=thief] job_level matches 1 if score @s level matches 30.. unless score @s skill_point > #GLOBAL temp at @s run function ARG(_PATH)run
			}
			
			func run()
			{
				tag @s add assassin
				function stat:job/hp_mp/archer_thief/second
				
				scoreboard players set @s 4100000_level 0
				scoreboard players set @s 4100001_level 0
				scoreboard players set @s 4100002_level 0
				scoreboard players set @s 4101003_level 0
				scoreboard players set @s 4101004_level 0
				scoreboard players set @s 4101005_level 0
			}
		}
		
		folder 0000()
		{
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 4100000_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					```
					for i in range(1, 21):
						print(f"execute if score @s 4100000_level matches {i} run function ARG(_PATH){i}")
					```
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4100000_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s claw_mastery
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s claw_color
					
					execute if score @s 4100000_level matches ..19 run function ARG(_PATH)get_next_level
					scoreboard players operation #GLOBAL temp = @s 4100000_level
					scoreboard players operation #GLOBAL temp *= #10 const
					execute if score @s 4100000_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4100000","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[0]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 4100000_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4100000_level
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s claw_mastery
					scoreboard players operation #GLOBAL temp = @s 4100000_level
					scoreboard players operation #GLOBAL temp *= #10 const
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[2]","storage":"ui:main"}]},{"translate":"skill.description.level.4100000","with":[{"nbt":"temp[3]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}}]}]'
					
					scoreboard players remove @s 4100000_level 1
					scoreboard players remove @s claw_accuracy 1
					execute store result score @s claw_mastery run data get storage ui:main temp[1]
					execute store result score @s claw_color run data get storage ui:main temp[4]
					execute if score @s 4100000_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			```
			for i in range(18):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s claw_mastery {i//2*5+15}
					scoreboard players set @s claw_accuracy {i+1}
					scoreboard players set @s claw_color {mastery_colors[i//2]}
				}}
				''')
			for i in range(18, 20):
				print(f'''
				func {i+1}()
				{{
					scoreboard players set @s claw_mastery {i//2*5+15}
					scoreboard players set @s claw_accuracy {i+1}
				}}
				''')
			```
			}
		}
		
		folder 0001()
		{
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 4100001_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					execute store result score @s crit_rate run scoreboard players operation @s crit_damage = @s 4100001_level
					scoreboard players add @s crit_rate 20
					scoreboard players operation @s crit_damage *= #3 const
					scoreboard players add @s crit_damage 10
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4100001_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s crit_rate
					execute store result storage ui:main temp[2] int 1 run scoreboard players add @s crit_damage 100
					
					execute if score @s 4100001_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 4100001_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4100001","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
					
					scoreboard players remove @s crit_damage 100
				}
				
				func get_next_level()
				{
					scoreboard players add @s 4100001_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 4100001_level
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s crit_rate
					execute store result storage ui:main temp[5] int 1 run scoreboard players add @s crit_damage 100
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.4100001","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 4100001_level 1
					execute store result score @s crit_rate run data get storage ui:main temp[1]
					execute store result score @s crit_damage run data get storage ui:main temp[2]
					execute if score @s 4100001_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			}
		}
		
		folder 0002()
		{
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 4100002_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					execute store result score @s hp_recovery run scoreboard players operation @s mp_recovery = @s 4100002_level
					scoreboard players operation @s hp_recovery *= #3 const
					scoreboard players add @s hp_recovery 10
					scoreboard players add @s mp_recovery 3
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					scoreboard players operation #GLOBAL temp = @s 4100002_level
					scoreboard players operation #GLOBAL hp_recovery = @s hp_recovery
					scoreboard players operation #GLOBAL mp_recovery = @s mp_recovery
					scoreboard players remove #GLOBAL hp_recovery 10
					scoreboard players remove #GLOBAL mp_recovery 3
					
					execute if score @s 4100002_level matches ..19 run function ARG(_PATH)get_next_level
					execute if score @s 4100002_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"score":{"objective":"temp","name":"#GLOBAL"}}]},{"translate":"skill.description.level.4100002","with":[{"score":{"objective":"hp_recovery","name":"#GLOBAL"}},{"score":{"objective":"mp_recovery","name":"#GLOBAL"}}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add #GLOBAL temp 1
					scoreboard players add #GLOBAL hp_recovery 3
					scoreboard players add #GLOBAL mp_recovery 1
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"score":{"objective":"temp","name":"#GLOBAL"}}]},{"translate":"skill.description.level.4100002","with":[{"score":{"objective":"hp_recovery","name":"#GLOBAL"}},{"score":{"objective":"mp_recovery","name":"#GLOBAL"}}]}]'
					
					scoreboard players remove #GLOBAL temp 1
					scoreboard players remove #GLOBAL hp_recovery 3
					scoreboard players remove #GLOBAL mp_recovery 1
					execute if score @s 4100002_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			}
		}
		
		folder 1003() from booster_skill_template(4101003, claw, p)
		{
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:134}
				give @s minecraft:carrot_on_a_stick{CustomModelData:134,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4101003","bold":true,"italic":false}'}}
			}
		}
		
		folder 1004()
		{
			func check()
			{
				execute unless score @s action_time matches 1.. if score @s 4101004_level matches 1.. run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				execute if score @s mp < @s 4101004_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"#efb7ab"}
				execute if score @s mp >= @s 4101004_mp run function ARG(_PATH)cast_effect
			}
			
			func cast_effect()
			{
				function skill:use_constraint
				scoreboard players set @s action_time 12
				
				scoreboard players operation #HASTE_LEVEL temp = @s 4101004_level
				scoreboard players operation #HASTE_TIME temp = @s 4101004_time
				
				function ARG(_PATH)setting
				tag @s add caster
				execute as @a[tag=!dead,tag=!caster,limit=5,distance=..5,sort=nearest] at @s run function ARG(_PATH)take_effect
				tag @s remove caster
				
				# VVV since we got mp change below, no need to update the bar display VVV
				# execute if entity @s[tag=!hp_mp_varying] run function bar_display:update_bar/run
				
				execute positioned ~ ~-0.4 ~ run function skill:summon_font_effect/4101004effect
				playsound minecraft:skill.4101004 player @a ~ ~ ~ 1 1 0
				
				scoreboard players operation @s mp -= @s 4101004_mp
				function bar_display:change/mp/dropped
			}
			
			func take_effect()
			{
				function ARG(_PATH)setting
				execute if entity @s[tag=!hp_mp_varying] run function bar_display:update_bar/run
				
				execute positioned ~ ~-0.4 ~ run function skill:summon_font_effect/4101004affected
			}
			
			func setting()
			{
				function skill:buff/haste/run
				
				# set sub buff values
				scoreboard players operation @s speed -= @s speed_buff_num
				scoreboard players operation @s speed_buff_num = #HASTE_LEVEL temp
				scoreboard players operation @s speed_buff_num *= #2 const
				scoreboard players operation @s speed += @s speed_buff_num
				function stat:speed_changed/check
				
				execute unless score @s action_time matches 1.. run effect clear @s minecraft:jump_boost
				scoreboard players operation @s jump_buff_num = #HASTE_LEVEL temp
				scoreboard players operation @s jump_buff_num /= #10 const
				execute if score @s jump_buff_num matches 0 run effect give @s minecraft:jump_boost 1000000 0 true
				execute if score @s jump_buff_num matches 1 run effect give @s minecraft:jump_boost 1000000 1 true
				execute if score @s jump_buff_num matches 2 run effect give @s minecraft:jump_boost 1000000 2 true
				
				# set this buff values
				scoreboard players operation @s haste_buff_time = #HASTE_TIME temp
				```
				print(f"attribute @s minecraft:generic.luck modifier add {buff_info_dict['haste']['uuid']} b 0 add")
				```
			}
			
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 4101004_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 4101004_mp 15
					scoreboard players add @s[scores={4101004_level=11..}] 4101004_mp 15
					scoreboard players operation @s 4101004_time = @s 4101004_level
					scoreboard players operation @s 4101004_time *= #10 const
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4101004_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 4101004_mp
					execute store result storage ui:main temp[2] int 2 run scoreboard players get @s 4101004_level
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 4101004_time
					
					execute if score @s 4101004_level matches ..19 run function ARG(_PATH)get_next_level
					scoreboard players set #GLOBAL temp 0
					execute if score @s 4101004_level matches 10.. run scoreboard players add #GLOBAL temp 10
					execute if score @s 4101004_level matches 20 run scoreboard players add #GLOBAL temp 10
					execute if score @s 4101004_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4101004","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}},{"nbt":"temp[3]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 4101004_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 4101004_level
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 4101004_mp
					execute store result storage ui:main temp[6] int 2 run scoreboard players get @s 4101004_level
					execute store result storage ui:main temp[7] int 1 run scoreboard players get @s 4101004_time
					scoreboard players set #GLOBAL temp 0
					execute if score @s 4101004_level matches 10.. run scoreboard players add #GLOBAL temp 10
					execute if score @s 4101004_level matches 20 run scoreboard players add #GLOBAL temp 10
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[4]","storage":"ui:main"}]},{"translate":"skill.description.level.4101004","with":[{"nbt":"temp[5]","storage":"ui:main"},{"nbt":"temp[6]","storage":"ui:main"},{"score":{"objective":"temp","name":"#GLOBAL"}},{"nbt":"temp[7]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 4101004_level 1
					execute store result score @s 4101004_mp run data get storage ui:main temp[1]
					execute store result score @s 4101004_time run data get storage ui:main temp[3]
					execute if score @s 4101004_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:135}
				give @s minecraft:carrot_on_a_stick{CustomModelData:135,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4101004","bold":true,"italic":false}'}}
			}
		}
		
		folder 1005() from bullet_skill_template(4101005)
		{
			func check()
			{
				execute unless score @s action_time matches 0.. if score @s 4101005_level matches 1.. unless score @s invis_buff_time matches 1.. unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
			}
			
			func other_check()
			{
				function skill:check_weapon/claw/run
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 4101005_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"#efb7ab"}
				execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 4101005_mp run function ARG(_PATH)star_check
			}
			
			func star_check()
			{
				scoreboard players set #LACK_BULLET temp 0
				execute unless score @s bullet_item matches ..20 run scoreboard players set #LACK_BULLET temp 1
				execute unless score @s bullet_amount matches 1.. run scoreboard players set #LACK_BULLET temp 1
				
				execute if score #LACK_BULLET temp matches 1 run function ARG(_PATH)lack_star
				execute if score #LACK_BULLET temp matches 0 run function ARG(_PATH)check_distance
			}
				
			func lack_star()
			{
				tellraw @s {"translate":"warning.skill.lack_throwing_star","color":"#efb7ab"}
				function skill:6/0/0/1001/mark_target
			}
			
			func check_distance()
			{
				function ARG(_PATH)mark_target
				execute unless score #FIRST_DISTANCE temp matches ..2 run function ARG(_PATH)run
				execute if score #FIRST_DISTANCE temp matches ..2 run function skill:6/0/0/1001/run
			}
			
			func mark_target()
			{
				scoreboard players operation #DISTANCE temp = @s keen_eyes_dist
				scoreboard players set #MAX_ENEMY temp 1
				function skill:enemy_search/search_front/search
			}
			
			func effect_model_set()
			{
				tp @s ~ ~ ~ ~180 ~
				scoreboard players set @s max_life 12
				scoreboard players set @s base_model 410000
				tag @s remove new
			}
			
			func summon_bullet()
			{
				execute rotated ~ 0 run summon minecraft:armor_stand ^ ^0.2 ^1 {Tags:["first_bullet","new_bullet","bullet"],CustomName:'{"text":"2","font":"space:default"}',ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1,tag:{appear_delay:7,max_distance:10,skill_id:9,bullet_model:4005,CustomModelData:1}}],Marker:1,Invisible:1,Invulnerable:1,NoGravity:1,Fire:32767s,CustomNameVisible:0}
				
				scoreboard players operation #GLOBAL bullet_model = @s bullet_model
				scoreboard players operation #BULLET_DISTANCE temp = #DISTANCE temp
				scoreboard players remove #BULLET_DISTANCE temp 3
				execute as @e[type=armor_stand,tag=new_bullet] run function ARG(_PATH)set_bullet
			}
			
			func set_bullet()
			{
				execute store result entity @s ArmorItems[3].tag.max_distance int 1 run scoreboard players get #BULLET_DISTANCE temp
				execute store result entity @s ArmorItems[3].tag.bullet_model int 1 run scoreboard players get #GLOBAL bullet_model
			}
			
			func set_numeric()
			{
				execute store result storage skill:main damage[0].base_percent int 1 run scoreboard players get @s 4101005_attack
				scoreboard players operation #DRAIN_RATE temp = @s 4101005_absorb
				scoreboard players operation #DRAIN_MAX temp = @s hp_max
				scoreboard players operation #DRAIN_MAX temp /= #2 const
				
				function skill:damage_formula/claw/normal
				
				scoreboard players remove @s bullet_amount 1
				scoreboard players operation @s mp -= @s 4101005_mp
				function bar_display:change/mp/dropped
				
				scoreboard players operation @s action_time = @s weapon_speed
				scoreboard players remove @s[scores={boost_buff_time=1..}] action_time 1
				scoreboard players add @s action_time 16
			}
			
			func add_debuff()
			{
				execute store result score #DRAIN_AMOUNT temp run data get entity @s ArmorItems[3].tag.damage_record[-1].number
				```
				raise Warning("TODO: if got shadow partner, add #DRAIN_AMOUNT temp by ArmorItems[3].tag.damage_record[-2].number")
				```
				scoreboard players operation #DRAIN_AMOUNT temp *= #DRAIN_RATE temp
				scoreboard players operation #DRAIN_AMOUNT temp /= #100 const
				
				scoreboard players operation #DRAIN_AMOUNT temp < @s hp_max
				scoreboard players operation #DRAIN_AMOUNT temp < #DRAIN_MAX temp
			}
			
			func cast_effect()
			{
				summon minecraft:armor_stand ~ ~ ~ {Tags:["model_animation","new"],Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,Fire:32767s,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:410000}}]}
				execute as @e[type=armor_stand,tag=new,limit=1] run function ARG(_PATH)effect_model_set
				
				function skill:afterimage/claw/swing
				playsound minecraft:skill.4101005 player @a ~ ~ ~ 1 1 0
				playsound minecraft:skill.attack.throw player @a ~ ~ ~ 1 1 0
				
				# hit_effect: 0 => None, 1 => mace, 2 => sword
				data modify storage skill:main damage_info.hit_effect set value 1
			}
			
			func effect()
			{
				function skill:mob_effect/play_hurt_sound/run
			}
			
			func append_scheduler()
			{
				data modify storage skill:main new_schedule append value {delay:0,time:0}
				execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
			}
				
			folder levels()
			{
				func add()
				{
					scoreboard players add @s 4101005_level 1
					function ARG(_PATH)check
					function skill:window/after_addition
				}
				
				func check()
				{
					scoreboard players set @s 4101005_mp 12
					scoreboard players add @s[scores={4101005_level=16..}] 4101005_mp 12
					execute store result score @s 4101005_attack run scoreboard players operation @s 4101005_absorb = @s 4101005_level
					scoreboard players operation @s 4101005_attack *= #2 const
					scoreboard players add @s 4101005_attack 100
					scoreboard players add @s 4101005_absorb 15
				}
				
				func set_description()
				{
					data modify block 0 0 0 Text1 set value '""'
					data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0, 0, 0]
					execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4101005_level
					execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 4101005_mp
					execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4101005_absorb
					execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 4101005_attack
					
					execute if score @s 4101005_level matches ..29 run function ARG(_PATH)get_next_level
					execute if score @s 4101005_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4101005","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					data modify storage ui:main skill_level_info append from block 0 0 0 Text1
				}
				
				func get_next_level()
				{
					scoreboard players add @s 4101005_level 1
					
					function ARG(_PATH)check
					execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 4101005_level
					execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 4101005_mp
					execute store result storage ui:main temp[6] int 1 run scoreboard players get @s 4101005_absorb
					execute store result storage ui:main temp[7] int 1 run scoreboard players get @s 4101005_attack
					data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[4]","storage":"ui:main"}]},{"translate":"skill.description.level.4101005","with":[{"nbt":"temp[5]","storage":"ui:main"},{"nbt":"temp[6]","storage":"ui:main"},{"nbt":"temp[7]","storage":"ui:main"}]}]'
					
					scoreboard players remove @s 4101005_level 1
					execute store result score @s 4101005_mp run data get storage ui:main temp[1]
					execute store result score @s 4101005_absorb run data get storage ui:main temp[2]
					execute store result score @s 4101005_attack run data get storage ui:main temp[3]
					execute if score @s 4101005_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
				}
			}
			
			func get()
			{
				clear @s minecraft:carrot_on_a_stick{CustomModelData:136}
				give @s minecraft:carrot_on_a_stick{CustomModelData:136,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4101005","bold":true,"italic":false}'}}
			}
		}
	}
}
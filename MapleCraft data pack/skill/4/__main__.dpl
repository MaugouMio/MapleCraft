import 41 as virtual
import 42 as virtual

namespace 4() as virtual
{
	folder 1() from 41();
	folder 2() from 42();
	
	func install()
	{
		scoreboard objectives add 4000000_level dummy
		scoreboard objectives add 4000001_level dummy
		scoreboard objectives add keen_eyes_dist dummy
		
		scoreboard objectives add 4001002_mp dummy
		scoreboard objectives add 4001002_time dummy
		scoreboard objectives add weaken_time dummy
		scoreboard objectives add weaken_from dummy
		
		scoreboard objectives add 4001003_level dummy
		scoreboard objectives add 4001003_mp dummy
		scoreboard objectives add 4001003_speed dummy
		scoreboard objectives add 4001003_time dummy
		scoreboard objectives add invis_buff_speed dummy
		
		scoreboard objectives add 4001334_mp dummy
		scoreboard objectives add 4001334_attack dummy
		
		scoreboard objectives add 4001344_mp dummy
		scoreboard objectives add 4001344_attack dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder 0()
		{
			folder transfer()
			{
				func check()
				{
					execute if score @s job_level matches 0 if score @s level matches 10.. if score @s DEX matches 25.. at @s run function ARG(_PATH)run
				}
				
				func run()
				{
					tag @s add thief
					function stat:job/hp_mp/archer_thief/first
					function stat:standard_pdd/set
					
					scoreboard players set @s 4000000_level 0
					scoreboard players set @s 4000001_level 0
					scoreboard players set @s 4001002_level 0
					scoreboard players set @s 4001003_level 0
					scoreboard players set @s 4001334_level 0
					scoreboard players set @s 4001344_level 0
				}
			}
			
			folder 0000()
			{
				folder levels()
				{
					func add()
					{
						scoreboard players add @s 4000000_level 1
						execute store result score @s base_accuracy run scoreboard players operation @s base_avoid = @s 4000000_level
						function skill:window/after_addition
					}
					
					func set_description()
					{
						data modify block 0 0 0 Text1 set value '""'
						
						execute if score @s 4000000_level matches ..19 run function ARG(_PATH)get_next_level
						scoreboard players operation #GLOBAL temp = @s 4000000_level
						execute if score @s 4000000_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"score":{"objective":"temp","name":"#GLOBAL"}}]},{"translate":"skill.description.level.4000000","with":[{"score":{"objective":"temp","name":"#GLOBAL"}}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
						data modify storage ui:main skill_level_info append from block 0 0 0 Text1
					}
					
					func get_next_level()
					{
						scoreboard players add @s 4000000_level 1
						
						scoreboard players operation #GLOBAL temp = @s 4000000_level
						data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"score":{"objective":"temp","name":"#GLOBAL"}}]},{"translate":"skill.description.level.4000000","with":[{"score":{"objective":"temp","name":"#GLOBAL"}}]}]'
						
						scoreboard players remove @s 4000000_level 1
						execute if score @s 4000000_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					}
				}
			}
			
			folder 0001()
			{
				folder levels()
				{
					func add()
					{
						scoreboard players add @s 4000001_level 1
						function ARG(_PATH)check
						function skill:window/after_addition
					}
					
					func check()
					{
						scoreboard players operation @s keen_eyes_dist = @s 4000001_level
						scoreboard players operation @s keen_eyes_dist /= #2 const
						scoreboard players add @s keen_eyes_dist 10
					}
					
					func set_description()
					{
						data modify block 0 0 0 Text1 set value '""'
						data modify storage ui:main temp set value [0, 0, 0, 0, 0]
						execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4000001_level
						execute store result storage ui:main temp[4] int 1 run scoreboard players operation #GLOBAL temp = @s keen_eyes_dist
						scoreboard players remove #GLOBAL temp 9
						execute store result storage ui:main temp[1] int 70 run scoreboard players get #GLOBAL temp
						
						execute if score @s 4000001_level matches ..7 run function ARG(_PATH)get_next_level
						execute if score @s 4000001_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4000001","with":[{"nbt":"temp[1]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
						data modify storage ui:main skill_level_info append from block 0 0 0 Text1
					}
					
					func get_next_level()
					{
						scoreboard players add @s 4000001_level 1
						
						function ARG(_PATH)check
						execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4000001_level
						scoreboard players operation #GLOBAL temp = @s keen_eyes_dist
						scoreboard players remove #GLOBAL temp 9
						execute store result storage ui:main temp[3] int 70 run scoreboard players get #GLOBAL temp
						data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[2]","storage":"ui:main"}]},{"translate":"skill.description.level.4000001","with":[{"nbt":"temp[3]","storage":"ui:main"}]}]'
						
						scoreboard players remove @s 4000001_level 1
						execute store result score @s keen_eyes_dist run data get storage ui:main temp[4]
						execute if score @s 4000001_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					}
				}
			}
			
			folder 1002() from attack_skill_template(4001002)
			{
				func check()
				{
					execute unless score @s action_time matches 0.. if score @s 4001002_level matches 1.. unless score @s invis_buff_time matches 1.. unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					function skill:check_weapon/check_empty
					execute if score #USED_WEAPON temp matches 1.. if score @s mp < @s 4001002_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
					execute if score #USED_WEAPON temp matches 1.. if score @s mp >= @s 4001002_mp run function ARG(_PATH)run
				}
				
				func mark_target()
				{
					execute unless score #USED_WEAPON temp matches 24 run function ARG(_PATH)check_weapon_distance
					execute if score #USED_WEAPON temp matches 24 run scoreboard players set #DISTANCE temp 3
					
					scoreboard players set #MAX_ENEMY temp 1
					function skill:enemy_search/search_front/search
				}
				
				func check_weapon_distance()
				{
					scoreboard players operation #DISTANCE temp = #USED_WEAPON temp
					# make single hand weapon %2 be 0
					scoreboard players add #DISTANCE temp 1
					scoreboard players operation #DISTANCE temp %= #2 const
					scoreboard players add #DISTANCE temp 4
				}
				
				func set_numeric()
				{
					# melee *RANDOM* swing damage formula and default attack speed is done by afterimage functions
					scoreboard players operation #DISORDER_TIME temp = @s 4001002_time
					scoreboard players operation #DISORDER_LEVEL temp = @s 4001002_level
					
					scoreboard players operation @s mp -= @s 4001002_mp
					function bar_display:change/mp/dropped
					
					scoreboard players operation @s action_time = @s weapon_speed
				}
				
				func cast_effect()
				{
					execute unless score #USED_WEAPON temp matches 24 run function skill:afterimage/warrior_swing/check
					execute if score #USED_WEAPON temp matches 24 run function skill:afterimage/claw/stab
					playsound minecraft:skill.4001002 player @a ~ ~ ~ 1 1 0
					
					# hit_effect: 0 => None, 1 => mace, 2 => sword
					data modify storage skill:main damage_info.hit_effect set value 1
					execute unless score #USED_WEAPON temp matches ..4 unless score #USED_WEAPON temp matches 9..10 unless score #USED_WEAPON temp matches 16..21 unless score #USED_WEAPON temp matches 24 run data modify storage skill:main damage_info.hit_effect set value 2
				}
				
				func effect()
				{
					summon minecraft:wandering_trader ~ -2 ~ {NoAI:1,Silent:1,DespawnDelay:1,Offers:{Recipes:[]},UUID:[I;0,2,0,2]}
					data modify entity @s AngryAt set value [I;0,2,0,2]
					
					execute if entity @s[tag=4001002_effect] positioned ~ ~0.8 ~ run function skill:summon_font_effect/4001002mob
					tag @s remove 4001002_effect
					
					function skill:mob_effect/play_hurt_sound/run
				}
				
				func add_debuff()
				{
					execute unless score @s weaken_from matches 1 run function ARG(_PATH)set_debuff
				}
				
				func set_debuff()
				{
					scoreboard players operation @s weaken_time = #DISORDER_TIME temp
					scoreboard players set @s weaken_from 1
					execute unless score @s stun_time matches 1.. run tag @s remove mob_effect
					tag @s add 4001002_effect
					
					# weapon attack drop
					execute store result score @s temp run data get entity @s Attributes[{Name:"minecraft:generic.attack_damage"}].Base 10
					scoreboard players remove @s temp 1
					execute store result entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"disorder"}].Amount float -0.1 run scoreboard players operation @s temp < #DISORDER_LEVEL temp
					execute if entity @s[tag=paired_mob] run function ARG(_PATH)debuff_pair
					
					# weapon defense drop
					scoreboard players operation @s wdef_drop = @s weapon_defense
					scoreboard players operation @s wdef_drop < #DISORDER_LEVEL temp
				}
			
				func debuff_pair()
				{
					scoreboard players operation #GLOBAL temp = @s temp
					scoreboard players operation #PAIR_NUMBER temp = @s number
					execute as @e[type=magma_cube,tag=enemy] if score @s number = #PAIR_NUMBER temp run function ARG(_PATH)pair_effect
				}
			
				func pair_effect()
				{
					execute store result entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"disorder"}].Amount float -0.1 run scoreboard players get #GLOBAL temp
					scoreboard players set @s weaken_from 1
				}
				
				folder mob()
				{
					func countdown()
					{
						scoreboard players remove @s weaken_time 1
						execute if score @s weaken_time matches 0 run function ARG(_PATH)reset
					}
					
					func reset()
					{
						scoreboard players reset @s weaken_time
						scoreboard players reset @s weaken_from
						execute unless score @s stun_time matches 1.. run tag @s remove mob_effect
						
						scoreboard players set @s wdef_drop 0
						data modify entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"disorder"}].Amount set value 0
						execute if entity @s[tag=paired_mob] run function ARG(_PATH)reset_pair
					}
					
					func reset_pair()
					{
						scoreboard players operation #PAIR_NUMBER temp = @s number
						execute as @e[type=magma_cube,tag=enemy] if score @s number = #PAIR_NUMBER temp run function ARG(_PATH)pair_reset_effect
					}
					
					func pair_reset_effect()
					{
						data modify entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"disorder"}].Amount set value 0
						scoreboard players reset @s weaken_from
					}
				}
				
				func append_scheduler()
				{
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 7t append
				}
				
				folder levels()
				{
					func add()
					{
						scoreboard players add @s 4001002_level 1
						function ARG(_PATH)check
						function skill:window/after_addition
					}
					
					func check()
					{
						```
						for i in range(1, 11):
							print(f"execute if score @s 4001002_level matches {i} run function ARG(_PATH){i}")
						```
					}
					
					func set_description()
					{
						data modify block 0 0 0 Text1 set value '""'
						data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
						execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4001002_level
						execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 4001002_mp
						execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4001002_time
						
						execute if score @s 4001002_level matches ..19 run function ARG(_PATH)get_next_level
						execute if score @s 4001002_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4001002","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[0]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
						data modify storage ui:main skill_level_info append from block 0 0 0 Text1
					}
					
					func get_next_level()
					{
						scoreboard players add @s 4001002_level 1
						
						function ARG(_PATH)check
						execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 4001002_level
						execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 4001002_mp
						execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 4001002_time
						data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.4001002","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[3]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
						
						scoreboard players remove @s 4001002_level 1
						execute store result score @s 4001002_mp run data get storage ui:main temp[1]
						execute store result score @s 4001002_time run data get storage ui:main temp[2]
						execute if score @s 4001002_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					}
				```
				mp_list = [5,5,5,5,5,6,6,6,7,7,7,8,8,8,9,9,9,10,10,10]
				time_list = [7,9,11,13,15,20,22,24,29,31,33,38,40,42,47,49,51,56,58,60]
				for i in range(20):
					print(f'''
					func {i+1}()
					{{
						scoreboard players set @s 4001002_mp {mp_list[i]}
						scoreboard players set @s 4001002_time {time_list[i]}
					}}
					''')
				```
				}
				
				func get()
				{
					clear @s minecraft:carrot_on_a_stick{CustomModelData:130}
					give @s minecraft:carrot_on_a_stick{CustomModelData:130,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4001002","bold":true,"italic":false}'}}
				}
			}
			
			folder 1003()
			{
				func check()
				{
					execute unless score @s action_time matches 1.. if score @s 4001003_level matches 1.. unless score @s invis_buff_time matches 1.. run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					execute if score @s mp < @s 4001003_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
					execute if score @s mp >= @s 4001003_mp run function ARG(_PATH)cast_effect
				}
				
				func cast_effect()
				{
					function skill:use_constraint
					
					function ARG(_PATH)invis/run
					
					# set this buff values
					scoreboard players operation @s invis_buff_time = @s 4001003_time
					scoreboard players operation @s invis_buff_speed = @s 4001003_speed
					
					scoreboard players operation @s speed += @s invis_buff_speed
					function stat:speed_changed/check
					
					effect give @s minecraft:invisibility 1000000 0 true
					effect give @s minecraft:resistance 1000000 4 true
					attribute @s minecraft:generic.knockback_resistance modifier add f7c5c3aa-4957-4321-8098-5ccef1ffdc8a invisible_knockback 1.0 add
					```
					print(f"attribute @s minecraft:generic.luck modifier add {simple_buff_info_dict['invis']['uuid']} d 0 add")
					```
					# VVV since we got mp change below, no need to update the bar display VVV
					# execute if entity @s[tag=!hp_mp_varying] run function bar_display:update_bar/run
					
					execute positioned ~ ~-0.2 ~ run function skill:summon_font_effect/4001003effect
					playsound minecraft:skill.4001003 player @a ~ ~ ~ 1 1 0
					
					scoreboard players operation @s mp -= @s 4001003_mp
					function bar_display:change/mp/dropped
					
					scoreboard players set @s action_time 4
				}
				
				func cancel()
				{
					function skill:use_constraint
					
					function ARG(_PATH)invis/reset/run
					
					execute positioned ~ ~-0.2 ~ run function skill:summon_font_effect/4001003effect
					playsound minecraft:skill.4001003 player @a ~ ~ ~ 1 1 0
					
					execute if entity @s[tag=!hp_mp_varying] run function bar_display:update_bar/run
					
					scoreboard players set @s action_time 4
				}
				
				folder invis() from simple_buff_template("invis");
				
				folder levels()
				{
					func add()
					{
						scoreboard players add @s 4001003_level 1
						function ARG(_PATH)check
						function skill:window/after_addition
					}
					
					func check()
					{
						```
						for i in range(1, 21):
							print(f"execute if score @s 4001003_level matches {i} run function ARG(_PATH){i}")
						```
					}
					
					func set_description()
					{
						data modify block 0 0 0 Text1 set value '""'
						data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
						execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4001003_level
						execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 4001003_mp
						execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4001003_time
						
						execute if score @s 4001003_level matches ..19 run function ARG(_PATH)get_next_level
						function ARG(_PATH)get_speed_description
						execute if score @s 4001003_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4001003","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"},{"nbt":"invis_level_text","storage":"ui:main","interpret":true}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
						data modify storage ui:main skill_level_info append from block 0 0 0 Text1
					}
					
					func get_next_level()
					{
						scoreboard players add @s 4001003_level 1
						
						function ARG(_PATH)check
						function ARG(_PATH)get_speed_description
						execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 4001003_level
						execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 4001003_mp
						execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 4001003_time
						data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.4001003","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"},{"nbt":"invis_level_text","storage":"ui:main","interpret":true}]}]'
						
						scoreboard players remove @s 4001003_level 1
						execute store result score @s 4001003_mp run data get storage ui:main temp[1]
						execute store result score @s 4001003_time run data get storage ui:main temp[2]
						execute if score @s 4001003_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					}
					
					func get_speed_description()
					{
						```
						for i in range(1, 21):
							print(f"""execute if score @s 4001003_level matches {i} run data modify storage ui:main invis_level_text set value '{{"translate":"skill.description.level.4001003.{i}"}}'""")
						```
					}
				```
				invisible_speed_list = [-30,-28,-26,-24,-22,-20,-18,-16,-14,-12,-10,-8,-7,-6,-5,-4,-3,-2,-1,0]
				for i in range(20):
					print(f'''
					func {i+1}()
					{{
						scoreboard players set @s 4001003_mp {24-i}
						scoreboard players set @s 4001003_speed {invisible_speed_list[i]}
						scoreboard players set @s 4001003_time {i*10+10}
					}}
					''')
				```
				}
				
				func get()
				{
					clear @s minecraft:carrot_on_a_stick{CustomModelData:131}
					give @s minecraft:carrot_on_a_stick{CustomModelData:131,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4001003","bold":true,"italic":false}'}}
				}
			}
			
			folder 1334() from attack_skill_template(4001334)
			{
				func check()
				{
					execute unless score @s action_time matches 0.. if score @s 4001334_level matches 1.. unless score @s invis_buff_time matches 1.. unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					function skill:check_weapon/dagger/run
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 4001334_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 4001334_mp run function ARG(_PATH)run
				}
				
				func mark_target()
				{
					scoreboard players set #DISTANCE temp 4
					scoreboard players set #MAX_ENEMY temp 1
					function skill:enemy_search/search_front/search
				}
				
				func set_numeric()
				{
					execute store result storage skill:main damage[0].base_percent int 1 run scoreboard players get @s 4001334_attack
					function skill:damage_formula/dagger/thief
					
					# 2 times
					data modify storage skill:main damage append from storage skill:main damage[0]
					
					scoreboard players operation @s mp -= @s 4001334_mp
					function bar_display:change/mp/dropped
					
					scoreboard players operation @s action_time = @s weapon_speed
					scoreboard players remove @s[scores={boost_buff_time=1..}] action_time 1
					scoreboard players operation @s action_time > #-1 const
					scoreboard players add @s action_time 13
				}
				
				func cast_effect()
				{
					function skill:afterimage/dagger/stab
					playsound minecraft:skill.4001334 player @a ~ ~ ~ 1 1 0
					# borrow hit_effect variable to set custom hit effect
					scoreboard players set #RANGE_MAX temp 6
					function main:rand_range
					execute store result storage skill:main damage_info.hit_effect int 1 run scoreboard players add #RAND_RANGE_RESULT temp 3
				}
				
				func effect()
				{
					execute if score @s display_number matches 1.. positioned ^ ^ ^0.5 run function ARG(_PATH)rand_hit_effect
					function skill:mob_effect/play_hurt_sound/run
				}
				
				func rand_hit_effect()
				{
					execute if score #HIT_EFFECT temp matches 3..5 run function skill:summon_font_effect/4001334hit0
					execute if score #HIT_EFFECT temp matches 6..8 run function skill:summon_font_effect/4001334hit1
				}
				
				func append_scheduler()
				{
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 7t append
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 9t append
				}
				
				folder levels()
				{
					func add()
					{
						scoreboard players add @s 4001334_level 1
						function ARG(_PATH)check
						function skill:window/after_addition
					}
					
					func check()
					{
						```
						for i in range(1, 21):
							print(f"execute if score @s 4001334_level matches {i} run function ARG(_PATH){i}")
						```
					}
					
					func set_description()
					{
						data modify block 0 0 0 Text1 set value '""'
						data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
						execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4001334_level
						execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 4001334_mp
						execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4001334_attack
						
						execute if score @s 4001334_level matches ..19 run function ARG(_PATH)get_next_level
						execute if score @s 4001334_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4001334","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
						data modify storage ui:main skill_level_info append from block 0 0 0 Text1
					}
					
					func get_next_level()
					{
						scoreboard players add @s 4001334_level 1
						
						function ARG(_PATH)check
						execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 4001334_level
						execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 4001334_mp
						execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 4001334_attack
						data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.4001334","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
						
						scoreboard players remove @s 4001334_level 1
						execute store result score @s 4001334_mp run data get storage ui:main temp[1]
						execute store result score @s 4001334_attack run data get storage ui:main temp[2]
						execute if score @s 4001334_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					}
				```
				mp_list = [6,6,6,6,7,7,7,8,8,9,9,10,10,11,11,12,12,13,13,14]
				atk_list = [65,68,71,74,78,81,84,88,91,95,98,102,105,109,112,116,119,123,126,130]
				for i in range(20):
					print(f'''
					func {i+1}()
					{{
						scoreboard players set @s 4001334_mp {mp_list[i]}
						scoreboard players set @s 4001334_attack {atk_list[i]}
					}}
					''')
				```
				}
				
				func get()
				{
					clear @s minecraft:carrot_on_a_stick{CustomModelData:132}
					give @s minecraft:carrot_on_a_stick{CustomModelData:132,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4001334","bold":true,"italic":false}'}}
				}
			}
			
			folder 1344() from bullet_skill_template(4001344)
			{
				func check()
				{
					execute unless score @s action_time matches 0.. if score @s 4001344_level matches 1.. unless score @s invis_buff_time matches 1.. unless block ~0.3 ~ ~0.3 #minecraft:climbable unless block ~0.3 ~ ~-0.3 #minecraft:climbable unless block ~-0.3 ~ ~0.3 #minecraft:climbable unless block ~-0.3 ~ ~-0.3 #minecraft:climbable run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					function skill:check_weapon/claw/run
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 4001344_mp run tellraw @s {"translate":"warning.skill.lack_mp","font":"minecraft:uniform","color":"#efb7ab"}
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 4001344_mp run function ARG(_PATH)star_check
				}
				
				func star_check()
				{
					scoreboard players set #LACK_BULLET temp 0
					execute unless score @s bullet_item matches ..20 run scoreboard players set #LACK_BULLET temp 1
					execute unless score @s bullet_amount matches 2.. run scoreboard players set #LACK_BULLET temp 1
					
					execute if score #LACK_BULLET temp matches 1 run function ARG(_PATH)lack_star
					execute if score #LACK_BULLET temp matches 0 run function ARG(_PATH)check_distance
				}
				
				func lack_star()
				{
					tellraw @s {"translate":"warning.skill.lack_throwing_star","font":"minecraft:uniform","color":"#efb7ab"}
					function skill:6/0/0/1001/mark_target
				}
				
				func check_distance()
				{
					function ARG(_PATH)mark_target
					execute unless score #FIRST_DISTANCE temp matches ..2 run function ARG(_PATH)run
					execute if score #FIRST_DISTANCE temp matches ..2 run function skill:6/0/0/1001/run
				}
				
				func mark_target()
				{
					scoreboard players operation #DISTANCE temp = @s keen_eyes_dist
					scoreboard players set #MAX_ENEMY temp 1
					function skill:enemy_search/search_front/search
				}
				
				func effect_model_set()
				{
					execute positioned as @s run tp @s ~ ~ ~ ~ ~
					scoreboard players set @s max_life 21
					scoreboard players set @s base_model 400000
					tag @s remove new
				}
				
				func summon_bullet()
				{
					execute rotated ~ 0 run summon minecraft:armor_stand ^ ^0.2 ^1 {Tags:["first_bullet","new_bullet","bullet"],CustomName:'{"text":"2","font":"space:default"}',ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1,tag:{appear_delay:7,max_distance:10,skill_id:2,bullet_model:4005,CustomModelData:1}}],Marker:1,Invisible:1,Invulnerable:1,NoGravity:1,Fire:32767s,CustomNameVisible:0}
					execute rotated ~ 0 run summon minecraft:armor_stand ^ ^-0.2 ^1 {Tags:["new_bullet","bullet"],CustomName:'{"text":"2","font":"space:default"}',ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1,tag:{appear_delay:9,max_distance:10,skill_id:2,bullet_model:4005,CustomModelData:1}}],Marker:1,Invisible:1,Invulnerable:1,NoGravity:1,Fire:32767s,CustomNameVisible:0}
					
					scoreboard players operation #GLOBAL bullet_model = @s bullet_model
					scoreboard players operation #BULLET_DISTANCE temp = #DISTANCE temp
					scoreboard players remove #BULLET_DISTANCE temp 3
					execute as @e[type=armor_stand,tag=new_bullet] run function ARG(_PATH)set_bullet
				}
				
				func set_bullet()
				{
					execute store result entity @s ArmorItems[3].tag.max_distance int 1 run scoreboard players get #BULLET_DISTANCE temp
					execute store result entity @s ArmorItems[3].tag.bullet_model int 1 run scoreboard players get #GLOBAL bullet_model
				}
				
				func set_numeric()
				{
					execute store result storage skill:main damage[0].base_percent int 1 run scoreboard players get @s 4001344_attack
					
					function skill:damage_formula/claw/lucky_seven
					
					data modify storage skill:main damage append from storage skill:main damage[0]
					
					scoreboard players remove @s bullet_amount 2
					scoreboard players operation @s mp -= @s 4001344_mp
					function bar_display:change/mp/dropped
					
					scoreboard players operation @s action_time = @s weapon_speed
					scoreboard players remove @s[scores={boost_buff_time=1..}] action_time 1
					scoreboard players add @s action_time 16
				}
				
				func cast_effect()
				{
					execute rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^1.7 {Tags:["model_animation","new"],Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,Fire:32767s,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:400000}}]}
					execute as @e[type=armor_stand,tag=new,limit=1] run function ARG(_PATH)effect_model_set
					function skill:afterimage/claw/swing
					playsound minecraft:skill.4001344 player @a ~ ~ ~ 1 1 0
				}
				
				func effect()
				{
					execute if score @s display_number matches 1.. positioned ^ ^ ^0.5 run function skill:summon_font_effect/4001344hit
					function skill:mob_effect/play_hurt_sound/run
				}
				
				func append_scheduler()
				{
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 3t append
				}
				
				folder levels()
				{
					func add()
					{
						scoreboard players add @s 4001344_level 1
						function ARG(_PATH)check
						function skill:window/after_addition
					}
					
					func check()
					{
						```
						for i in range(1, 21):
							print(f"execute if score @s 4001344_level matches {i} run function ARG(_PATH){i}")
						```
					}
					
					func set_description()
					{
						data modify block 0 0 0 Text1 set value '""'
						data modify storage ui:main temp set value [0, 0, 0, 0, 0, 0]
						execute store result storage ui:main temp[0] int 1 run scoreboard players get @s 4001344_level
						execute store result storage ui:main temp[1] int 1 run scoreboard players get @s 4001344_mp
						execute store result storage ui:main temp[2] int 1 run scoreboard players get @s 4001344_attack
						
						execute if score @s 4001344_level matches ..19 run function ARG(_PATH)get_next_level
						execute if score @s 4001344_level matches 1.. run data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.current","with":[{"nbt":"temp[0]","storage":"ui:main"}]},{"translate":"skill.description.level.4001344","with":[{"nbt":"temp[1]","storage":"ui:main"},{"nbt":"temp[2]","storage":"ui:main"}]},{"nbt":"Text1","block":"0 0 0","interpret":true}]'
						data modify storage ui:main skill_level_info append from block 0 0 0 Text1
					}
					
					func get_next_level()
					{
						scoreboard players add @s 4001344_level 1
						
						function ARG(_PATH)check
						execute store result storage ui:main temp[3] int 1 run scoreboard players get @s 4001344_level
						execute store result storage ui:main temp[4] int 1 run scoreboard players get @s 4001344_mp
						execute store result storage ui:main temp[5] int 1 run scoreboard players get @s 4001344_attack
						data modify block 0 0 0 Text1 set value '[{"translate":"skill.description.level.next","with":[{"nbt":"temp[3]","storage":"ui:main"}]},{"translate":"skill.description.level.4001344","with":[{"nbt":"temp[4]","storage":"ui:main"},{"nbt":"temp[5]","storage":"ui:main"}]}]'
						
						scoreboard players remove @s 4001344_level 1
						execute store result score @s 4001344_mp run data get storage ui:main temp[1]
						execute store result score @s 4001344_attack run data get storage ui:main temp[2]
						execute if score @s 4001344_level matches 1.. run data modify block 0 0 0 Text1 set value '["\\n",{"nbt":"Text1","block":"0 0 0","interpret":true}]'
					}
				```
				mp_list = [8,8,8,8,9,9,9,10,10,11,11,12,12,13,13,14,14,15,15,16]
				atk_list = [58,62,66,70,76,80,84,90,94,100,104,110,114,120,124,130,134,140,144,150]
				for i in range(20):
					print(f'''
					func {i+1}()
					{{
						scoreboard players set @s 4001344_mp {mp_list[i]}
						scoreboard players set @s 4001344_attack {atk_list[i]}
					}}
					''')
				```
				}
				
				func get()
				{
					clear @s minecraft:carrot_on_a_stick{CustomModelData:133}
					give @s minecraft:carrot_on_a_stick{CustomModelData:133,Unbreakable:1,HideFlags:127,display:{Name:'{"translate":"skill.4001344","bold":true,"italic":false}'}}
				}
			}
		}
	}
}
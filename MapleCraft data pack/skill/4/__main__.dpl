import 41 as virtual
import 42 as virtual

namespace 4() as virtual
{
	folder 1() from 41();
	folder 2() from 42();
	
	func install()
	{
		scoreboard objectives add 4001002_mp dummy
		scoreboard objectives add 4001002_time dummy
		scoreboard objectives add 4001002_effect dummy
		
		scoreboard objectives add 4001334_mp dummy
		scoreboard objectives add 4001334_attack dummy
		
		scoreboard objectives add 4001344_mp dummy
		scoreboard objectives add 4001344_attack dummy
		
		function ARG(_PATH)1/install
		function ARG(_PATH)2/install
	}
	
	folder 0()
	{
		folder 0()
		{
			folder 1002() from attack_skill_template(4001002)
			{
				func check()
				{
					execute unless score @s action_time matches 0.. if score @s 4001002_level matches 1.. run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					function skill:check_weapon/check_empty
					execute if score #USED_WEAPON temp matches 1.. if score @s mp < @s 4001002_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
					execute if score #USED_WEAPON temp matches 1.. if score @s mp >= @s 4001002_mp run function ARG(_PATH)run
				}
				
				func mark_target()
				{
					execute unless score #USED_WEAPON temp matches 24 run function ARG(_PATH)check_weapon_distance
					execute if score #USED_WEAPON temp matches 24 run scoreboard players set #DISTANCE temp 2
					
					scoreboard players set #MAX_ENEMY temp 1
					function skill:enemy_search/search_front/search
				}
				
				func check_weapon_distance()
				{
					scoreboard players operation #DISTANCE temp = #USED_WEAPON temp
					# make single hand weapon %2 be 0
					scoreboard players add #DISTANCE temp 1
					scoreboard players operation #DISTANCE temp %= #2 const
					scoreboard players add #DISTANCE temp 2
				}
				
				func set_numeric()
				{
					# melee *RANDOM* swing damage formula and default attack speed is done by afterimage functions
					scoreboard players operation #DISORDER_TIME temp = @s 4001002_time
					scoreboard players operation #DISORDER_LEVEL temp = @s 4001002_level
					
					scoreboard players operation @s mp -= @s 4001002_mp
					function bar_display:change/mp/dropped
					
					execute store result score @s action_time run data get entity @s Inventory[{Slot:-106b}].tag.action_time
				}
				
				func cast_effect()
				{
					execute unless score #USED_WEAPON temp matches 24 run function skill:afterimage/warrior_swing/check
					execute if score #USED_WEAPON temp matches 24 run function skill:afterimage/claw/stab
					playsound minecraft:skill.4001002 player @a ~ ~ ~ 1 1 0
					
					# hit_effect: 0 => None, 1 => mace, 2 => sword
					data modify storage skill:main damage_info.hit_effect set value 1
					execute unless score #USED_WEAPON temp matches ..4 unless score #USED_WEAPON temp matches 9..10 unless score #USED_WEAPON temp matches 16..21 unless score #USED_WEAPON temp matches 24 run data modify storage skill:main damage_info.hit_effect set value 2
				}
				
				func effect()
				{
					summon minecraft:wandering_trader ~ -2 ~ {NoAI:1,Silent:1,DespawnDelay:1,Offers:{Recipes:[]},UUID:[I;0,1,0,1]}
					data modify entity @s AngryAt set value [I;0,1,0,1]
					
					execute if score @s 4001002_effect matches 1 positioned ~ ~0.8 ~ run function skill:summon_font_effect/4001002mob
					scoreboard players reset @s 4001002_effect
					
					function skill:mob_effect/play_hurt_sound/run
				}
				
				func add_debuff()
				{
					execute unless score @s 4001002_time matches 1.. run function ARG(_PATH)set_debuff
				}
				
				func set_debuff()
				{
					scoreboard players operation @s 4001002_time = #DISORDER_TIME temp
					scoreboard players set @s 4001002_effect 1
					
					# weapon attack drop
					execute store result score @s temp run data get entity @s Attributes[{Name:"minecraft:generic.attack_damage"}].Base 10
					scoreboard players remove @s temp 1
					execute store result entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"disorder"}].Amount float -0.1 run scoreboard players operation @s temp < #DISORDER_LEVEL temp
					
					# weapon defense drop
					scoreboard players operation @s wdef_drop = @s weapon_defense
					scoreboard players operation @s wdef_drop < #DISORDER_LEVEL temp
				}
				
				folder mob()
				{
					func countdown()
					{
						scoreboard players remove @s 4001002_time 1
						execute if score @s 4001002_time matches 0 run function ARG(_PATH)reset
					}
					
					func reset()
					{
						scoreboard players reset @s 4001002_time
						scoreboard players set @s wdef_drop 0
						data modify entity @s ArmorItems[3].tag.AttributeModifiers[{Name:"disorder"}].Amount set value 0
					}
				}
				
				func append_scheduler()
				{
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 7t append
				}
			}
			
			folder 1003()
			{
				func check()
				{
					
				}
			}
			
			folder 1334() from attack_skill_template(4001334)
			{
				func check()
				{
					execute unless score @s action_time matches 0.. if score @s 4001334_level matches 1.. run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					function skill:check_weapon/dagger/run
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 4001334_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 4001334_mp run function ARG(_PATH)run
				}
				
				func mark_target()
				{
					scoreboard players set #DISTANCE temp 2
					scoreboard players set #MAX_ENEMY temp 1
					function skill:enemy_search/search_front/search
				}
				
				func set_numeric()
				{
					execute store result storage skill:main damage[0].base_percent int 1 run scoreboard players get @s 4001334_attack
					function skill:damage_formula/dagger/thief
					
					# 2 times
					data modify storage skill:main damage append from storage skill:main damage[0]
					
					scoreboard players operation @s mp -= @s 4001334_mp
					function bar_display:change/mp/dropped
					
					execute store result score @s action_time run data get entity @s Inventory[{Slot:-106b}].tag.action_time
					# booster action_time setting belongs here
					scoreboard players operation @s action_time > #-1 const
					scoreboard players add @s action_time 13
				}
				
				func cast_effect()
				{
					function skill:afterimage/dagger/stab
					playsound minecraft:skill.4001334 player @a ~ ~ ~ 1 1 0
					# borrow hit_effect variable to set custom hit effect
					scoreboard players set #RANGE_MAX temp 6
					function main:rand_range
					execute store result storage skill:main damage_info.hit_effect int 1 run scoreboard players add #RAND_RANGE_RESULT temp 3
				}
				
				func effect()
				{
					execute if score @s display_number matches 1.. run function ARG(_PATH)rand_hit_effect
					function skill:mob_effect/play_hurt_sound/run
				}
				
				func rand_hit_effect()
				{
					execute if score #HIT_EFFECT temp matches 3..5 run function skill:summon_font_effect/4001334hit0
					execute if score #HIT_EFFECT temp matches 6..8 run function skill:summon_font_effect/4001334hit1
				}
				
				func append_scheduler()
				{
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 7t append
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 9t append
				}
			}
			
			folder 1344() from bullet_skill_template(4001344)
			{
				func check()
				{
					execute unless score @s action_time matches 0.. if score @s 4001344_level matches 1.. run function ARG(_PATH)other_check
				}
				
				func other_check()
				{
					function skill:check_weapon/claw/run
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp < @s 4001344_mp run tellraw @s {"translate":"warning.skill.lack_mp","color":"red"}
					execute if score #ACCEPTED_WEAPON temp matches 1.. if score @s mp >= @s 4001344_mp run function ARG(_PATH)run
				}
				
				func mark_target()
				{
					```
					raise Warning("TODO: set mark_target distance according to keen eyes")
					```
					# scoreboard players operation #DISTANCE temp = @s keen_eyes_dist
					scoreboard players set #DISTANCE temp 14
					scoreboard players set #MAX_ENEMY temp 1
					function skill:enemy_search/search_front/search
				}
				
				func effect_model_set()
				{
					execute positioned as @s run tp @s ~ ~ ~ ~ ~
					scoreboard players set @s max_life 21
					scoreboard players set @s base_model 400000
					tag @s remove new
				}
				
				func summon_bullet()
				{
					# max_distance should be set to (#DISTANCE temp / 2 + 2)
					execute rotated ~ 0 run summon minecraft:armor_stand ^ ^0.2 ^1 {Tags:["first_bullet","new_bullet","bullet"],CustomName:'{"text":"2","font":"space:default"}',ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1,tag:{appear_delay:7,max_distance:9,skill_id:2,bullet_model:4005,CustomModelData:1}}],Marker:1,Invisible:1,Invulnerable:1,NoGravity:1,Fire:32767s,CustomNameVisible:0}
					execute rotated ~ 0 run summon minecraft:armor_stand ^ ^-0.2 ^1 {Tags:["new_bullet","bullet"],CustomName:'{"text":"2","font":"space:default"}',ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1,tag:{appear_delay:9,max_distance:9,skill_id:2,bullet_model:4005,CustomModelData:1}}],Marker:1,Invisible:1,Invulnerable:1,NoGravity:1,Fire:32767s,CustomNameVisible:0}
					```
					raise Warning("TODO: set throwing star model and max_distance")
					```
					# scoreboard players operation #GLOBAL temp = @s using_bullet
					# execute as @e[tag=new_bullet] store result entity @s ArmorItems[3].tag.bullet_model int 1 run scoreboard players get #GLOBAL temp
				}
				
				func set_numeric()
				{
					execute store result storage skill:main damage[0].base_percent int 1 run scoreboard players get @s 4001344_attack
					```
					raise Warning("TODO: set throwing star damage")
					```
					scoreboard players set #BULLET_DAMAGE temp 25
					function skill:damage_formula/claw/lucky_seven
					
					data modify storage skill:main damage append from storage skill:main damage[0]
					
					scoreboard players operation @s mp -= @s 4001344_mp
					function bar_display:change/mp/dropped
					
					execute store result score @s action_time run data get entity @s Inventory[{Slot:-106b}].tag.action_time
					# booster action_time setting belongs here
					scoreboard players add @s action_time 16
				}
				
				func cast_effect()
				{
					execute rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^1.7 {Tags:["model_animation","new"],Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,Fire:32767s,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:400000}}]}
					execute as @e[type=armor_stand,tag=new,limit=1] run function ARG(_PATH)effect_model_set
					function skill:afterimage/claw/swing
					playsound minecraft:skill.4001344 player @a ~ ~ ~ 1 1 0
				}
				
				func effect()
				{
					execute if score @s display_number matches 1.. run function skill:summon_font_effect/4001344hit
					function skill:mob_effect/play_hurt_sound/run
				}
				
				func append_scheduler()
				{
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 1t append
					data modify storage skill:main new_schedule append value {delay:0,time:0}
					execute store result storage skill:main new_schedule[-1].time int 1 run schedule function ARG(_PATH)hit_effect 3t append
				}
			}
		}
	}
}
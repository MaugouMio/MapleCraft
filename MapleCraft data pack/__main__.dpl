import random
import loop

description = "MapleCraft data pack"

```
constants = [-7, -1, 0, 1, 2, 3, 4, 5, 6, 8, 9, 10, 11, 13, 15, 17, 20, 25, 28, 30, 32, 34, 36, 40, 41, 44, 45, 46, 48, 50, 56, 66, 92, 100, 150, 700, 1000, 2000, 10000, 30000, 99999, 100000]
```

import math
import number_display
import bar_display
import skill
import stat
import item

namespace main()
{
	func install()
	{
		gamerule commandBlockOutput false
		gamerule disableRaids true
		gamerule doDaylightCycle false
		gamerule doEntityDrops false
		gamerule doFireTick false
		gamerule doInsomnia false
		gamerule doMobLoot false
		gamerule doMobSpawning false
		gamerule doPatrolSpawning false
		gamerule doTileDrops false
		gamerule doTraderSpawning false
		gamerule doWeatherCycle false
		gamerule drowningDamage false
		gamerule fallDamage false
		gamerule fireDamage false
		gamerule forgiveDeadPlayers true
		gamerule keepInventory true
		gamerule logAdminCommands false
		gamerule mobGriefing false
		gamerule naturalRegeneration false
		# gamerule sendCommandFeedback false
		gamerule showDeathMessages false
		gamerule spawnRadius 0
		gamerule universalAnger false
		
		scoreboard objectives add const dummy
		scoreboard objectives add number dummy
		scoreboard objectives add type dummy
		scoreboard objectives add temp dummy
		scoreboard objectives add life dummy
		scoreboard objectives add max_life dummy
		scoreboard objectives add meso dummy
		scoreboard objectives add jump minecraft.custom:minecraft.jump
		scoreboard objectives add just_jump dummy
		scoreboard objectives add command trigger
		scoreboard objectives add login minecraft.custom:minecraft.leave_game
		
		team add enemy
		team modify enemy collisionRule never
		team add player
		team modify player collisionRule never
		```
		constants = set(constants)
		for i in constants:
			print("scoreboard players set #{0} const {0}".format(i))
		```
		scoreboard players set #GLOBAL number 0
		
		setworldspawn 0 4 0
		fill -1 0 -1 1 2 1 minecraft:bedrock
		setblock 0 0 0 minecraft:acacia_wall_sign
		setblock 0 1 0 minecraft:shulker_box
		summon minecraft:snow_golem 0 -2 0 {NoAI:1,NoGravity:1,Invulnerable:1,Silent:1,UUID:[I;0,1,0,1]}
		
		function skill:install
		function number_display:install
		function bar_display:install
		function stat:install
		function item:install
		
		function loop:schedule_1s/run
		function loop:schedule_10s/run
		function loop:schedule_1d/run
		
		datapack disable vanilla
	}
	
	func main()
	{
		execute as @a[scores={jump=1..}] at @s run function ARG(_PATH)jump_effect
		execute as @a[scores={just_jump=1..}] run function ARG(_PATH)jump_check_countdown
		execute as @a unless score @s command matches 0 run function ARG(_PATH)command/check
		execute as @a[scores={login=1..}] run function ARG(_PATH)login_reset
		
		function skill:main/run
		# function number_display:main
		function bar_display:main
		function item:main
	}
	
	func jump_effect()
	{
		playsound minecraft:game.jump player @a ~ ~ ~ 1 1 0
		scoreboard players set @s just_jump 3
		scoreboard players reset @s jump
	}
	
	func jump_check_countdown()
	{
		scoreboard players remove @s just_jump 1
		scoreboard players reset @s[scores={just_jump=0}] just_jump
	}
	
	func new_player()
	{
		execute store result score @s number run scoreboard players add #GLOBAL number 1
		
		scoreboard players set @s display_num_type 2
		
		scoreboard players set @s level 1
		scoreboard players set @s standard_pdd 7
		
		scoreboard players set @s hp_max_base 50
		scoreboard players set @s hp_max 50
		scoreboard players set @s hp 50
		scoreboard players set @s display_hp 100
		scoreboard players set @s hp_warning 30
		scoreboard players set @s mp_max_base 5
		scoreboard players set @s mp_max 5
		scoreboard players set @s mp 5
		scoreboard players set @s display_mp 100
		scoreboard players set @s mp_warning 30
		function bar_display:update_bar/run
		
		scoreboard players set @s hp_recover_time 10
		scoreboard players set @s next_hp_recover 10
		scoreboard players set @s hp_recovery 10
		scoreboard players set @s mp_recovery 3
		
		scoreboard players set @s STR 4
		scoreboard players set @s DEX 4
		scoreboard players set @s INT 4
		scoreboard players set @s LUK 4
		
		scoreboard players enable @s command
		
		scoreboard players set @s amazon_eyes_dist 9
		scoreboard players set @s keen_eyes_dist 9
		
		function skill:player/reset_mastery
		
		effect give @s minecraft:absorption 1000000 255 true
		attribute @s minecraft:generic.attack_damage base set -100.0
		replaceitem entity @s armor.head minecraft:potion{CustomModelData:1,display:{Name:'""'},HideFlags:127,Enchantments:[{id:"minecraft:binding_curse",lvl:1}],AttributeModifiers:[{AttributeName:"generic.movement_speed",Name:"equip",Amount:0,Operation:1,UUID:[I;0,1,0,1]}]}
		
		team join player @s
		
		execute at @s run playsound minecraft:game.game_in player @s
		title @s times 1 5 10
		title @s title {"text":"c","font":"ui:main"}
	}
	
	func login_reset()
	{
		execute if entity @s[tag=dead] run function ARG(_PATH)town_revive/run
		# special map teleport to nearest town belongs here
		
		execute at @s run playsound minecraft:game.game_in player @s
		title @s times 1 5 10
		title @s title {"text":"c","font":"ui:main"}
		scoreboard players reset @s login
	}
	
	folder town_revive()
	{
		func run()
		{
			function bar_display:death/revive
			
			execute unless score @s login matches 1.. run function ARG(_PATH)reset_screen
			```
			raise Warning("TODO: teleport to the nearest town")
			```
			tp @s -80 4 55 -90 40
		}
		
		func reset_screen()
		{
			tellraw @s "\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n"
			title @s times 1 5 10
			title @s title {"text":"c","font":"ui:main"}
		}
	}
	
	folder command()
	{
		func check()
		{
			execute if score @s command matches 1.. run function ARG(_PATH)search/run
			execute at @s run playsound minecraft:ui.button.click player @s
			
			scoreboard players reset @s command
			scoreboard players enable @s command
		}
		
		folder search()
		{
		```
		def binary_search_cmd(cmd_list, start, end):
			if len(cmd_list) == 1:
				print("func run()")
				print("{")
				print(f"\texecute if score @s command matches {cmd_list[0][0]} {cmd_list[0][1]}")
				print("}")
				return
			
			if start == end:
				return
			
			mid = (start + end) // 2
			mid_right = mid + 1
			
			if start == 0 and end == len(cmd_list) - 1:
				print("func run()")
			else:
				print(f"func {start}_{end}()")
			print("{")
			if start == mid:
				print(f"\texecute if score @s command matches {cmd_list[start][0]} {cmd_list[start][1]}")
			else:
				print(f"\texecute if score @s command matches {cmd_list[start][0]}..{cmd_list[mid][0]} run function ARG(_PATH){start}_{mid}")
			if mid_right == end:
				print(f"\texecute if score @s command matches {cmd_list[end][0]} {cmd_list[end][1]}")
			else:
				print(f"\texecute if score @s command matches {cmd_list[mid_right][0]}..{cmd_list[end][0]} run function ARG(_PATH){mid_right}_{end}")
			print("}")
			
			binary_search_cmd(cmd_list, start, mid)
			binary_search_cmd(cmd_list, mid_right, end)
		
		with open(PROJ_DIR + "/../commands.csv") as f:
			commands = f.read().split("\n")
			for i in range(len(commands)):
				sep_point = commands[i].find(",")
				commands[i] = [commands[i][:sep_point], commands[i][(sep_point+1):]]
		
		binary_search_cmd(commands, 0, len(commands) - 1)
		```
		}
	}
	
	func rand_rate()
	{
		loot replace block 0 1 0 container.0 loot main:percent_rate
		execute store result score #RAND_RESULT temp run data get block 0 1 0 Items[0].tag.Damage
	}
	
	func rand_range() from random().generate("#0 const", "#RANGE_MAX temp", "#RAND_RANGE_RESULT temp");
}

namespace loop()
{
	folder schedule_1s() from schedule_loop(1s, "")
	{
		func execute()
		{
			function skill:loop/schedule_1s
			function bar_display:loop/schedule_1s
			function item:loop/schedule_1s
			
			execute as @a[predicate=main:underground] at @s run tp @s ~ 10 ~
		}
	}
	
	folder schedule_10s() from schedule_loop(10s, "")
	{
		func execute()
		{
			function bar_display:loop/schedule_10s
			
			effect give @a minecraft:saturation 10 100 true
			# unlock players that set command trigger to 0
			scoreboard players enable @a[scores={command=0}] command
		}
	}
	
	folder schedule_1d() from schedule_loop(1d, "")
	{
		func execute()
		{
			function bar_display:loop/schedule_1d
			function skill:loop/schedule_1d
		}
	}
}



tag tick
{
	main:main
}

tag load	
{
	# samp:samp_folder/samp_function
}
import random
import loop

description = "MapleCraft data pack"

```
constants = [-7, -1, 0, 1, 2, 3, 4, 5, 6, 8, 9, 10, 17, 20, 30, 32, 34, 36, 40, 41, 44, 45, 46, 48, 50, 66, 100, 150, 1000, 10000, 99999]
```

import math
import number_display
import bar_display
import skill
import stat
import item

namespace main()
{
	func install()
	{
		gamerule commandBlockOutput false
		gamerule disableRaids true
		gamerule doDaylightCycle false
		gamerule doEntityDrops false
		gamerule doFireTick false
		gamerule doInsomnia false
		gamerule doMobLoot false
		gamerule doMobSpawning false
		gamerule doPatrolSpawning false
		gamerule doTileDrops false
		gamerule doTraderSpawning false
		gamerule doWeatherCycle false
		gamerule drowningDamage false
		gamerule fallDamage false
		gamerule fireDamage false
		gamerule forgiveDeadPlayers true
		gamerule keepInventory true
		gamerule logAdminCommands false
		gamerule mobGriefing false
		gamerule naturalRegeneration false
		# gamerule sendCommandFeedback false
		gamerule showDeathMessages false
		gamerule spawnRadius 0
		gamerule universalAnger false
		
		scoreboard objectives add const dummy
		scoreboard objectives add number dummy
		scoreboard objectives add type dummy
		scoreboard objectives add temp dummy
		scoreboard objectives add life dummy
		scoreboard objectives add max_life dummy
		scoreboard objectives add meso dummy
		scoreboard objectives add jump minecraft.custom:minecraft.jump
		scoreboard objectives add pick_meso minecraft.picked_up:minecraft.diamond
		scoreboard objectives add pick_etc minecraft.picked_up:minecraft.string
		
		team add enemy
		team modify enemy collisionRule never
		```
		constants = set(constants)
		for i in constants:
			print("scoreboard players set #{0} const {0}".format(i))
		```
		scoreboard players set #GLOBAL number 0
		
		setworldspawn 0 4 0
		fill -1 0 -1 1 3 1 minecraft:bedrock
		setblock 0 0 0 minecraft:acacia_wall_sign
		setblock 0 1 0 minecraft:chest
		setblock 0 2 0 minecraft:shulker_box
		summon minecraft:snow_golem 0 -2 0 {NoAI:1,NoGravity:1,Invulnerable:1,Silent:1,UUID:[I;0,1,0,1]}
		
		function skill:install
		function number_display:install
		function bar_display:install
		function stat:install
		function item:install
		
		function loop:schedule_1s/run
		function loop:schedule_10s/run
		function loop:schedule_1d/run
		
		datapack disable vanilla
	}
	
	func main()
	{
		execute as @a[scores={jump=1..}] at @s run function ARG(_PATH)jump_effect
		execute as @a[predicate=main:pickup] at @s run function ARG(_PATH)pick/check
		
		function skill:main/run
		# function number_display:main
		function bar_display:main
		function item:main
	}
	
	folder pick()
	{
		func check()
		{
			execute if score @s pick_meso matches 1.. run function ARG(_PATH)meso/record
			scoreboard players reset @s[scores={pick_etc=1..}] pick_etc
			
			playsound minecraft:game.pick_up_item player @a ~ ~ ~ 1 1 0
		}
		
		folder meso() from for_loop().for("@s pick_meso", "")
		{
			func record()
			{
				data modify storage main:item picked_meso append from entity @s Inventory[{id:"minecraft:diamond"}].tag.CustomModelData
				function ARG(_PATH)run
				
				clear @s minecraft:diamond
				scoreboard players reset @s pick_meso
			}
			
			func execute()
			{
				execute store result score #GLOBAL temp run data get storage main:item picked_meso[0]
				data remove storage main:item picked_meso[0]
				
				scoreboard players operation @s meso += #GLOBAL temp
			}
		}
		
		func etc()
		{
			scoreboard players reset @s pick_etc
		}
	}
	
	func jump_effect()
	{
		playsound minecraft:game.jump player @a ~ ~ ~ 1 1 0
		scoreboard players reset @s jump
	}
	
	func new_player()
	{
		execute store result score @s number run scoreboard players add #GLOBAL number 1
		
		scoreboard players set @s display_num_type 2
		
		scoreboard players set @s level 1
		
		scoreboard players set @s hp_max 50
		scoreboard players set @s hp 50
		scoreboard players set @s display_hp 100
		scoreboard players set @s hp_warning 30
		scoreboard players set @s mp_max 5
		scoreboard players set @s mp 5
		scoreboard players set @s display_mp 100
		scoreboard players set @s mp_warning 30
		
		scoreboard players set @s hp_recover_time 10
		scoreboard players set @s next_hp_recover 10
		scoreboard players set @s hp_recovery 10
		scoreboard players set @s mp_recovery 3
		
		scoreboard players set @s STR 4
		scoreboard players set @s DEX 4
		scoreboard players set @s INT 4
		scoreboard players set @s LUK 4
		
		scoreboard players set @s amazon_eyes_dist 9
		scoreboard players set @s keen_eyes_dist 9
		
		function skill:player/reset_mastery
		
		effect give @s minecraft:absorption 1000000 255 true
		attribute @s minecraft:generic.attack_damage base set -100.0
	}
	
	func rand_rate()
	{
		loot replace block 0 1 0 container.0 loot main:percent_rate
		execute store result score #RAND_RESULT temp run data get block 0 1 0 Items[0].tag.Damage
	}
	
	func rand_range() from random().generate("#0 const", "#RANGE_MAX temp", "#RAND_RANGE_RESULT temp");
}

namespace loop()
{
	folder schedule_1s() from schedule_loop(1s, "")
	{
		func execute()
		{
			function skill:loop/schedule_1s
			function bar_display:loop/schedule_1s
		}
	}
	
	folder schedule_10s() from schedule_loop(10s, "")
	{
		func execute()
		{
			function bar_display:loop/schedule_10s
			effect give @a minecraft:saturation 10 100 true
		}
	}
	
	folder schedule_1d() from schedule_loop(1d, "")
	{
		func execute()
		{
			function bar_display:loop/schedule_1d
			function skill:loop/schedule_1d
		}
	}
}



tag tick
{
	main:main
}

tag load	
{
	# samp:samp_folder/samp_function
}
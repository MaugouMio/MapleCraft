import utils

namespace bar_display()
{
	func install()
	{
		scoreboard objectives add hp dummy
		scoreboard objectives add mp dummy
		scoreboard objectives add hp_max dummy
		scoreboard objectives add mp_max dummy
		
		scoreboard objectives add display_hp dummy
		scoreboard objectives add display_mp dummy
		scoreboard objectives add target_hp dummy
		scoreboard objectives add target_mp dummy
		scoreboard objectives add hp_level dummy
		
		scoreboard objectives add hp_vary_time dummy
		scoreboard objectives add mp_vary_time dummy
		scoreboard objectives add hp_flash dummy
		scoreboard objectives add mp_flash dummy
		scoreboard objectives add hp_warning trigger
		scoreboard objectives add mp_warning trigger
		
		scoreboard objectives add next_hp_recover dummy
		scoreboard objectives add hp_recovery dummy
		scoreboard objectives add mp_recovery dummy
		
		scoreboard objectives add hurt_time dummy
		scoreboard objectives add damage_taken dummy
	}
	
	func main()
	{
		execute as @a[scores={hurt_time=1..}] run function ARG(_PATH)hurt_time_countdown/run
		execute as @a[tag=hp_mp_varying] run function ARG(_PATH)bar_vary/check
		function ARG(_PATH)show_head/run
	}
	
	folder hurt_time_countdown()
	{
		func run()
		{
			scoreboard players remove @s hurt_time 1
			execute if score @s hurt_time matches 0 run function ARG(_PATH)reset
		}
		
		func reset()
		{
			effect clear @s minecraft:glowing
			scoreboard players reset @s hurt_time
		}
	}
	
	folder defense_formula()
	{
		func weapon()
		{
			# base damage
			scoreboard players operation @s damage_taken *= @s damage_taken
			
			scoreboard players set #RANGE_MAX temp 50
			function main:rand_range
			scoreboard players operation #RAND_RANGE_RESULT temp *= @s damage_taken
			scoreboard players operation #RAND_RANGE_RESULT temp /= #100000 const
			
			scoreboard players operation @s damage_taken *= #8 const
			scoreboard players operation @s damage_taken /= #1000 const
			scoreboard players operation @s damage_taken += #RAND_RANGE_RESULT temp
			
			# set #C temp as C * 10,000
			function ARG(_PATH)get_c/run
			
			# reduce by weapon defense times A
			scoreboard players operation #A temp = #C temp
			scoreboard players add #A temp 2800
			
			scoreboard players operation #GLOBAL temp = #WEAPON_DEFENSE temp
			scoreboard players operation #GLOBAL temp *= #A temp
			scoreboard players operation #GLOBAL temp /= #10000 const
			
			scoreboard players operation @s damage_taken -= #GLOBAL temp
			
			# set #B temp as B * 1,000,000
			function ARG(_PATH)get_b/run
			
			# reduce by (weapon_defense - standard_pdd) * B
			scoreboard players operation #WEAPON_DEFENSE temp -= @s standard_pdd
			scoreboard players operation #WEAPON_DEFENSE temp *= #B temp
			scoreboard players operation #WEAPON_DEFENSE temp /= #1000000 const
			
			scoreboard players operation @s damage_taken -= #WEAPON_DEFENSE temp
		}
		
		func magic()
		{
			# base damage
			scoreboard players operation @s damage_taken *= @s damage_taken
			
			scoreboard players set #RANGE_MAX temp 50
			function main:rand_range
			scoreboard players operation #RAND_RANGE_RESULT temp *= @s damage_taken
			scoreboard players operation #RAND_RANGE_RESULT temp /= #100000 const
			
			scoreboard players operation @s damage_taken *= #15 const
			scoreboard players operation @s damage_taken /= #2000 const
			scoreboard players operation @s damage_taken += #RAND_RANGE_RESULT temp
			
			# magic defense related
			scoreboard players operation #MAGIC_DEFENSE temp *= #25 const
			
			scoreboard players operation #GLOBAL STR = @s STR
			scoreboard players operation #GLOBAL STR *= #100 const
			scoreboard players operation #GLOBAL STR /= #28 const
			scoreboard players operation #MAGIC_DEFENSE temp += #GLOBAL STR
			
			scoreboard players operation #GLOBAL DEX = @s DEX
			scoreboard players operation #GLOBAL DEX *= #25 const
			scoreboard players operation #GLOBAL DEX /= #6 const
			scoreboard players operation #MAGIC_DEFENSE temp += #GLOBAL DEX
			
			scoreboard players operation #GLOBAL LUK = @s LUK
			scoreboard players operation #GLOBAL LUK *= #5 const
			scoreboard players operation #MAGIC_DEFENSE temp += #GLOBAL LUK
			
			execute if entity @s[tag=magician] run function ARG(_PATH)k_multiply
			scoreboard players operation #MAGIC_DEFENSE temp /= #100 const
			
			scoreboard players operation @s damage_taken -= #MAGIC_DEFENSE temp
		}
		
		folder get_c()
		{
			func run()
			{
				scoreboard players operation #C temp = @s STR
				scoreboard players operation #C temp *= #100 const
				
				scoreboard players operation #GLOBAL DEX = @s DEX
				scoreboard players operation #GLOBAL DEX *= #100 const
				
				execute if entity @s[tag=warrior] run function ARG(_PATH)warrior
				execute if entity @s[tag=!warrior] run function ARG(_PATH)non_warrior
				scoreboard players operation #C temp += #GLOBAL DEX
				
				scoreboard players operation #GLOBAL INT = @s INT
				scoreboard players operation #GLOBAL INT *= #50 const
				scoreboard players operation #GLOBAL INT /= #36 const
				scoreboard players operation #C temp += #GLOBAL INT
				
				scoreboard players operation #GLOBAL LUK = @s LUK
				scoreboard players operation #GLOBAL LUK *= #100 const
				scoreboard players operation #GLOBAL LUK /= #32 const
				scoreboard players operation #C temp += #GLOBAL LUK
			}
			
			func warrior()
			{
				scoreboard players operation #C temp /= #28 const
				scoreboard players operation #GLOBAL DEX /= #32 const
			}
			
			func non_warrior()
			{
				scoreboard players operation #C temp /= #20 const
				scoreboard players operation #GLOBAL DEX /= #28 const
			}
		}
		
		folder get_b()
		{
			func run()
			{
				execute if score #WEAPON_DEFENSE temp >= @s standard_pdd run function ARG(_PATH)wdef_greater
				execute if score #WEAPON_DEFENSE temp < @s standard_pdd run function ARG(_PATH)wdef_smaller
			}
			
			func wdef_greater()
			{
				scoreboard players operation #C temp *= #560 const
				scoreboard players operation #C temp /= #9 const
				
				scoreboard players operation #B temp = @s level
				scoreboard players operation #B temp *= #7000 const
				scoreboard players operation #B temp /= #13 const
				
				scoreboard players operation #B temp += #C temp
				scoreboard players add #B temp 196000
			}
			
			func wdef_smaller()
			{
				scoreboard players operation #C temp *= #10 const
				
				scoreboard players operation #B temp = @s level
				scoreboard players operation #B temp *= #2000 const
				scoreboard players operation #B temp /= #11 const
				
				scoreboard players operation #B temp += #C temp
				scoreboard players add #B temp 28000
				
				execute if score @s level >= #ATTACKER_LEVEL temp run function ARG(_PATH)multi_d
				execute if score @s level < #ATTACKER_LEVEL temp run scoreboard players operation #B temp *= #13 const
			}
			
			func multi_d()
			{
				scoreboard players set #LEVEL_DIFF temp 13
				scoreboard players operation #LEVEL_DIFF temp += @s level
				scoreboard players operation #LEVEL_DIFF temp -= #ATTACKER_LEVEL temp
				
				scoreboard players set #D temp 13000
				scoreboard players operation #D temp /= #LEVEL_DIFF temp
				
				scoreboard players operation #B temp *= #D temp
				scoreboard players operation #B temp /= #100 const
			}
		}
		
		func k_multiply()
		{
			scoreboard players operation #MAGIC_DEFENSE temp *= #6 const
			scoreboard players operation #MAGIC_DEFENSE temp /= #5 const
		}
	}
	
	folder take_damage()
	{
		func touch()
		{
			scoreboard players set #TOUCH_DAMAGE temp 1
			# 碰撞傷害前面已經檢查過命中了，跑到這一定是有命中
			function ARG(_PATH)physic/defense
		}
		
		folder get_final_avoid()
		{
			func run()
			{
				scoreboard players set #AVOIDABILITY temp 0
				execute if score @s level < #ATTACKER_LEVEL temp run function ARG(_PATH)level_effect
				scoreboard players operation #AVOIDABILITY temp += @s avoidability
			}
			
			func level_effect()
			{
				# 這邊是玩家比怪物低等的情況，算出來會是負值
				scoreboard players operation #AVOIDABILITY temp = @s level
				scoreboard players operation #AVOIDABILITY temp -= #ATTACKER_LEVEL temp
				scoreboard players operation #AVOIDABILITY temp /= #2 const
			}
		}
		
		folder physic()
		{
			func check()
			{
				# 把判斷命中拉出來，給碰撞判斷用
				function ARG(_PATH)check_only_miss
				execute if score #RAND_RESULT number >= #AVOIDABILITY temp run function ARG(_PATH)defense
			}
			
			func check_only_miss()
			{
				# 避免特殊情況沒有值導致 division by zero
				execute unless score #ATTACKER_ACC temp matches 1.. run scoreboard players set #ATTACKER_ACC temp 1
				
				function ARG(__PATH)get_final_avoid/run
				
				scoreboard players operation #AVOIDABILITY temp *= #1000 const
				scoreboard players operation #AVOIDABILITY temp /= #ATTACKER_ACC temp
				scoreboard players operation #AVOIDABILITY temp /= #45 const
				
				execute if entity @s[tag=!thief] run function ARG(_PATH)clamp
				execute if entity @s[tag=thief] run function ARG(_PATH)clamp_thief
				
				function random:rand_rate
				execute if score #RAND_RESULT number < #AVOIDABILITY temp run function ARG(__PATH)miss
			}
			
			func clamp()
			{
				execute if score #AVOIDABILITY temp matches 81.. run scoreboard players set #AVOIDABILITY temp 80
				execute if score #AVOIDABILITY temp matches ..1 run scoreboard players set #AVOIDABILITY temp 2
			}
			
			func clamp_thief()
			{
				execute if score #AVOIDABILITY temp matches 96.. run scoreboard players set #AVOIDABILITY temp 95
				execute if score #AVOIDABILITY temp matches ..4 run scoreboard players set #AVOIDABILITY temp 5
			}
			
			func defense()
			{
				scoreboard players operation #WEAPON_DEFENSE temp = @s weapon_defense
				execute if score @s wdef_buff_id matches 0.. run scoreboard players operation #WEAPON_DEFENSE temp += @s wdef_buff_num
				function bar_display:defense_formula/weapon
				function ARG(__PATH)take
			}
		}
		
		folder magic()
		{
			func check()
			{
				# 避免特殊情況沒有值導致 division by zero
				execute unless score #ATTACKER_ACC temp matches 1.. run scoreboard players set #ATTACKER_ACC temp 1
				
				function ARG(__PATH)get_final_avoid/run
				
				scoreboard players operation #ACCURACY temp = #ATTACKER_ACC temp
				scoreboard players operation #ACCURACY temp *= #100 const
				scoreboard players operation #ACCURACY temp /= #AVOIDABILITY temp
				scoreboard players set #AVOIDABILITY temp 100
				scoreboard players operation #AVOIDABILITY temp -= #ACCURACY temp
				scoreboard players operation #AVOIDABILITY temp *= #10 const
				scoreboard players operation #AVOIDABILITY temp /= #9 const
				
				function random:rand_rate
				execute if score #RAND_RESULT number < #AVOIDABILITY temp run function ARG(__PATH)miss
				execute if score #RAND_RESULT number >= #AVOIDABILITY temp run function ARG(_PATH)defense
			}
			
			func defense()
			{
				scoreboard players operation #MAGIC_DEFENSE temp = @s magic_defense
				execute if score @s mdef_buff_id matches 0.. run scoreboard players operation #MAGIC_DEFENSE temp += @s mdef_buff_num
				function bar_display:defense_formula/magic
				function ARG(__PATH)take
			}
		}
		
		func miss()
		{
			scoreboard players set @s display_num_type 2
			scoreboard players set @s display_number 0
			execute positioned ~ ~2 ~ run function number_display:show
			
			scoreboard players set @s hurt_time 30
			scoreboard players reset @s damage_taken
		}
		
		func take()
		{
			scoreboard players operation @s damage_taken > #1 const
			
			execute if score @s invin_buff_time matches 1.. run function skill:2/3/0/1003/guard
			execute if score #TOUCH_DAMAGE temp matches 1 if score @s refle_buff_time matches 1.. run function skill:1/1/0/1007/reflect
			scoreboard players operation @s damage_taken > #1 const
			
			scoreboard players set @s display_num_type 2
			scoreboard players operation @s display_number = @s damage_taken
			execute positioned ~ ~2 ~ run function number_display:show
			
			execute if score @s mguar_buff_time matches 1.. if score @s mp matches 1.. run function skill:2/0/0/1002/guard
			scoreboard players operation @s damage_taken > #1 const
			
			scoreboard players operation @s hp -= @s damage_taken
			execute if score @s hp matches ..0 run function ARG(__PATH)death/die
			function ARG(__PATH)change/hp/dropped
			
			scoreboard players operation @s next_hp_recover = @s hp_recover_time
			scoreboard players set @s hurt_time 30
			
			# 擊退效果
			```
			print("execute if score #TOUCH_DAMAGE temp matches 1 on attacker at @s run summon marker ~ ~ ~ {UUID:%s}" %UUID_LIST["mob_damage"])
			```
			# 要先解除騎乘狀態
			execute on vehicle run function ARG(_PATH)sit_knock
			function mob:damage
			execute if score #TOUCH_DAMAGE temp matches 1 run kill 00000000-0000-0006-0000-000000000006
			
			effect give @s minecraft:glowing infinite 0 true

			scoreboard players reset @s damage_taken
			scoreboard players reset #TOUCH_DAMAGE temp
			scoreboard players set @s tired_time 5
		}

		func sit_knock()
		{
			# 用 tp 解除騎乘狀態才不會有從坐騎下來的動量刷新
			execute on passengers run tp @s @s
			kill @s
		}
	}
	
	folder death()
	{
		func die()
		{
			gamemode spectator @s
			scoreboard players set @s hp 0
			execute if score @s job_level matches 1.. run function ARG(_PATH)drop_exp
			function skill:buff/reset_all
			tag @s add dead
			
			playsound minecraft:game.die player @a ~ ~ ~ 1 1 0
			
			data modify entity 00000000-0000-0001-0000-000000000001 DeathLootTable set value "bar_display:skull"
			loot replace block 250 2 0 container.1 kill 00000000-0000-0001-0000-000000000001
			scoreboard players operation #GLOBAL temp = @s number
			execute summon minecraft:item_display run function ARG(_PATH)head_set
			execute summon minecraft:item_display run function ARG(_PATH)body_set
			execute summon minecraft:item_display run function ARG(_PATH)tomb_set
			
			scoreboard players add #TOMB_AMOUNT temp 1
			forceload add ~ ~
			tellraw @s [{"text":"0_1\n2_3\n4_5\n6_7\n8_9\na_b\nc_d\n","font":"ui:window/death"},{"text":"\uF82A\uF829\uF828\uF823","font":"space:default"},{"text":"2","font":"ui:main","clickEvent":{"action":"run_command","value":"/trigger command set 7"},"hoverEvent":{"action":"show_text","contents":{"text":"3","font":"ui:main"}}},{"text":"B94","font":"space:default"},{"text":"e_f\ng_h","font":"ui:window/death"}]
		}
		
		func drop_exp()
		{
			scoreboard players operation #GLOBAL temp = @s exp_max
			scoreboard players operation #GLOBAL temp /= #10 const
			scoreboard players operation @s exp -= #GLOBAL temp
			scoreboard players operation @s exp > #0 const
		}

		func head_set()
		{
			data merge entity @s {Tags:["dead_decoration"],CustomNameVisible:1,transformation:{scale:[1.2f,1.2f,1.2f]}}
			item replace entity @s container.0 from block 250 2 0 container.1
			function ARG(_PATH)set_name
			tp @s ~ ~2 ~ ~ 0
			scoreboard players operation @s number = #GLOBAL temp
		}

		func body_set()
		{
			data merge entity @s {Tags:["dead_decoration"],transformation:{translation:[0.0f,1.5f,0.0f],scale:[0.75f,0.75f,0.75f]},item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:2}}}
			tp @s ~ ~ ~ ~ 0
			scoreboard players operation @s number = #GLOBAL temp
		}

		func tomb_set()
		{
			data merge entity @s {Tags:["dead_decoration","tomb"],transformation:{translation:[0.0f,10.6f,1.5f]},item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:3,CustomPotionColor:-1}},item_display:"head"}
			execute store result score @s temp run schedule function ARG(_PATH)tomb_anim 1t append
			tp @s ~ ~ ~ ~ 0
			scoreboard players operation @s number = #GLOBAL temp
		}
		
		func tomb_anim()
		{
			execute store result score #GLOBAL temp run time query gametime
			execute as @e[type=item_display,tag=tomb] if score @s temp = #GLOBAL temp run data merge entity @s {transformation:{translation:[0.0f,2.6f,1.5f]},interpolation_duration:13,start_interpolation:0}
		}
		
		func set_name()
		{
			data modify storage bar:main name set from entity @s ArmorItems[3].tag.SkullOwner.Name
			data modify block 250 0 0 Text1 set value '{"nbt":"name","storage":"bar:main"}'
			data modify entity @s CustomName set from block 250 0 0 Text1
		}
		
		func revive()
		{
			scoreboard players operation #GLOBAL temp = @s number
			execute as @e[type=item_display,tag=dead_decoration] if score @s number = #GLOBAL temp at @s run function ARG(_PATH)remove_dead_decoration/all
			execute as @e[type=item_display,tag=tomb] at @s run forceload add ~ ~
			
			scoreboard players set @s hp 50
			function ARG(__PATH)change/hp/recovered
			tag @s remove dead
			
			gamemode adventure @s
			effect give @s minecraft:invisibility 1 0 true
		}
		
		folder tomb_check()
		{
			func find()
			{
				scoreboard players set #FOUND_TOMB_OWNER temp 0
				scoreboard players operation #GLOBAL temp = @s number
				execute as @a[tag=dead] if score @s number = #GLOBAL temp run scoreboard players set #FOUND_TOMB_OWNER temp 1
				execute if score #FOUND_TOMB_OWNER temp matches 0 at @s run function ARG(_PATH)clear
			}
			
			func clear()
			{
				function ARG(__PATH)remove_dead_decoration/reset
				execute as @e[type=item_display,tag=dead_decoration,distance=..1] if score @s number = #GLOBAL temp run kill @s
				execute as @e[type=item_display,tag=tomb] at @s run forceload add ~ ~
			}
		}
		
		folder remove_dead_decoration()
		{
			func all()
			{
				execute if entity @s[tag=tomb] run function ARG(_PATH)reset
				kill @s
			}
			
			func reset()
			{
				forceload remove ~ ~
				scoreboard players remove #TOMB_AMOUNT temp 1
			}
		}
	}
	
	folder change()
	{
		folder hp()
		{
			func dropped()
			{
				scoreboard players set #HP_DROPPED temp 1
				function ARG(_PATH)set
			}
			
			func recovered()
			{
				scoreboard players set #HP_DROPPED temp 0
				function ARG(_PATH)set
			}
		
			func set()
			{
				scoreboard players operation @s target_hp = @s hp
				scoreboard players operation @s target_hp *= #100 const
				scoreboard players operation @s target_hp /= @s hp_max
				scoreboard players operation @s[scores={hp=1..}] target_hp > #1 const
				
				scoreboard players operation #GLOBAL temp = @s target_hp
				scoreboard players add #GLOBAL temp 1
				scoreboard players operation #GLOBAL temp *= #46 const
				scoreboard players operation #GLOBAL temp /= #100 const
				
				scoreboard players operation @s hp_level = #GLOBAL temp
				
				scoreboard players set @s hp_vary_time 20
				tag @s add hp_mp_varying
				execute if score #HP_DROPPED temp matches 1 if score @s target_hp <= @s hp_warning run scoreboard players set @s hp_flash 10
			}
		}
		
		folder mp()
		{
			func dropped()
			{
				scoreboard players set #MP_DROPPED temp 1
				function ARG(_PATH)set
			}
			
			func recovered()
			{
				scoreboard players set #MP_DROPPED temp 0
				function ARG(_PATH)set
			}
			
			func set()
			{
				scoreboard players operation @s target_mp = @s mp
				scoreboard players operation @s target_mp *= #100 const
				scoreboard players operation @s target_mp /= @s mp_max
				scoreboard players operation @s[scores={mp=1..}] target_mp > #1 const
				
				scoreboard players set @s mp_vary_time 20
				tag @s add hp_mp_varying
				execute if score #MP_DROPPED temp matches 1 if score @s target_mp < @s mp_warning run scoreboard players set @s mp_flash 10
			}
		}
		
	}
	
	folder bar_vary()
	{
		func check()
		{
			execute if score @s hp_vary_time matches 1.. run function ARG(_PATH)hp_vary
			execute if score @s mp_vary_time matches 1.. run function ARG(_PATH)mp_vary
			
			function ARG(__PATH)update_bar/run
			execute unless score @s hp_vary_time matches 1.. unless score @s mp_vary_time matches 1.. run tag @s remove hp_mp_varying
		}
		
		func hp_vary()
		{
			scoreboard players operation #GLOBAL temp = @s target_hp
			scoreboard players operation #GLOBAL temp -= @s display_hp
			execute if score @s hp_vary_time matches 10.. run scoreboard players operation #GLOBAL temp *= #2 const
			scoreboard players operation #GLOBAL temp /= @s hp_vary_time
			scoreboard players operation @s display_hp += #GLOBAL temp
			
			scoreboard players remove @s[scores={hp_flash=0..}] hp_flash 1
			scoreboard players remove @s hp_vary_time 1
			scoreboard players reset @s[scores={hp_vary_time=0}] hp_vary_time
		}
		
		func mp_vary()
		{
			scoreboard players operation #GLOBAL temp = @s target_mp
			scoreboard players operation #GLOBAL temp -= @s display_mp
			execute if score @s mp_vary_time matches 10.. run scoreboard players operation #GLOBAL temp *= #2 const
			scoreboard players operation #GLOBAL temp /= @s mp_vary_time
			scoreboard players operation @s display_mp += #GLOBAL temp
			
			scoreboard players remove @s[scores={mp_flash=0..}] mp_flash 1
			scoreboard players remove @s mp_vary_time 1
			scoreboard players reset @s[scores={mp_vary_time=0}] mp_vary_time
		}
	}
	
	folder update_bar()
	{
		func run()
		{
			data modify storage bar:main hp set value []
			scoreboard players operation #VALUE_TO_INSERT temp = @s display_hp
			function ARG(_PATH)insert_hp_digit
			
			data modify storage bar:main mp set value []
			scoreboard players operation #VALUE_TO_INSERT temp = @s display_mp
			function ARG(_PATH)insert_mp_digit
			
			function ARG(_PATH)get_exp_show/calculate
			
			execute unless score @s hp_flash matches 0.. run data modify storage bar:main hp_flash set value "-"
			execute unless score @s mp_flash matches 0.. run data modify storage bar:main mp_flash set value "-"
			execute if score @s hp_flash matches 0.. run execute store result storage bar:main hp_flash int 1 run scoreboard players get @s hp_flash
			execute if score @s mp_flash matches 0.. run execute store result storage bar:main mp_flash int 1 run scoreboard players get @s mp_flash
			
			title @s actionbar [{"text":"A83","font":"space:default"},{"text":"4","font":"ui:stats_bar/hp_mp"},{"text":"92","font":"space:default"},{"score":{"name":"@s","objective":"level"},"font":"ui:stats_bar/lv_num_back"},{"score":{"name":"@s","objective":"level"},"font":"ui:stats_bar/lv_num"},{"score":{"name":"@s","objective":"level"},"font":"ui:stats_bar/lv_num_back2"},{"text":"\uF822","font":"space:default"},{"text":"0","font":"ui:stats_bar/hp_mp"},{"text":"CA86","font":"space:default"},{"nbt":"hp[0]","storage":"bar:main","font":"ui:stats_bar/hp1"},{"text":"1","font":"space:default"},{"nbt":"hp[1]","storage":"bar:main","font":"ui:stats_bar/hp10"},{"nbt":"hp[2]","storage":"bar:main","font":"ui:stats_bar/hp100"},{"nbt":"hp[0]","storage":"bar:main","font":"ui:stats_bar/bar_back1"},{"nbt":"hp[1]","storage":"bar:main","font":"ui:stats_bar/bar_back10"},{"nbt":"hp[2]","storage":"bar:main","font":"ui:stats_bar/bar_back100"},{"text":"\uF828\uF827","font":"space:default"},[{"text":"[","font":"ui:stats_bar/bar_num"},{"score":{"name":"@s","objective":"hp"}},"/",{"score":{"name":"@s","objective":"hp_max"}},"]"],{"text":"87","font":"space:default"},[{"text":"[","font":"ui:stats_bar/bar_num_back"},{"score":{"name":"@s","objective":"hp"}},"/",{"score":{"name":"@s","objective":"hp_max"}},"]"],{"text":"\uF82B\uF829\uF828\uF821","font":"space:default"},{"nbt":"mp[0]","storage":"bar:main","font":"ui:stats_bar/mp1"},{"text":"1","font":"space:default"},{"nbt":"mp[1]","storage":"bar:main","font":"ui:stats_bar/mp10"},{"nbt":"mp[2]","storage":"bar:main","font":"ui:stats_bar/mp100"},{"nbt":"mp[0]","storage":"bar:main","font":"ui:stats_bar/bar_back1"},{"nbt":"mp[1]","storage":"bar:main","font":"ui:stats_bar/bar_back10"},{"nbt":"mp[2]","storage":"bar:main","font":"ui:stats_bar/bar_back100"},{"text":"\uF829\uF822","font":"space:default"},[{"text":"[","font":"ui:stats_bar/bar_num"},{"score":{"name":"@s","objective":"mp"}},"/",{"score":{"name":"@s","objective":"mp_max"}},"]"],{"text":"92","font":"space:default"},[{"text":"[","font":"ui:stats_bar/bar_num_back"},{"score":{"name":"@s","objective":"mp"}},"/",{"score":{"name":"@s","objective":"mp_max"}},"]"],{"text":"B97","font":"space:default"},{"text":"1","font":"ui:stats_bar/hp_mp"},{"text":"CA9","font":"space:default"},{"nbt":"hp_flash","storage":"bar:main","font":"ui:stats_bar/bar_flash"},{"text":"2","font":"space:default"},{"nbt":"mp_flash","storage":"bar:main","font":"ui:stats_bar/bar_flash"},{"text":"2","font":"ui:stats_bar/hp_mp"},{"text":"B987","font":"space:default"},{"nbt":"exp[0]","storage":"bar:main","font":"ui:stats_bar/exp1"},{"text":"1","font":"space:default"},{"nbt":"exp[1]","storage":"bar:main","font":"ui:stats_bar/exp10"},{"nbt":"exp[2]","storage":"bar:main","font":"ui:stats_bar/exp100"},{"nbt":"exp[0]","storage":"bar:main","font":"ui:stats_bar/exp_bar_back1"},{"nbt":"exp[1]","storage":"bar:main","font":"ui:stats_bar/exp_bar_back10"},{"nbt":"exp[2]","storage":"bar:main","font":"ui:stats_bar/exp_bar_back100"},{"text":"\uF829\uF826","font":"space:default"},[{"score":{"name":"@s","objective":"exp"},"font":"ui:stats_bar/bar_num"},"[",{"score":{"name":"#PERC","objective":"temp"}},".",{"score":{"name":"#PERC_POINT","objective":"temp"}},{"score":{"name":"#PERC_POINT2","objective":"temp"}},"%]"],[{"score":{"name":"@s","objective":"exp"},"font":"ui:stats_bar/bar_num_back"},"[",{"score":{"name":"#PERC","objective":"temp"}},".",{"score":{"name":"#PERC_POINT","objective":"temp"}},{"score":{"name":"#PERC_POINT2","objective":"temp"}},"%]"],{"text":"96","font":"space:default"},{"text":"3","font":"ui:stats_bar/hp_mp"},{"text":"A7","font":"space:default"},{"nbt":"Attributes[{Name:\"minecraft:generic.luck\"}].Modifiers[].Name","entity":"@s","font":"skill:buff"},{"nbt":"Attributes[{Name:\"minecraft:generic.luck\"}].Modifiers[].Name","entity":"@s","font":"skill:buff_back"},{"text":"\uF82A\uF828\uF827","font":"space:default"}]
		}
		
		folder get_exp_show()
		{
			func calculate()
			{
				scoreboard players operation #PERC temp = @s exp
				scoreboard players operation #PERC_POINT temp = @s exp_max
				# 在不 overflow 的情況下做誤差最小化
				execute if score @s exp matches ..210000 run scoreboard players operation #PERC temp *= #10000 const
				execute if score @s exp matches 210001..2100000 run function ARG(_PATH)times1000
				execute if score @s exp matches 2100001..21000000 run function ARG(_PATH)times100
				execute if score @s exp matches 21000001..210000000 run function ARG(_PATH)times10
				execute if score @s exp matches 210000001.. run scoreboard players operation #PERC_POINT temp /= #10000 const
				scoreboard players operation #PERC temp /= #PERC_POINT temp
				scoreboard players operation #PERC_POINT temp = #PERC temp
				scoreboard players operation #PERC temp /= #100 const
				
				scoreboard players operation #PERC_POINT temp %= #100 const
				scoreboard players operation #PERC_POINT2 temp = #PERC_POINT temp
				scoreboard players operation #PERC_POINT temp /= #10 const
				scoreboard players operation #PERC_POINT2 temp %= #10 const
				
				data modify storage bar:main exp set value []
				scoreboard players operation #VALUE_TO_INSERT temp = #PERC temp
				function ARG(_PATH)insert_exp_digit
			}
			
			func times1000()
			{
				scoreboard players operation #PERC temp *= #1000 const
				scoreboard players operation #PERC_POINT temp /= #10 const
			}
			
			func times100()
			{
				scoreboard players operation #PERC temp *= #100 const
				scoreboard players operation #PERC_POINT temp /= #100 const
			}
			
			func times10()
			{
				scoreboard players operation #PERC temp *= #10 const
				scoreboard players operation #PERC_POINT temp /= #1000 const
			}
		
			func insert_exp_digit()
			{
				data modify storage bar:main exp append value 0
				scoreboard players operation #GLOBAL temp = #VALUE_TO_INSERT temp
				execute store result storage bar:main exp[-1] int 1 run scoreboard players operation #GLOBAL temp %= #10 const
				
				scoreboard players operation #VALUE_TO_INSERT temp /= #10 const
				execute if score #VALUE_TO_INSERT temp matches 1.. run function ARG(_PATH)insert_exp_digit
			}
		}
		
		func insert_hp_digit()
		{
			data modify storage bar:main hp append value 0
			scoreboard players operation #GLOBAL temp = #VALUE_TO_INSERT temp
			execute store result storage bar:main hp[-1] int 1 run scoreboard players operation #GLOBAL temp %= #10 const
			
			scoreboard players operation #VALUE_TO_INSERT temp /= #10 const
			execute if score #VALUE_TO_INSERT temp matches 1.. run function ARG(_PATH)insert_hp_digit
		}
		
		func insert_mp_digit()
		{
			data modify storage bar:main mp append value 0
			scoreboard players operation #GLOBAL temp = #VALUE_TO_INSERT temp
			execute store result storage bar:main mp[-1] int 1 run scoreboard players operation #GLOBAL temp %= #10 const
			
			scoreboard players operation #VALUE_TO_INSERT temp /= #10 const
			execute if score #VALUE_TO_INSERT temp matches 1.. run function ARG(_PATH)insert_mp_digit
		}
	}
	
	folder recover()
	{
		func mp()
		{
			scoreboard players operation #GLOBAL temp = @s mp_recovery
			execute on vehicle run function ARG(_PATH)chair_mp
			scoreboard players operation @s mp += #GLOBAL temp
			scoreboard players operation @s mp < @s mp_max
			function ARG(__PATH)change/mp/recovered
		}
		
		func chair_mp()
		{
			scoreboard players operation #GLOBAL temp *= #3 const
			scoreboard players operation #GLOBAL temp /= #2 const
		}
		
		func hp()
		{
			scoreboard players remove @s next_hp_recover 1
			execute if score @s next_hp_recover matches 0 at @s run function ARG(_PATH)do_recover_hp
		}
		
		func do_recover_hp()
		{
			scoreboard players operation @s next_hp_recover = @s hp_recover_time
			
			scoreboard players operation #GLOBAL temp = @s hp_recovery
			execute on vehicle store result score #GLOBAL temp run data get entity @s item.tag.hp
			scoreboard players operation @s hp += #GLOBAL temp
			scoreboard players operation @s hp < @s hp_max
			
			scoreboard players set @s display_num_type 3
			scoreboard players operation @s display_number = #GLOBAL temp
			execute positioned ~ ~2 ~ run function number_display:show
			
			function ARG(__PATH)change/hp/recovered
		}
	}
	
	folder show_head()
	{
		func run()
		{
			data modify storage bar:main head set value []
			
			execute as @a[gamemode=adventure] at @s run function ARG(_PATH)write_data
			execute as @e[type=interaction,tag=player_pos] run function ARG(_PATH)update_pos
			
			execute if data storage bar:main head[0] run function ARG(_PATH)spawn
		}
		
		func write_data()
		{
			data modify storage bar:main head append value {pos:[],value:0b,id:0,width:0,height:0}
			
			data modify storage bar:main head[-1].pos set from entity @s Pos
			execute store result storage bar:main head[-1].value byte 1 run scoreboard players get @s hp_level
			execute store result storage bar:main head[-1].id int 1 run scoreboard players get @s number
			execute if predicate skill:quick_cast_skill unless data entity @s {SelectedItemSlot:8} run data modify storage bar:main head[-1] merge value {width:0.8,height:1.8}
		}
		
		func deep_kill()
		{
			execute on passengers run kill @s
			kill @s
		}
		
		func update_pos()
		{
			execute unless data storage bar:main head[0] run function ARG(_PATH)deep_kill
			execute if data storage bar:main head[0] run function ARG(_PATH)check_owner
		}
		
		func check_owner()
		{
			execute store result score #GLOBAL temp run data get storage bar:main head[0].id
			# 新生出來的物件初始化
			execute unless score @s number matches 0.. run scoreboard players operation @s number = #GLOBAL temp
			execute unless score @s number = #GLOBAL temp run function ARG(_PATH)deep_kill
			execute if score @s number = #GLOBAL temp run function ARG(_PATH)read_pos
		}
		
		func read_pos()
		{
			data modify entity @s Pos set from storage bar:main head[0].pos
			execute store result entity @s Air short 1 run time query gametime
			
			data modify entity @s width set from storage bar:main head[0].width
			data modify entity @s height set from storage bar:main head[0].height
			
			execute on passengers if entity @s[type=text_display,tag=head_hp] run function ARG(_PATH)read_data
			
			data remove storage bar:main head[0]
		}
		
		func read_data()
		{
			execute store result score #GLOBAL temp run data get storage bar:main head[0].value
			execute unless score @s hp_level = #GLOBAL temp run function ARG(_PATH)set_name/check
		}
		
		folder set_name()
		{
			func check()
			{
				scoreboard players operation @s hp_level = #GLOBAL temp
				
				execute if score @s hp_level matches ..11 run function ARG(_PATH)search/0_11
				execute if score @s hp_level matches 12..23 run function ARG(_PATH)search/12_23
				execute if score @s hp_level matches 24..35 run function ARG(_PATH)search/24_35
				execute if score @s hp_level matches 36.. run function ARG(_PATH)search/36_46
			}
			
			folder search()
			{
				func 0_11()
				{
					execute if score @s hp_level matches ..3 run function ARG(_PATH)0_3
					execute if score @s hp_level matches 4..7 run function ARG(_PATH)4_7
					execute if score @s hp_level matches 8.. run function ARG(_PATH)8_11
				}
				
				func 12_23()
				{
					execute if score @s hp_level matches ..15 run function ARG(_PATH)12_15
					execute if score @s hp_level matches 16..19 run function ARG(_PATH)16_19
					execute if score @s hp_level matches 20.. run function ARG(_PATH)20_23
				}
				
				func 24_35()
				{
					execute if score @s hp_level matches ..27 run function ARG(_PATH)24_27
					execute if score @s hp_level matches 28..31 run function ARG(_PATH)28_31
					execute if score @s hp_level matches 32.. run function ARG(_PATH)32_35
				}
				
				func 36_46()
				{
					execute if score @s hp_level matches ..39 run function ARG(_PATH)36_39
					execute if score @s hp_level matches 40..43 run function ARG(_PATH)40_43
					execute if score @s hp_level matches 44.. run function ARG(_PATH)44_46
				}
				
				func 0_3()
				{
					execute if score @s hp_level matches 0 run data modify entity @s text set value '{"text":"0","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 1 run data modify entity @s text set value '{"text":"1","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 2 run data modify entity @s text set value '{"text":"2","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 3 run data modify entity @s text set value '{"text":"3","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 4_7()
				{
					execute if score @s hp_level matches 4 run data modify entity @s text set value '{"text":"4","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 5 run data modify entity @s text set value '{"text":"5","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 6 run data modify entity @s text set value '{"text":"6","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 7 run data modify entity @s text set value '{"text":"7","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 8_11()
				{
					execute if score @s hp_level matches 8 run data modify entity @s text set value '{"text":"8","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 9 run data modify entity @s text set value '{"text":"9","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 10 run data modify entity @s text set value '{"text":"A","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 11 run data modify entity @s text set value '{"text":"B","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 12_15()
				{
					execute if score @s hp_level matches 12 run data modify entity @s text set value '{"text":"C","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 13 run data modify entity @s text set value '{"text":"D","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 14 run data modify entity @s text set value '{"text":"E","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 15 run data modify entity @s text set value '{"text":"F","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 16_19()
				{
					execute if score @s hp_level matches 16 run data modify entity @s text set value '{"text":"G","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 17 run data modify entity @s text set value '{"text":"H","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 18 run data modify entity @s text set value '{"text":"I","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 19 run data modify entity @s text set value '{"text":"J","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 20_23()
				{
					execute if score @s hp_level matches 20 run data modify entity @s text set value '{"text":"K","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 21 run data modify entity @s text set value '{"text":"L","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 22 run data modify entity @s text set value '{"text":"M","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 23 run data modify entity @s text set value '{"text":"N","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 24_27()
				{
					execute if score @s hp_level matches 24 run data modify entity @s text set value '{"text":"O","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 25 run data modify entity @s text set value '{"text":"P","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 26 run data modify entity @s text set value '{"text":"Q","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 27 run data modify entity @s text set value '{"text":"R","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 28_31()
				{
					execute if score @s hp_level matches 28 run data modify entity @s text set value '{"text":"S","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 29 run data modify entity @s text set value '{"text":"T","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 30 run data modify entity @s text set value '{"text":"U","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 31 run data modify entity @s text set value '{"text":"V","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 32_35()
				{
					execute if score @s hp_level matches 32 run data modify entity @s text set value '{"text":"W","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 33 run data modify entity @s text set value '{"text":"X","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 34 run data modify entity @s text set value '{"text":"Y","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 35 run data modify entity @s text set value '{"text":"Z","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 36_39()
				{
					execute if score @s hp_level matches 36 run data modify entity @s text set value '{"text":"a","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 37 run data modify entity @s text set value '{"text":"b","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 38 run data modify entity @s text set value '{"text":"c","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 39 run data modify entity @s text set value '{"text":"d","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 40_43()
				{
					execute if score @s hp_level matches 40 run data modify entity @s text set value '{"text":"e","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 41 run data modify entity @s text set value '{"text":"f","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 42 run data modify entity @s text set value '{"text":"g","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 43 run data modify entity @s text set value '{"text":"h","font":"ui:player_hp","color":"#000016"}'
				}
				
				func 44_46()
				{
					execute if score @s hp_level matches 44 run data modify entity @s text set value '{"text":"i","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 45 run data modify entity @s text set value '{"text":"j","font":"ui:player_hp","color":"#000016"}'
					execute if score @s hp_level matches 46 run data modify entity @s text set value '{"text":"k","font":"ui:player_hp","color":"#000016"}'
				}
			}
		}
		
		func spawn()
		{
			summon interaction 250 0 0 {width:8,height:1,Tags:["player_pos"],Passengers:[{id:"minecraft:text_display",background:0,billboard:"center",Tags:["head_hp"]}]}
			data remove storage bar:main head[0]
			execute if data storage bar:main head[0] run function ARG(_PATH)spawn
		}
	}
	
	folder loop()
	{
		func schedule_1s()
		{
			execute as @a[tag=!hp_mp_varying] run function ARG(__PATH)update_bar/run
			execute as @a[tag=!dead] if score @s hp < @s hp_max run function ARG(__PATH)recover/hp
			
			execute store result score #DEAD_PLAYER_AMOUNT temp if entity @a[tag=dead]
			execute if score #TOMB_AMOUNT temp > #DEAD_PLAYER_AMOUNT temp as @e[type=item_display,tag=tomb] run function ARG(__PATH)death/tomb_check/find
		}
		
		func schedule_10s()
		{
			execute as @a[tag=!dead] if score @s mp < @s mp_max run function ARG(__PATH)recover/mp
		}
	}
}
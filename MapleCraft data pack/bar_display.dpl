namespace bar_display()
{
	func install()
	{
		scoreboard objectives add hp dummy
		scoreboard objectives add mp dummy
		scoreboard objectives add hp_max dummy
		scoreboard objectives add mp_max dummy
		
		scoreboard objectives add display_hp dummy
		scoreboard objectives add display_mp dummy
		scoreboard objectives add target_hp dummy
		scoreboard objectives add target_mp dummy
		
		scoreboard objectives add hp_vary_time dummy
		scoreboard objectives add mp_vary_time dummy
		scoreboard objectives add hp_flash dummy
		scoreboard objectives add mp_flash dummy
		scoreboard objectives add hp_warning trigger
		scoreboard objectives add mp_warning trigger
		
		scoreboard objectives add next_hp_recover dummy
		scoreboard objectives add hp_recovery dummy
		scoreboard objectives add mp_recovery dummy
		
		scoreboard objectives add damage_taken minecraft.custom:minecraft.damage_absorbed
	}
	
	func main()
	{
		execute as @a[scores={damage_taken=1..}] at @s run function ARG(_PATH)take_damage
		execute as @a[tag=hp_mp_varying] run function ARG(_PATH)bar_vary/check
	}
	
	folder defense_formula()
	{
		func weapon()
		{
			# base damage
			scoreboard players operation @s damage_taken *= @s damage_taken
			
			scoreboard players set #RANGE_MAX temp 51
			function main:rand_range
			scoreboard players operation #RAND_RANGE_RESULT temp *= @s damage_taken
			scoreboard players operation #RAND_RANGE_RESULT temp /= #100000 const
			
			scoreboard players operation @s damage_taken *= #8 const
			scoreboard players operation @s damage_taken /= #1000 const
			scoreboard players operation @s damage_taken += #RAND_RANGE_RESULT temp
			
			# set #C temp as C * 10000
			function ARG(_PATH)get_c/run
			
			# reduce by weapon defense times A
			scoreboard players operation #A temp = #C temp
			scoreboard players add #A temp 2800
			
			scoreboard players operation #GLOBAL temp = #WEAPON_DEFENSE temp
			scoreboard players operation #GLOBAL temp *= #A temp
			scoreboard players operation #GLOBAL temp /= #10000 const
			
			scoreboard players operation @s damage_taken -= #GLOBAL temp
			
			# set #B temp as B * 100000
			function ARG(_PATH)get_b/run
			
			# reduce by (weapon_defense - standard_pdd) * B
			scoreboard players operation #WEAPON_DEFENSE temp -= @s standard_pdd
			scoreboard players operation #WEAPON_DEFENSE temp *= #B temp
			scoreboard players operation #WEAPON_DEFENSE temp /= #100000 const
			
			scoreboard players operation @s damage_taken -= #WEAPON_DEFENSE temp
		}
		
		func magic()
		{
			# base damage
			scoreboard players operation @s damage_taken *= @s damage_taken
			
			scoreboard players set #RANGE_MAX temp 51
			function main:rand_range
			scoreboard players operation #RAND_RANGE_RESULT temp *= @s damage_taken
			scoreboard players operation #RAND_RANGE_RESULT temp /= #100000 const
			
			scoreboard players operation @s damage_taken *= #15 const
			scoreboard players operation @s damage_taken /= #2000 const
			scoreboard players operation @s damage_taken += #RAND_RANGE_RESULT temp
			
			# magic defense related
			scoreboard players operation #MAGIC_DEFENSE temp *= #25 const
			
			scoreboard players operation #GLOBAL STR = @s STR
			scoreboard players operation #GLOBAL STR *= #100 const
			scoreboard players operation #GLOBAL STR /= #28 const
			scoreboard players operation #MAGIC_DEFENSE temp += #GLOBAL STR
			
			scoreboard players operation #GLOBAL DEX = @s DEX
			scoreboard players operation #GLOBAL DEX *= #25 const
			scoreboard players operation #GLOBAL DEX /= #6 const
			scoreboard players operation #MAGIC_DEFENSE temp += #GLOBAL DEX
			
			scoreboard players operation #GLOBAL LUK = @s LUK
			scoreboard players operation #GLOBAL LUK *= #5 const
			scoreboard players operation #MAGIC_DEFENSE temp += #GLOBAL LUK
			
			execute if entity @s[tag=magician] run function ARG(_PATH)k_multiply
			scoreboard players operation #MAGIC_DEFENSE temp /= #100 const
			
			scoreboard players operation @s damage_taken -= #MAGIC_DEFENSE temp
		}
		
		folder get_c()
		{
			func run()
			{
				scoreboard players operation #C temp = @s STR
				scoreboard players operation #C temp *= #100 const
				
				scoreboard players operation #GLOBAL DEX = @s DEX
				scoreboard players operation #GLOBAL DEX *= #100 const
				
				execute if entity @s[tag=warrior] run function ARG(_PATH)warrior
				execute if entity @s[tag=!warrior] run function ARG(_PATH)non_warrior
				scoreboard players operation #C temp += #GLOBAL DEX
				
				scoreboard players operation #GLOBAL INT = @s INT
				scoreboard players operation #GLOBAL INT *= #50 const
				scoreboard players operation #GLOBAL INT /= #36 const
				scoreboard players operation #C temp += #GLOBAL INT
				
				scoreboard players operation #GLOBAL LUK = @s LUK
				scoreboard players operation #GLOBAL LUK *= #100 const
				scoreboard players operation #GLOBAL LUK /= #32 const
				scoreboard players operation #C temp += #GLOBAL LUK
			}
			
			func warrior()
			{
				scoreboard players operation #C temp /= #28 const
				scoreboard players operation #GLOBAL DEX /= #32 const
			}
			
			func non_warrior()
			{
				scoreboard players operation #C temp /= #20 const
				scoreboard players operation #GLOBAL DEX /= #28 const
			}
		}
		
		folder get_b()
		{
			func run()
			{
				execute if score #WEAPON_DEFENSE temp >= @s standard_pdd run function ARG(_PATH)wdef_greater
				execute unless score #WEAPON_DEFENSE temp >= @s standard_pdd run function ARG(_PATH)wdef_smaller
			}
			
			func wdef_greater()
			{
				scoreboard players operation #C temp *= #56 const
				scoreboard players operation #C temp /= #9 const
				
				scoreboard players operation #B temp = @s level
				scoreboard players operation #B temp *= #700 const
				scoreboard players operation #B temp /= #13 const
				
				scoreboard players operation #B temp += #C temp
				scoreboard players add #B temp 19600
			}
			
			func wdef_smaller()
			{
				scoreboard players operation #C temp *= #10 const
				
				scoreboard players operation #B temp = @s level
				scoreboard players operation #B temp *= #2000 const
				scoreboard players operation #B temp /= #11 const
				
				scoreboard players operation #B temp += #C temp
				scoreboard players add #B temp 28000
			}
		}
		
		func k_multiply()
		{
			scoreboard players operation #MAGIC_DEFENSE temp *= #6 const
			scoreboard players operation #MAGIC_DEFENSE temp /= #5 const
		}
	}
	
	func take_damage()
	{
		scoreboard players operation @s next_hp_recover = @s hp_recover_time
		
		scoreboard players operation #WEAPON_DEFENSE temp = @s weapon_defense
		scoreboard players operation #WEAPON_DEFENSE temp += @s wdef_buff_num
		function ARG(_PATH)defense_formula/weapon
		
		execute if score @s invin_buff_time matches 1.. run function skill:2/3/0/1003/guard
		execute if score @s refle_buff_time matches 1.. run function skill:1/1/0/1007/reflect
		scoreboard players operation @s damage_taken > #1 const
		
		scoreboard players set @s display_num_type 2
		scoreboard players operation @s display_number = @s damage_taken
		function number_display:show
		
		execute if score @s mguar_buff_time matches 1.. if score @s mp matches 1.. run function skill:2/0/0/1002/guard
		
		scoreboard players operation @s hp -= @s damage_taken
		execute if score @s hp matches ..0 run function ARG(_PATH)death/die
		function ARG(_PATH)change/hp/dropped
		
		effect give @s minecraft:absorption 1000000 255 true
		scoreboard players reset @s damage_taken
	}
	
	folder death()
	{
		func die()
		{
			gamemode spectator @s
			scoreboard players set @s hp 0
			tag @s add dead
			
			playsound minecraft:game.die player @a ~ ~ ~ 1 1 0
			
			scoreboard players operation #GLOBAL temp = @s number
			summon minecraft:vex ~ ~0.3 ~ {Tags:["dead_decoration","new"],PersistenceRequired:1,Silent:1,NoAI:1,CustomNameVisible:1,CustomName:'{"text":"2","font":"space:default"}',Invulnerable:1,Team:"player",HandItems:[{id:"minecraft:potion",Count:1b,tag:{CustomModelData:2}},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:2}}]}
			execute rotated ~ 0 run summon minecraft:armor_stand ~ ~1.7 ~ {Tags:["dead_decoration","new"],Motion:[0.0,-0.15,0.0],ActiveEffects:[{Id:28,Amplifier:0b,Duration:100000,Ambient:1b,ShowParticles:0b}],Silent:1,Invisible:1,Invulnerable:1,DisabledSlots:4144959,Team:"player",ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:3,CustomPotionColor:16777215}}]}
			
			data modify entity 00000000-0000-0001-0000-000000000001 DeathLootTable set value "bar_display:skull"
			loot replace entity @e[type=minecraft:vex,tag=new,limit=1] armor.head kill 00000000-0000-0001-0000-000000000001
			execute as @e[type=#bar_display:dead_decoration,tag=new] positioned as @s run function ARG(_PATH)set_dead_decoration
			
			forceload add ~ ~
			tellraw @s [{"text":"\uF82A\uF829\uF828\uF823","font":"space:default"},{"text":"2","font":"ui:main","clickEvent":{"action":"run_command","value":"/trigger command set 7"},"hoverEvent":{"action":"show_text","contents":{"text":"3","font":"ui:main"}}},{"text":"B94","font":"space:default"},{"text":"4","font":"ui:main"},"\n"]
		}
		
		func set_dead_decoration()
		{
			tp @s ~ ~ ~ ~ 0
			execute if entity @s[type=vex] run function ARG(_PATH)set_name
			scoreboard players operation @s number = #GLOBAL temp
			tag @s remove new
		}
		
		func set_name()
		{
			data modify storage bar:main name set from entity @s ArmorItems[3].tag.SkullOwner.Name
			data modify block 0 0 0 Text1 set value '{"nbt":"name","storage":"bar:main"}'
			data modify entity @s CustomName set from block 0 0 0 Text1
		}
		
		func revive()
		{
			scoreboard players operation #GLOBAL temp = @s number
			execute as @e[type=#bar_display:dead_decoration,tag=dead_decoration] if score @s number = #GLOBAL temp at @s run function ARG(_PATH)remove_dead_decoration
			execute as @e[type=#bar_display:dead_decoration,tag=dead_decoration] at @s run forceload add ~ ~
			
			scoreboard players set @s hp 50
			function ARG(__PATH)change/hp/recovered
			tag @s remove dead
			
			tellraw @s "\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n"
			title @s times 1 5 10
			title @s title {"text":"0","font":"ui:transition"}
			```
			raise Warning("TODO: teleport to the nearest town")
			```
			tp @s -80 4 55 -90 40
			
			gamemode adventure @s
		}
		
		func remove_dead_decoration()
		{
			execute if entity @s[type=vex] run forceload remove ~ ~
			tp @s[type=vex] ~ -10 ~
			kill @s
		}
	}
	
	folder change()
	{
		folder hp()
		{
			func dropped()
			{
				scoreboard players set #HP_DROPPED temp 1
				function ARG(_PATH)set
			}
			
			func recovered()
			{
				scoreboard players set #HP_DROPPED temp 0
				function ARG(_PATH)set
			}
		
			func set()
			{
				scoreboard players operation @s target_hp = @s hp
				scoreboard players operation @s target_hp *= #100 const
				scoreboard players operation @s target_hp /= @s hp_max
				
				scoreboard players set @s hp_vary_time 20
				tag @s add hp_mp_varying
				execute if score #HP_DROPPED temp matches 1 if score @s target_hp < @s hp_warning run scoreboard players set @s hp_flash 10
			}
		}
		
		folder mp()
		{
			func dropped()
			{
				scoreboard players set #MP_DROPPED temp 1
				function ARG(_PATH)set
			}
			
			func recovered()
			{
				scoreboard players set #MP_DROPPED temp 0
				function ARG(_PATH)set
			}
			
			func set()
			{
				scoreboard players operation @s target_mp = @s mp
				scoreboard players operation @s target_mp *= #100 const
				scoreboard players operation @s target_mp /= @s mp_max
				
				scoreboard players set @s mp_vary_time 20
				tag @s add hp_mp_varying
				execute if score #MP_DROPPED temp matches 1 if score @s target_mp < @s mp_warning run scoreboard players set @s mp_flash 10
			}
		}
		
	}
	
	folder bar_vary()
	{
		func check()
		{
			execute if score @s hp_vary_time matches 1.. run function ARG(_PATH)hp_vary
			execute if score @s mp_vary_time matches 1.. run function ARG(_PATH)mp_vary
			
			function ARG(__PATH)update_bar/run
			execute unless score @s hp_vary_time matches 1.. unless score @s mp_vary_time matches 1.. run tag @s remove hp_mp_varying
		}
		
		func hp_vary()
		{
			scoreboard players operation #GLOBAL temp = @s target_hp
			scoreboard players operation #GLOBAL temp -= @s display_hp
			execute if score @s hp_vary_time matches 15.. run scoreboard players operation #GLOBAL temp *= #2 const
			scoreboard players operation #GLOBAL temp /= @s hp_vary_time
			scoreboard players operation @s display_hp += #GLOBAL temp
			
			scoreboard players remove @s[scores={hp_flash=0..}] hp_flash 1
			scoreboard players remove @s hp_vary_time 1
			scoreboard players reset @s[scores={hp_vary_time=0}] hp_vary_time
		}
		
		func mp_vary()
		{
			scoreboard players operation #GLOBAL temp = @s target_mp
			scoreboard players operation #GLOBAL temp -= @s display_mp
			execute if score @s mp_vary_time matches 15.. run scoreboard players operation #GLOBAL temp *= #2 const
			scoreboard players operation #GLOBAL temp /= @s mp_vary_time
			scoreboard players operation @s display_mp += #GLOBAL temp
			
			scoreboard players remove @s[scores={mp_flash=0..}] mp_flash 1
			scoreboard players remove @s mp_vary_time 1
			scoreboard players reset @s[scores={mp_vary_time=0}] mp_vary_time
		}
	}
	
	folder update_bar()
	{
		func run()
		{
			data modify storage bar:main hp set value []
			scoreboard players operation #VALUE_TO_INSERT temp = @s display_hp
			function ARG(_PATH)insert_hp_digit
			data modify storage bar:main mp set value []
			scoreboard players operation #VALUE_TO_INSERT temp = @s display_mp
			function ARG(_PATH)insert_mp_digit
			
			execute unless score @s hp_flash matches 0.. run data modify storage bar:main hp_flash set value "-"
			execute unless score @s mp_flash matches 0.. run data modify storage bar:main mp_flash set value "-"
			execute if score @s hp_flash matches 0.. run execute store result storage bar:main hp_flash int 1 run scoreboard players get @s hp_flash
			execute if score @s mp_flash matches 0.. run execute store result storage bar:main mp_flash int 1 run scoreboard players get @s mp_flash
			
			title @s actionbar [{"text":"0","font":"ui:hp_mp"},{"text":"CA86","font":"space:default"},{"nbt":"hp[0]","storage":"bar:main","font":"ui:hp1"},{"text":"1","font":"space:default"},{"nbt":"hp[1]","storage":"bar:main","font":"ui:hp10"},{"nbt":"hp[2]","storage":"bar:main","font":"ui:hp100"},{"nbt":"hp[0]","storage":"bar:main","font":"ui:bar_back1"},{"nbt":"hp[1]","storage":"bar:main","font":"ui:bar_back10"},{"nbt":"hp[2]","storage":"bar:main","font":"ui:bar_back100"},{"text":"\uF828\uF827","font":"space:default"},[{"text":"[","font":"ui:bar_num"},{"score":{"name":"@s","objective":"hp"}},"/",{"score":{"name":"@s","objective":"hp_max"}},"]"],{"text":"87","font":"space:default"},[{"text":"[","font":"ui:bar_num_back"},{"score":{"name":"@s","objective":"hp"}},"/",{"score":{"name":"@s","objective":"hp_max"}},"]"],{"text":"\uF82B\uF829\uF828\uF821","font":"space:default"},{"nbt":"mp[0]","storage":"bar:main","font":"ui:mp1"},{"text":"1","font":"space:default"},{"nbt":"mp[1]","storage":"bar:main","font":"ui:mp10"},{"nbt":"mp[2]","storage":"bar:main","font":"ui:mp100"},{"nbt":"mp[0]","storage":"bar:main","font":"ui:bar_back1"},{"nbt":"mp[1]","storage":"bar:main","font":"ui:bar_back10"},{"nbt":"mp[2]","storage":"bar:main","font":"ui:bar_back100"},{"text":"\uF829\uF822","font":"space:default"},[{"text":"[","font":"ui:bar_num"},{"score":{"name":"@s","objective":"mp"}},"/",{"score":{"name":"@s","objective":"mp_max"}},"]"],{"text":"92","font":"space:default"},[{"text":"[","font":"ui:bar_num_back"},{"score":{"name":"@s","objective":"mp"}},"/",{"score":{"name":"@s","objective":"mp_max"}},"]"],{"text":"B97","font":"space:default"},{"text":"1","font":"ui:hp_mp"},{"text":"CA9","font":"space:default"},{"nbt":"hp_flash","storage":"bar:main","font":"ui:bar_flash"},{"text":"2","font":"space:default"},{"nbt":"mp_flash","storage":"bar:main","font":"ui:bar_flash"},{"text":"\uF826","font":"space:default"},{"nbt":"Attributes[{Name:\"minecraft:generic.luck\"}].Modifiers[].Name","entity":"@s","font":"skill:buff"},{"nbt":"Attributes[{Name:\"minecraft:generic.luck\"}].Modifiers[].Name","entity":"@s","font":"skill:buff_back"},{"text":"6","font":"space:default"}]
		}
		
		func insert_hp_digit()
		{
			data modify storage bar:main hp append value 0
			scoreboard players operation #GLOBAL temp = #VALUE_TO_INSERT temp
			execute store result storage bar:main hp[-1] int 1 run scoreboard players operation #GLOBAL temp %= #10 const
			
			scoreboard players operation #VALUE_TO_INSERT temp /= #10 const
			execute if score #VALUE_TO_INSERT temp matches 1.. run function ARG(_PATH)insert_hp_digit
		}
		
		func insert_mp_digit()
		{
			data modify storage bar:main mp append value 0
			scoreboard players operation #GLOBAL temp = #VALUE_TO_INSERT temp
			execute store result storage bar:main mp[-1] int 1 run scoreboard players operation #GLOBAL temp %= #10 const
			
			scoreboard players operation #VALUE_TO_INSERT temp /= #10 const
			execute if score #VALUE_TO_INSERT temp matches 1.. run function ARG(_PATH)insert_mp_digit
		}
	}
	
	folder recover()
	{
		func mp()
		{
			scoreboard players operation @s mp += @s mp_recovery
			scoreboard players operation @s mp < @s mp_max
			function ARG(__PATH)change/mp/recovered
		}
		
		func hp()
		{
			scoreboard players remove @s next_hp_recover 1
			execute if score @s next_hp_recover matches 0 at @s run function ARG(_PATH)do_recover_hp
		}
		
		func do_recover_hp()
		{
			scoreboard players operation @s next_hp_recover = @s hp_recover_time
			scoreboard players operation @s hp += @s hp_recovery
			scoreboard players operation @s hp < @s hp_max
			
			scoreboard players set @s display_num_type 3
			scoreboard players operation @s display_number = @s hp_recovery
			function number_display:show
			
			function ARG(__PATH)change/hp/recovered
		}
	}
	
	folder loop()
	{
		func schedule_1s()
		{
			execute as @a[tag=!hp_mp_varying] run function ARG(__PATH)update_bar/run
			execute as @a[tag=!dead] if score @s hp < @s hp_max run function ARG(__PATH)recover/hp
		}
		
		func schedule_10s()
		{
			execute as @a[tag=!dead] if score @s mp < @s mp_max run function ARG(__PATH)recover/mp
		}
		
		func schedule_1d()
		{
			effect give @a minecraft:absorption 1000000 255 true
		}
	}
}
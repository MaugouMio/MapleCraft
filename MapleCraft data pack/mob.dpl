```
with open(PROJ_DIR + "/../mobs.py", "r", encoding="utf8") as f:
	exec(f.read())

MOB_UUID = 0
```
namespace mob()
{
	func install()
	{
		scoreboard objectives add spawn_alpha dummy
		scoreboard objectives add skill_delay dummy
		scoreboard objectives add anger_time dummy
		
		scoreboard objectives add uuid0 dummy
		scoreboard objectives add uuid1 dummy
		scoreboard objectives add uuid2 dummy
		scoreboard objectives add uuid3 dummy
	}
	
	folder setting()
	{
		func check()
		{
			scoreboard players add @s spawn_alpha 1
			execute if score @s[type=zombified_piglin] spawn_alpha matches 1 run function ARG(_PATH)set_model
			execute if score @s[type=zombified_piglin] spawn_alpha matches ..9 store result entity @s ArmorItems[3].tag.CustomPotionColor int 657930 run scoreboard players get @s spawn_alpha
			execute if score @s spawn_alpha matches 10 run function ARG(_PATH)run
		}
		
		func set_model()
		{
			execute if data entity @s ArmorItems[3].tag.info.move_frame store result score @s move_frame run data get entity @s ArmorItems[3].tag.info.move_frame
			execute if data entity @s ArmorItems[3].tag.info.stand_frame store result score @s stand_frame run data get entity @s ArmorItems[3].tag.info.stand_frame
			execute store result score @s base_model run data get entity @s ArmorItems[3].tag.CustomModelData
			
			data remove entity @s ArmorItems[3].tag.info
		}
		
		func run()
		{
			execute if entity @s[type=ocelot] run function ARG(_PATH)set_stats
			execute if entity @s[type=zombified_piglin] run function ARG(_PATH)piglin_set
			execute if entity @s[type=magma_cube,tag=!no_body_attack] run function ARG(_PATH)magma_cube_set
			scoreboard players operation @s number = #MOB_ID number
			
			scoreboard players set @s spawn_alpha 10
			tag @s remove new_summon
			tag @s remove new_enemy
			tag @s add enemy
		}
		
		func set_stats()
		{
			execute store result score @s level run data get entity @s ArmorItems[3].tag.info.level
			scoreboard players operation #LEVEL temp = @s level
			
			execute store result score @s hp_max run data get entity @s ArmorItems[3].tag.info.hp_max
			scoreboard players operation @s hp = @s hp_max
			execute store result score @s mp_max run data get entity @s ArmorItems[3].tag.info.mp_max
			scoreboard players operation @s mp = @s mp_max
			
			execute store result score @s base_accuracy run data get entity @s ArmorItems[3].tag.info.accuracy
			scoreboard players operation #ACCURACY temp = @s base_accuracy
			
			execute store result score @s avoidability run data get entity @s ArmorItems[3].tag.info.avoidability
			execute if data entity @s ArmorItems[3].tag.info.magic_attack store result score @s weapon_mattack run data get entity @s ArmorItems[3].tag.info.magic_attack
			execute store result score @s weapon_defense run data get entity @s ArmorItems[3].tag.info.weapon_defense
			execute store result score @s magic_defense run data get entity @s ArmorItems[3].tag.info.magic_defense
			execute store result score @s kb run data get entity @s ArmorItems[3].tag.info.kb
			execute store result score @s speed run data get entity @s ArmorItems[3].tag.info.speed
			execute store result score @s exp run data get entity @s ArmorItems[3].tag.info.exp
			execute store result score @s default_width run data get entity @s ArmorItems[3].tag.info.width
			execute store result score @s default_height run data get entity @s ArmorItems[3].tag.info.height
			execute store result score @s hurt_sound run data get entity @s ArmorItems[3].tag.info.hurt_sound
			execute store result score @s die_sound run data get entity @s ArmorItems[3].tag.info.die_sound
			
			execute if data entity @s ArmorItems[3].tag.info.skill_id store result score @s skill_id run data get entity @s ArmorItems[3].tag.info.skill_id
			scoreboard players operation #SKILL_ID temp = @s skill_id
			
			data remove entity @s ArmorItems[3].tag.info
			
			# scoreboard players set @s status 0
			scoreboard players set @s hit_distance 0
			# scoreboard players set @s[tag=!no_jump] rand_jump 60
			scoreboard players add #MOB_ID number 1
		}
		
		func piglin_set()
		{
			# 召喚的怪物不會經過淡入流程，這邊要再做一次模型設定
			execute unless score @s base_model matches 1.. run function ARG(_PATH)set_model
			data modify entity @s ArmorItems[3].tag.CustomPotionColor set value -1
			
			# 同步技能 id 設定
			scoreboard players operation @s skill_id = #SKILL_ID temp
		}
		
		func magma_cube_set()
		{
			function ARG(_PATH)level_tag/add
			function ARG(_PATH)accuracy_tag/set
			data modify entity @s NoAI set value 0
		}
		
		folder level_tag()
		{
			func add()
			{
				execute if score #LEVEL temp matches 128.. run function ARG(_PATH)lv128
				execute if score #LEVEL temp matches 64.. run function ARG(_PATH)lv64
				execute if score #LEVEL temp matches 32.. run function ARG(_PATH)lv32
				execute if score #LEVEL temp matches 16.. run function ARG(_PATH)lv16
				execute if score #LEVEL temp matches 8.. run function ARG(_PATH)lv8
				execute if score #LEVEL temp matches 4.. run function ARG(_PATH)lv4
				execute if score #LEVEL temp matches 2.. run function ARG(_PATH)lv2
				execute if score #LEVEL temp matches 1.. run function ARG(_PATH)lv1
			}
			
			func lv128()
			{
				tag @s add lvbit128
				scoreboard players remove #LEVEL temp 128
			}
			
			func lv64()
			{
				tag @s add lvbit64
				scoreboard players remove #LEVEL temp 64
			}
			
			func lv32()
			{
				tag @s add lvbit32
				scoreboard players remove #LEVEL temp 32
			}
			
			func lv16()
			{
				tag @s add lvbit16
				scoreboard players remove #LEVEL temp 16
			}
			
			func lv8()
			{
				tag @s add lvbit8
				scoreboard players remove #LEVEL temp 8
			}
			
			func lv4()
			{
				tag @s add lvbit4
				scoreboard players remove #LEVEL temp 4
			}
			
			func lv2()
			{
				tag @s add lvbit2
				scoreboard players remove #LEVEL temp 2
			}
			
			func lv1()
			{
				tag @s add lvbit1
				scoreboard players remove #LEVEL temp 1
			}
		}
		
		folder accuracy_tag()
		{
			func set()
			{
				tag @s remove accbit256
				tag @s remove accbit128
				tag @s remove accbit64
				tag @s remove accbit32
				tag @s remove accbit16
				tag @s remove accbit8
				tag @s remove accbit4
				tag @s remove accbit2
				tag @s remove accbit1
				
				execute if score #ACCURACY temp matches 256.. run function ARG(_PATH)lv256
				execute if score #ACCURACY temp matches 128.. run function ARG(_PATH)lv128
				execute if score #ACCURACY temp matches 64.. run function ARG(_PATH)lv64
				execute if score #ACCURACY temp matches 32.. run function ARG(_PATH)lv32
				execute if score #ACCURACY temp matches 16.. run function ARG(_PATH)lv16
				execute if score #ACCURACY temp matches 8.. run function ARG(_PATH)lv8
				execute if score #ACCURACY temp matches 4.. run function ARG(_PATH)lv4
				execute if score #ACCURACY temp matches 2.. run function ARG(_PATH)lv2
				execute if score #ACCURACY temp matches 1.. run function ARG(_PATH)lv1
			}
			
			func lv256()
			{
				tag @s add accbit256
				scoreboard players remove #ACCURACY temp 256
			}
			
			func lv128()
			{
				tag @s add accbit128
				scoreboard players remove #ACCURACY temp 128
			}
			
			func lv64()
			{
				tag @s add accbit64
				scoreboard players remove #ACCURACY temp 64
			}
			
			func lv32()
			{
				tag @s add accbit32
				scoreboard players remove #ACCURACY temp 32
			}
			
			func lv16()
			{
				tag @s add accbit16
				scoreboard players remove #ACCURACY temp 16
			}
			
			func lv8()
			{
				tag @s add accbit8
				scoreboard players remove #ACCURACY temp 8
			}
			
			func lv4()
			{
				tag @s add accbit4
				scoreboard players remove #ACCURACY temp 4
			}
			
			func lv2()
			{
				tag @s add accbit2
				scoreboard players remove #ACCURACY temp 2
			}
			
			func lv1()
			{
				tag @s add accbit1
				scoreboard players remove #ACCURACY temp 1
			}
		}
	}
	
	func main()
	{
		execute as @e[type=#skill:enemy,tag=new_enemy] run function ARG(_PATH)setting/check
		execute as @e[type=#skill:enemy_ai,tag=enemy,scores={skill_id=1..}] run function ARG(_PATH)skills/update
	}
	
	folder loop()
	{
		func schedule_1s()
		{
			execute as @e[type=ocelot] if score @s anger_time matches 1.. run function ARG(_PATH)anger_countdown/run
		}
		
		folder anger_countdown()
		{
			func run()
			{
				scoreboard players remove @s anger_time 1
				execute if score @s anger_time matches 0 run function ARG(_PATH)reset
			}
			
			func reset()
			{
				scoreboard players operation #GLOBAL temp = @s number
				execute as @e[type=zombified_piglin] if score @s number = #GLOBAL temp run attribute @s minecraft:generic.follow_range base set 0.0
				scoreboard players reset @s anger_time
			}
		}
		
		func schedule_10s()
		{
			function random:rand_rate
			execute as @e[type=marker,tag=mob_spawner] at @s run data modify block ~ ~ ~ auto set value 1b
			
			execute as @e[type=ocelot] run data modify entity @s Age set value -2147483648
		}
	}
	
	folder map()
	{
		folder maple_island()
		{
			func tutorial()
			{
				```
				spawn_positions = ["-1 31 497","-5 31 500","-3 31 502"]
				for pos in spawn_positions:
					print(f"execute positioned {pos} run", spawn_mob("2230101", uuid=MOB_UUID))
					MOB_UUID += 1
				```
			}
		}
		
		folder party_quest()
		{
			folder kpq()
			{
				folder stage1()
				{
					func main()
					{
						```
						spawn_positions = ["-280.4 51 1.5","-285.2 51 -4.5","-277.2 57 -9","-269 56 -4","-286.0 61 9.4","-275 63 -2.0","-277.0 70 -11.0","-273 69 -6.0","-273.0 75 -5","-277.8 76 2.2","-290 61 18","-295.0 62 19.3","-295.7 68 11.2","-290.6 69 7.3","-287.9 73 12.7","-291.0 73 19.2","-299.3 71 19.9","-301.9 77 22","-299.0 78 23","-287.3 78 13.4","-284.2 79 17"]
						for pos in spawn_positions:
							print(f"execute positioned {pos} run", spawn_mob("9300001", uuid=MOB_UUID))
							MOB_UUID += 1
						```
						execute if score #RAND_RESULT number matches ..24 run function ARG(_PATH)rand0
						execute if score #RAND_RESULT number matches 25..49 run function ARG(_PATH)rand1
						execute if score #RAND_RESULT number matches 50..74 run function ARG(_PATH)rand2
						execute if score #RAND_RESULT number matches 75.. run function ARG(_PATH)rand3
					}
					
					```
					spawn_positions_a = [("-271.0 56 -4.0","-282.76 51 -2.0"),("-273.7 69 -7.3","-276 75 -2"),("-287.4 69 6.0","-286.6 73 11.7"),("-300.2 78 22.9","-285.4 79 15.4")]
					spawn_positions_b = [("-282.4 51 -1.6","-290 61 8"),("-278 63 1","-275 69 -9"),("-292.2 61 20.7","-296.9 68 13.0"),("-289.6 73 21","-303.2 72 21")]
					rand_pairs = [[],[],[],[]]
					for pos in spawn_positions_a:
						for i in range(4):
							rand_pairs[i].append((pos[i//2], MOB_UUID))
						MOB_UUID += 1
					for pos in spawn_positions_b:
						for i in range(4):
							rand_pairs[i].append((pos[i%2], MOB_UUID))
						MOB_UUID += 1
					
					#####################################
					
					for i in range(4):
						print(f"func rand{i}()")
						print("{")
						for j in range(len(spawn_positions_a) + len(spawn_positions_b)):
							print(f"execute positioned {rand_pairs[i][j][0]} run", spawn_mob("9300001", uuid=rand_pairs[i][j][1]))
						print("}")
					```
				}
				
				folder stage6()
				{
					func main()
					{
						```
						g_mush_spawn_positions = ["-1820 42 4","-1826 42 5","-1839 42 4","-1824 47 -3","-1841 47 1","-1819.0 52 3.0","-1834 52 5","-1841 52 1","-1828 57 2","-1830 57 -3","-1843 57 0"]
						for pos in g_mush_spawn_positions:
							print(f"execute positioned {pos} run", spawn_mob("1110100", uuid=MOB_UUID))
							MOB_UUID += 1
							
						h_mush_spawn_positions = ["-1824 42 1","-1829 42 3","-1842 42 6","-1842.0 42 1","-1840 42 2","-1837 42 0","-1821 47 -3.0","-1826 47 -1","-1828 47 -4","-1830 47 -2","-1832.0 47 -3","-1842 47 0","-1844 47 -2.0","-1843 47 -4","-1839.0 47 -3.0","-1822 52 6","-1824 52 4","-1822 52 1","-1832 52 3","-1835.0 52 1","-1838 52 4","-1842 52 5","-1821 57 1","-1823 57 -3","-1826 57 -1","-1832 57 0","-1841 57 3","-1839 57 -1","-1841.0 57 -3"]
						for pos in h_mush_spawn_positions:
							print(f"execute positioned {pos} run", spawn_mob("2110200", uuid=MOB_UUID))
							MOB_UUID += 1
						```
					}
				}
			}
		}
	}
	
	folder skills()
	{
		folder king_slime()
		{
			func check()
			{
				execute unless score @s kb_time matches 1.. if score @s[nbt={OnGround:1b}] anger_time matches 1.. at @s run function ARG(_PATH)skill_check
			}
			
			func skill_check()
			{
				scoreboard players set #GLOBAL temp 0
				execute if score @s hp matches ..4000 unless entity @s[tag=skill_used] store result score #GLOBAL temp run function ARG(_PATH)skill
				execute if score #GLOBAL temp matches 0 run function ARG(_PATH)attack_check
			}
			
			func skill()
			{
				# 放招時刷新憤怒時間
				function skill:rand_anger_time
				
				scoreboard players set @s action_time 26
				scoreboard players set @s kb_time 24
				scoreboard players set #KB_TIME temp 24
				scoreboard players set @s status 4
				scoreboard players set #STATUS temp 4
				scoreboard players reset #ACTION_FRAME_S temp
				scoreboard players set #FORCE_MODEL temp 10083
				tag @s add skill_used
				
				playsound minecraft:mob.special.9300003_skill player @a ~ ~ ~ 1 1 0
				execute store result score @s skill_delay run schedule function ARG(_PATH)run_flash 8t append
			}
			
			func run_flash()
			{
				execute store result score #GLOBAL temp run time query gametime
				execute as @e[type=ocelot,tag=!dead,scores={skill_id=1,status=4}] if score @s skill_delay = #GLOBAL temp at @s run function ARG(_PATH)flash
			}
			
			func flash()
			{
				particle minecraft:flash ~ ~7 ~ 0 0 0 1 1 force
				particle minecraft:explosion ~ ~7 ~ 0 0 0 0 1 force
				
				scoreboard players set @s status 5
				execute store result score @s skill_delay run schedule function ARG(_PATH)call_summon 16t append
			}
			
			func call_summon()
			{
				execute store result score #GLOBAL temp run time query gametime
				execute as @e[type=ocelot,tag=!dead,scores={skill_id=1,status=5}] if score @s skill_delay = #GLOBAL temp at @s positioned ~ ~2 ~ run function ARG(_PATH)summon
			}
			
			func summon()
			{
				```
				print(spawn_mob("0210100", is_summon=True, prefix="Motion:[0.26,0.5,0.15],"))
				print(spawn_mob("0210100", is_summon=True, prefix="Motion:[0.0,0.5,-0.3],"))
				print(spawn_mob("0210100", is_summon=True, prefix="Motion:[-0.26,0.5,0.15],"))
				```
				execute as @e[type=#skill:enemy,tag=new_summon] run function mob:setting/run
			}
			
			func attack_check()
			{
				scoreboard players set #EXIST_TARGET temp 0
				
				function ARG(__PATH)write_target_uuid
				execute as @a[gamemode=adventure,tag=!dead,distance=..7.5] positioned ~-20 ~-0.5 ~-20 if entity @s[dx=40,dy=3,dz=40] run function ARG(__PATH)check_is_target
				
				execute if score #EXIST_TARGET temp matches 1 if score @s mp matches 2.. at @s run function ARG(_PATH)attack
			}
			
			func attack()
			{
				scoreboard players remove @s mp 2
				
				# 放招時刷新憤怒時間
				function skill:rand_anger_time
				
				scoreboard players set @s action_time 35
				scoreboard players set @s kb_time 33
				scoreboard players set #KB_TIME temp 33
				scoreboard players set @s status 6
				scoreboard players set #STATUS temp 6
				scoreboard players set #ACTION_FRAME_S temp 34
				scoreboard players reset #FORCE_MODEL temp
				
				playsound minecraft:mob.special.9300003_attack player @a ~ ~ ~ 1 1 0
				execute store result score @s skill_delay run schedule function ARG(_PATH)spawn_quake 20t append
			}
			
			func spawn_quake()
			{
				execute store result score #GLOBAL temp run time query gametime
				execute as @e[type=ocelot,tag=!dead,scores={skill_id=1,status=6}] if score @s skill_delay = #GLOBAL temp at @s run function ARG(_PATH)quake
			}
			
			func quake()
			{
				particle minecraft:explosion_emitter ~ ~ ~ 0 0 0 1 1 force
				particle minecraft:cloud ~ ~0.2 ~ 2 0 2 0.15 30 force
				
				scoreboard players set #SKILL_HIT temp 0
				scoreboard players operation #MAGIC_ATTACK temp = @s weapon_mattack
				scoreboard players operation #ATTACKER_ACC temp = @s base_accuracy
				execute as @a[gamemode=adventure,tag=!dead,distance=..7.5] positioned ~-20 ~-0.5 ~-20 if entity @s[dx=40,dy=3,dz=40] if data entity @s {OnGround:1b} unless score @s hurt_time matches 1.. at @s run function ARG(_PATH)hit
				execute if score #SKILL_HIT temp matches 1 run playsound minecraft:mob.special.9300003_char_damage player @a ~ ~ ~ 1 1 0
			}
			
			func hit()
			{
				scoreboard players set @s attacker_level 40
				scoreboard players operation @s attacker_acc = #ATTACKER_ACC temp
				scoreboard players operation @s damage_taken = #MAGIC_ATTACK temp
				function bar_display:take_damage/physic/check
				scoreboard players reset @s attacker_level
				scoreboard players reset @s attacker_acc
				
				# 給一點角色受傷的動作
				execute if score @s damage_taken matches 1.. run effect give @s minecraft:instant_damage 1 0 true
				
				scoreboard players set #SKILL_HIT temp 1
			}
		}
		
		folder rombot()
		{
			func check()
			{
				execute unless score @s kb_time matches 1.. if score @s anger_time matches 1.. at @s run function ARG(_PATH)check_summon
			}
			
			func check_summon()
			{
				scoreboard players set #GLOBAL temp 0
				execute if entity @s[tag=ldpq_mob,tag=!skill_used] run function ARG(_PATH)try_summon
				execute if score #GLOBAL temp matches 0 run function ARG(_PATH)check_attack
			}
			
			func try_summon()
			{
				function random:rand_rate
				execute if score #RAND_RESULT number matches ..19 run function ARG(_PATH)cast_summon
			}
			
			func cast_summon()
			{
				# 放招時刷新憤怒時間
				function skill:rand_anger_time
				
				scoreboard players set @s action_time 28
				scoreboard players set @s kb_time 18
				scoreboard players set #KB_TIME temp 18
				scoreboard players set @s status 4
				scoreboard players set #STATUS temp 4
				scoreboard players set #ACTION_FRAME_S temp 44
				scoreboard players reset #FORCE_MODEL temp
				
				execute store result score @s skill_delay run schedule function ARG(_PATH)call_summon 18t append
				scoreboard players set #GLOBAL temp 1
				tag @s add skill_used
			}
			
			func call_summon()
			{
				execute store result score #GLOBAL temp run time query gametime
				execute as @e[type=ocelot,tag=!dead,scores={skill_id=2,status=4}] if score @s skill_delay = #GLOBAL temp at @s run function ARG(_PATH)summon
			}
			
			func summon()
			{
				summon marker ~ ~ ~ {Tags:["rand_pos"]}
				summon marker ~ ~ ~ {Tags:["rand_pos"]}
				```
				raise Warning("TODO: set block golem random spawn position under height")
				```
				spreadplayers ~ ~ 0 4 under 10 false @e[type=marker,tag=rand_pos]
				```
				print("execute at @e[type=marker,tag=rand_pos] run", spawn_mob("9300009", is_summon=True))
				```
				kill @e[tag=rand_pos]
				
				execute as @e[type=#skill:enemy,tag=new_summon] at @s run function ARG(_PATH)summon_setting
			}
			
			func summon_setting()
			{
				function mob:setting/run
				execute if entity @s[type=ocelot] run particle minecraft:explosion ~ ~1 ~ 0 0 0 1 3 force
			}
			
			func check_attack()
			{
				scoreboard players set #EXIST_TARGET temp 0
				
				function ARG(__PATH)write_target_uuid
				execute as @a[gamemode=adventure,tag=!dead,distance=..7.5] positioned ~-20 ~-0.5 ~-20 if entity @s[dx=40,dy=3,dz=40] run function ARG(__PATH)check_is_target
				
				execute if score #EXIST_TARGET temp matches 1 at @s run function ARG(_PATH)cast_attack
			}
			
			func cast_attack()
			{
				# 放招時刷新憤怒時間
				function skill:rand_anger_time
				
				scoreboard players set @s action_time 39
				scoreboard players set @s kb_time 37
				scoreboard players set #KB_TIME temp 37
				scoreboard players set @s status 5
				scoreboard players set #STATUS temp 5
				scoreboard players set #ACTION_FRAME_S temp 62
				scoreboard players reset #FORCE_MODEL temp
				
				playsound minecraft:mob.special.4130103_attack player @a ~ ~ ~ 1 1 0
				execute store result score @s skill_delay run schedule function ARG(_PATH)call_attack 22t append
			}
			
			func call_attack()
			{
				execute store result score #GLOBAL temp run time query gametime
				execute as @e[type=ocelot,tag=!dead,scores={skill_id=2,status=5}] if score @s skill_delay = #GLOBAL temp at @s run function ARG(_PATH)attack
			}
			
			func attack()
			{
				execute rotated ~ 0 run particle minecraft:explosion ^1.8 ^ ^2 0 0 0 3 3 force
				
				scoreboard players operation #MAGIC_ATTACK temp = @s weapon_mattack
				scoreboard players operation #ATTACKER_ACC temp = @s base_accuracy
				execute as @a[gamemode=adventure,tag=!dead,distance=..7.5] positioned ~-20 ~-0.5 ~-20 if entity @s[dx=40,dy=3,dz=40] unless score @s hurt_time matches 1.. if data entity @s {OnGround:1b} at @s run function ARG(_PATH)hit
			}
			
			func hit()
			{
				scoreboard players set @s attacker_level 47
				scoreboard players operation @s attacker_acc = #ATTACKER_ACC temp
				scoreboard players operation @s damage_taken = #MAGIC_ATTACK temp
				function bar_display:take_damage/physic/check
				scoreboard players reset @s attacker_level
				scoreboard players reset @s attacker_acc
				
				# 給一點角色受傷的動作
				execute if score @s damage_taken matches 1.. run effect give @s minecraft:instant_damage 1 0 true
				
				function skill:summon_font_effect/9000000rombot_hit
			}
		}
		
		folder platoon_chronos()
		{
			func check()
			{
				execute unless score @s kb_time matches 1.. if score @s anger_time matches 1.. at @s run function ARG(_PATH)check_attack
			}
			
			func check_attack()
			{
				scoreboard players set #EXIST_TARGET temp 0
				
				function ARG(__PATH)write_target_uuid
				# 使用 check_is_target_mark 要記得 kill 00000000-0000-0002-0000-000000000002
				execute as @a[gamemode=adventure,tag=!dead,distance=..6] positioned ~-20 ~-0.3 ~-20 if entity @s[dx=40,dy=1,dz=40] run function ARG(__PATH)check_is_target_mark
				
				execute if score #EXIST_TARGET temp matches 1 at @s run function ARG(_PATH)cast_attack
			}
			
			func cast_attack()
			{
				# 放招時刷新憤怒時間
				function skill:rand_anger_time
				
				# 紀錄攻擊目標 UUID
				function ARG(__PATH)record_target_uuid
				
				# 強制面向目標
				execute facing entity 00000000-0000-0002-0000-000000000002 feet run tp @s ~ ~ ~ ~ 0
				kill 00000000-0000-0002-0000-000000000002
				
				scoreboard players set @s action_time 37
				scoreboard players set @s kb_time 35
				scoreboard players set #KB_TIME temp 35
				scoreboard players set @s status 4
				scoreboard players set #STATUS temp 4
				scoreboard players set #ACTION_FRAME_S temp 34
				scoreboard players reset #FORCE_MODEL temp
				
				execute at @s rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^0.5 {Tags:["model_animation","new"],Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,Fire:32767s,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:900000}}]}
				execute at @s as @e[type=armor_stand,tag=new,limit=1] run function ARG(_PATH)effect_model_set
				
				playsound minecraft:mob.special.4230114_attack player @a ~ ~ ~ 1 1 0
				execute store result score @s skill_delay run schedule function ARG(_PATH)call_attack 16t append
			}
			
			func effect_model_set()
			{
				execute positioned as @s run tp @s ~ ~ ~ ~ 0
				scoreboard players set @s max_life 26
				scoreboard players set @s base_model 900000
				tag @s remove new
			}
			
			func call_attack()
			{
				execute store result score #GLOBAL temp run time query gametime
				execute as @e[type=ocelot,tag=!dead,scores={skill_id=3,status=4}] if score @s skill_delay = #GLOBAL temp at @s run function ARG(_PATH)attack
			}
			
			func attack()
			{
				scoreboard players operation #MAGIC_ATTACK temp = @s weapon_mattack
				scoreboard players operation #ATTACKER_ACC temp = @s base_accuracy
				# 被瞄準的目標一定寫過 uuid 系列分數，直接判斷就好
				function ARG(__PATH)write_record_uuid
				execute as @a[gamemode=adventure,tag=!dead,distance=..30] unless score @s hurt_time matches 1.. if score @s uuid0 = #GLOBAL uuid0 if score @s uuid1 = #GLOBAL uuid1 if score @s uuid2 = #GLOBAL uuid2 if score @s uuid3 = #GLOBAL uuid3 at @s run function ARG(_PATH)hit
			}
			
			func hit()
			{
				scoreboard players set @s attacker_level 41
				scoreboard players operation @s attacker_acc = #ATTACKER_ACC temp
				scoreboard players operation @s damage_taken = #MAGIC_ATTACK temp
				function bar_display:take_damage/magic/check
				scoreboard players reset @s attacker_level
				scoreboard players reset @s attacker_acc
				
				# 給一點角色受傷的動作
				execute if score @s damage_taken matches 1.. run effect give @s minecraft:instant_damage 1 0 true
				
				playsound minecraft:mob.special.4230114_char_damage player @a ~ ~ ~ 1 1 0
				function skill:summon_font_effect/9000000platoon_chronos_hit
			}
		}
		
		folder master_chronos()
		{
			func check()
			{
				execute unless score @s kb_time matches 1.. if score @s anger_time matches 1.. at @s run function ARG(_PATH)check_attack
			}
			
			func check_attack()
			{
				scoreboard players set #EXIST_TARGET temp 0
				
				function ARG(__PATH)write_target_uuid
				# 使用 check_is_target_mark 要記得 kill 00000000-0000-0002-0000-000000000002
				execute as @a[gamemode=adventure,tag=!dead,distance=..6] positioned ~-20 ~-0.3 ~-20 if entity @s[dx=40,dy=1,dz=40] run function ARG(__PATH)check_is_target_mark
				
				execute if score #EXIST_TARGET temp matches 1 at @s run function ARG(_PATH)cast_attack
			}
			
			func cast_attack()
			{
				# 放招時刷新憤怒時間
				function skill:rand_anger_time
				
				# 紀錄攻擊目標 UUID
				function ARG(__PATH)record_target_uuid
				
				# 強制面向目標
				execute facing entity 00000000-0000-0002-0000-000000000002 feet run tp @s ~ ~ ~ ~ 0
				kill 00000000-0000-0002-0000-000000000002
				
				scoreboard players set @s action_time 37
				scoreboard players set @s kb_time 35
				scoreboard players set #KB_TIME temp 35
				scoreboard players set @s status 4
				scoreboard players set #STATUS temp 4
				scoreboard players set #ACTION_FRAME_S temp 34
				scoreboard players reset #FORCE_MODEL temp
				
				execute at @s rotated ~ 0 run summon minecraft:armor_stand ^ ^ ^0.5 {Tags:["model_animation","new"],Invisible:1,Invulnerable:1,NoGravity:1,Marker:1,Fire:32767s,ArmorItems:[{},{},{},{id:"minecraft:potion",Count:1b,tag:{CustomModelData:900026}}]}
				execute at @s as @e[type=armor_stand,tag=new,limit=1] run function ARG(_PATH)effect_model_set
				
				playsound minecraft:mob.special.4230115_attack player @a ~ ~ ~ 1 1 0
				execute store result score @s skill_delay run schedule function ARG(_PATH)call_attack 16t append
			}
			
			func effect_model_set()
			{
				execute positioned as @s run tp @s ~ ~ ~ ~ 0
				scoreboard players set @s max_life 26
				scoreboard players set @s base_model 900026
				tag @s remove new
			}
			
			func call_attack()
			{
				execute store result score #GLOBAL temp run time query gametime
				execute as @e[type=ocelot,tag=!dead,scores={skill_id=4,status=4}] if score @s skill_delay = #GLOBAL temp at @s run function ARG(_PATH)attack
			}
			
			func attack()
			{
				scoreboard players operation #MAGIC_ATTACK temp = @s weapon_mattack
				scoreboard players operation #ATTACKER_ACC temp = @s base_accuracy
				# 被瞄準的目標一定寫過 uuid 系列分數，直接判斷就好
				function ARG(__PATH)write_record_uuid
				execute as @a[gamemode=adventure,tag=!dead,distance=..30] unless score @s hurt_time matches 1.. if score @s uuid0 = #GLOBAL uuid0 if score @s uuid1 = #GLOBAL uuid1 if score @s uuid2 = #GLOBAL uuid2 if score @s uuid3 = #GLOBAL uuid3 at @s run function ARG(_PATH)hit
			}
			
			func hit()
			{
				scoreboard players set @s attacker_level 46
				scoreboard players operation @s attacker_acc = #ATTACKER_ACC temp
				scoreboard players operation @s damage_taken = #MAGIC_ATTACK temp
				function bar_display:take_damage/magic/check
				scoreboard players reset @s attacker_level
				scoreboard players reset @s attacker_acc
				
				# 給一點角色受傷的動作
				execute if score @s damage_taken matches 1.. run effect give @s minecraft:instant_damage 1 0 true
				
				playsound minecraft:mob.special.4230115_char_damage player @a ~ ~ ~ 1 1 0
				function skill:summon_font_effect/9000000master_chronos_hit
			}
		}
		
		func update()
		{
			execute if score @s action_time matches 1.. run scoreboard players remove @s action_time 1
			
			execute if entity @s[type=ocelot] run scoreboard players set #KB_TIME temp 0
			execute if entity @s[type=ocelot,tag=!dead] unless score @s action_time matches 1.. run function ARG(_PATH)check
			execute if entity @s[type=zombified_piglin] if score #KB_TIME temp matches 1.. run function ARG(_PATH)sync_score
		}
		
		func sync_score()
		{
			execute at @s run tp @s ~ ~ ~ ~ 0
			data modify entity @s NoAI set value 1b

			scoreboard players operation @s kb_time = #KB_TIME temp
			scoreboard players operation @s status = #STATUS temp
			scoreboard players set @s action_frame -1
			scoreboard players operation @s action_frame_s = #ACTION_FRAME_S temp
			
			scoreboard players reset @s action_frame_max
			execute if score #ACTION_FRAME_S temp matches 1.. run scoreboard players operation @s action_frame_max = #KB_TIME temp
			
			execute if score #FORCE_MODEL temp matches 1.. store result entity @s ArmorItems[3].tag.CustomModelData int 1 run scoreboard players get #FORCE_MODEL temp
		}
		
		func write_target_uuid()
		{
			execute store result score #GLOBAL uuid0 run data get entity @s Passengers[0].AngryAt[0]
			execute store result score #GLOBAL uuid1 run data get entity @s Passengers[0].AngryAt[1]
			execute store result score #GLOBAL uuid2 run data get entity @s Passengers[0].AngryAt[2]
			execute store result score #GLOBAL uuid3 run data get entity @s Passengers[0].AngryAt[3]
		}
		
		func record_target_uuid()
		{
			scoreboard players operation @s uuid0 = #GLOBAL uuid0
			scoreboard players operation @s uuid1 = #GLOBAL uuid1
			scoreboard players operation @s uuid2 = #GLOBAL uuid2
			scoreboard players operation @s uuid3 = #GLOBAL uuid3
		}
		
		func write_record_uuid()
		{
			scoreboard players operation #GLOBAL uuid0 = @s uuid0
			scoreboard players operation #GLOBAL uuid1 = @s uuid1
			scoreboard players operation #GLOBAL uuid2 = @s uuid2
			scoreboard players operation #GLOBAL uuid3 = @s uuid3
		}
		
		func write_uuid()
		{
			execute store result score @s uuid0 run data get entity @s UUID[0]
			execute store result score @s uuid1 run data get entity @s UUID[1]
			execute store result score @s uuid2 run data get entity @s UUID[2]
			execute store result score @s uuid3 run data get entity @s UUID[3]
		}
		
		func check_is_target()
		{
			execute unless score @s uuid0 = @s uuid0 run function ARG(_PATH)write_uuid
			execute if score @s uuid0 = #GLOBAL uuid0 if score @s uuid1 = #GLOBAL uuid1 if score @s uuid2 = #GLOBAL uuid2 if score @s uuid3 = #GLOBAL uuid3 run scoreboard players set #EXIST_TARGET temp 1
		}
		
		func check_is_target_mark()
		{
			function ARG(_PATH)check_is_target
			execute if score #EXIST_TARGET temp matches 1 at @s run summon marker ~ ~ ~ {UUID:[I;0,2,0,2]}
		}
		
		```
		def binary_search_mob_skill(skill_list, start, end):
			if len(skill_list) == 1:
				print("func check()")
				print("{")
				print(f"\texecute if score @s skill_id matches {skill_list[0][0]} run function ARG(_PATH){skill_list[0][1]}")
				print("}")
				return
			
			if start == end:
				return
			
			mid = (start + end) // 2
			mid_right = mid + 1
			
			if start == 0 and end == len(skill_list) - 1:
				print("func check()")
			else:
				print(f"func {start}_{end}()")
			print("{")
			if start == mid:
				print(f"\texecute if score @s skill_id matches {skill_list[start][0]} run function ARG(_PATH){skill_list[start][1]}")
			else:
				print(f"\texecute if score @s skill_id matches {skill_list[start][0]}..{skill_list[mid][0]} run function ARG(_PATH){start}_{mid}")
			if mid_right == end:
				print(f"\texecute if score @s skill_id matches {skill_list[end][0]} run function ARG(_PATH){skill_list[end][1]}")
			else:
				print(f"\texecute if score @s skill_id matches {skill_list[mid_right][0]}..{skill_list[end][0]} run function ARG(_PATH){mid_right}_{end}")
			print("}")
			
			binary_search_mob_skill(skill_list, start, mid)
			binary_search_mob_skill(skill_list, mid_right, end)
		
		with open(PROJ_DIR + "/../mob_skills.csv") as f:
			mob_skills = f.read().split("\n")
			for i in range(len(mob_skills)):
				sep_point = mob_skills[i].find(",")
				mob_skills[i] = [mob_skills[i][:sep_point], mob_skills[i][(sep_point+1):]]
		
		binary_search_mob_skill(mob_skills, 0, len(mob_skills) - 1)
		```
	}
	
	func test()
	{
		```
		# 殭屍菇菇
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("2230101"))
		# 鱷魚 KPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300001"))
		# 青蛇 KPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300000"))
		# 風獨眼獸 KPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300002"))
		# 超級綠水靈
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300003"))
		# 綠水靈
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("0210100"))
		# 綠菇菇
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("1110100"))
		# 刺菇菇
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("2110200"))
		# 小石球 新手訓練
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300018"))
		# 白色發條鼠 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300005"))
		# 黑色發條鼠 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300006"))
		# 機械章魚 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300007"))
		# 次元之哀目
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300008"))
		# 次元之怒目
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300014"))
		# 積木泥人王 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300013"))
		# 積木泥人 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300009"))
		# 泥人領導者 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300010"))
		# 妖魔 LDPQ
		print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300015"))
		# 進化妖魔 LDPQ
		print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300016"))
		# 妖魔隊長 LDPQ
		print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300017"))
		```
	}
}
import loop
import random

namespace item()
{
	func install()
	{
		scoreboard objectives add pick_meso minecraft.picked_up:minecraft.diamond
		scoreboard objectives add pick_etc minecraft.picked_up:minecraft.string
		scoreboard objectives add pick_weapon minecraft.picked_up:minecraft.diamond_axe
		scoreboard objectives add pick_consume minecraft.picked_up:minecraft.warped_fungus_on_a_stick
		
		scoreboard objectives add right_click_item minecraft.used:minecraft.warped_fungus_on_a_stick
		scoreboard objectives add bullet_item dummy
		scoreboard objectives add bullet_model dummy
		scoreboard objectives add bullet_damage dummy
		scoreboard objectives add bullet_amount dummy
		
		scoreboard objectives add weapon_type dummy
		scoreboard objectives add weapon_attack dummy
		scoreboard objectives add weapon_mattack dummy
		scoreboard objectives add weapon_knock dummy
		scoreboard objectives add weapon_speed dummy
		scoreboard objectives add equip_accuracy dummy
		scoreboard objectives add equip_str dummy
		scoreboard objectives add equip_dex dummy
		scoreboard objectives add equip_int dummy
		scoreboard objectives add equip_luk dummy
	}
	
	folder loop()
	{
		func schedule_1s()
		{
			execute as @e[type=item,tag=!checked] at @s run function ARG(__PATH)entity/check
		}
	}
	
	func main()
	{
		# used item check
		execute as @a[scores={right_click_item=1..},tag=!dead] at @s run function ARG(_PATH)use/check
		# pick item check
		execute as @a[predicate=item:pickup] at @s run function ARG(_PATH)pick/check
	}
	
	folder entity()
	{
		func check()
		{
			execute if entity @s[tag=loot_item] run function ARG(_PATH)free_loot/check
			execute if entity @s[tag=!loot_item] run function ARG(_PATH)new
		}
		
		func new()
		{
			# set meso
			execute if data entity @s Item{id:"minecraft:diamond"} store result entity @s Item.tag.CustomModelData int 1 run data get entity @s Item.tag.AttributeModifiers[0].Amount 1000
			# set skill item and display weapon
			execute if data entity @s Item{id:"minecraft:carrot_on_a_stick"} run data modify entity @s Owner set from entity @s Thrower
			execute if data entity @s Item{id:"minecraft:diamond_axe"} run data modify entity @s Owner set from entity @s Thrower
			# set loot item
			execute unless data entity @s Thrower unless data entity @s Owner run function ARG(_PATH)loot
			
			data modify entity @s Age set value 2400
			tag @s[tag=!loot_item] add checked
		}
		
		func loot()
		{
			data modify entity @s Owner set from storage skill:main damage_info.attacker
			playsound minecraft:game.drop_item player @a ~ ~ ~ 1 1 0
			tag @s add loot_item
		}
	
		folder free_loot()
		{
			func check()
			{
				execute store result score #GLOBAL temp run data get entity @s Age
				execute if score #GLOBAL temp matches 4800.. run function ARG(_PATH)free
			}
			
			func free()
			{
				data modify storage item:main item set from entity @s Item
				summon minecraft:item ~ ~ ~ {Item:{id:"minecraft:potion",Count:1b,tag:{CustomModelData:1}},Age:4800,Tags:["checked","new"]}
				execute as @e[type=item,tag=new,limit=1] run function ARG(_PATH)respawn
				kill @s
			}
			
			func respawn()
			{
				data modify entity @s Item set from storage item:main item
				tag @s remove new
			}
		}
	}
	
	folder pick()
	{
		func check()
		{
			execute if score @s pick_meso matches 1.. run function ARG(_PATH)meso/record
			scoreboard players reset @s[scores={pick_etc=1..}] pick_etc
			scoreboard players reset @s[scores={pick_weapon=1..}] pick_weapon
			scoreboard players reset @s[scores={pick_consume=1..}] pick_consume
			
			playsound minecraft:game.pick_up_item player @a ~ ~ ~ 1 1 0
		}
		
		folder meso() from for_loop().for("@s pick_meso", "")
		{
			func record()
			{
				data modify storage main:item picked_meso append from entity @s Inventory[{id:"minecraft:diamond"}].tag.CustomModelData
				function ARG(_PATH)run
				
				clear @s minecraft:diamond
				scoreboard players reset @s pick_meso
			}
			
			func execute()
			{
				execute store result score #GLOBAL temp run data get storage main:item picked_meso[0]
				data remove storage main:item picked_meso[0]
				
				scoreboard players operation @s meso += #GLOBAL temp
			}
		}
		
		func etc()
		{
			scoreboard players reset @s pick_etc
		}
	}
	
	folder use()
	{
		func check()
		{
			execute store result score #USING_ITEM temp run data get entity @s[nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick"}}] SelectedItem.tag.CustomModelData
			
			execute if score #USING_ITEM temp matches ..499 run function ARG(_PATH)consume/check
			execute if score #USING_ITEM temp matches 10001.. unless score @s action_time matches 1.. run function ARG(_PATH)weapon/check
			
			scoreboard players reset @s right_click_item
		}
		
		folder equip_numeric()
		{
			func apply()
			{
				# need to store item info in storage item:main equip
				execute store result score #GLOBAL temp run data get storage item:main equip.hp
				scoreboard players operation @s hp_max_base += #GLOBAL temp
				execute store result score #GLOBAL temp run data get storage item:main equip.mp
				scoreboard players operation @s mp_max_base += #GLOBAL temp
				execute unless score @s hyper_buff_time matches 1.. run function ARG(_PATH)update_hp_mp
				execute if score @s hyper_buff_time matches 1.. run function skill:1/3/0/1007/set_hp_mp
				
				function bar_display:change/hp/recovered
				function bar_display:change/mp/recovered
				
				execute store result score #GLOBAL temp run data get storage item:main equip.defense
				scoreboard players operation @s weapon_defense += #GLOBAL temp
				execute store result score #GLOBAL temp run data get storage item:main equip.magic_defense
				scoreboard players operation @s magic_defense += #GLOBAL temp
				execute store result score #GLOBAL temp run data get storage item:main equip.accuracy
				scoreboard players operation @s equip_accuracy += #GLOBAL temp
				
				execute store result score #GLOBAL temp run data get storage item:main equip.speed
				execute store result block 0 1 0 Items[0].tag.AttributeModifiers[0].Amount float 0.01 run scoreboard players operation @s speed += #GLOBAL temp
				
				execute store result score #GLOBAL temp run data get storage item:main equip.str
				scoreboard players operation @s STR += #GLOBAL temp
				scoreboard players operation @s equip_str += #GLOBAL temp
				execute store result score #GLOBAL temp run data get storage item:main equip.dex
				scoreboard players operation @s DEX += #GLOBAL temp
				scoreboard players operation @s equip_dex += #GLOBAL temp
				execute store result score #GLOBAL temp run data get storage item:main equip.int
				scoreboard players operation @s INT += #GLOBAL temp
				scoreboard players operation @s equip_int += #GLOBAL temp
				execute store result score #GLOBAL temp run data get storage item:main equip.luk
				scoreboard players operation @s LUK += #GLOBAL temp
				scoreboard players operation @s equip_luk += #GLOBAL temp
			}
			
			func update_hp_mp()
			{
				scoreboard players operation @s hp_max = @s hp_max_base
				scoreboard players operation @s hp_max < #30000 const
				scoreboard players operation @s hp < @s hp_max
				scoreboard players operation @s mp_max = @s mp_max_base
				scoreboard players operation @s mp_max < #30000 const
				scoreboard players operation @s mp < @s mp_max
			}
			
			func remove()
			{
				# removed item is stored in block 0 0 0 Items[0]
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.hp
				scoreboard players operation @s hp_max_base -= #GLOBAL temp
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.mp
				scoreboard players operation @s mp_max_base -= #GLOBAL temp
				execute unless score @s hyper_buff_time matches 1.. run function ARG(_PATH)update_hp_mp
				execute if score @s hyper_buff_time matches 1.. run function skill:1/3/0/1007/set_hp_mp
				
				function bar_display:change/hp/recovered
				function bar_display:change/mp/recovered
				
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.defense
				scoreboard players operation @s weapon_defense -= #GLOBAL temp
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.magic_defense
				scoreboard players operation @s magic_defense -= #GLOBAL temp
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.accuracy
				scoreboard players operation @s equip_accuracy -= #GLOBAL temp
				
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.str
				scoreboard players operation @s STR -= #GLOBAL temp
				scoreboard players operation @s equip_str -= #GLOBAL temp
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.dex
				scoreboard players operation @s DEX -= #GLOBAL temp
				scoreboard players operation @s equip_dex -= #GLOBAL temp
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.int
				scoreboard players operation @s INT -= #GLOBAL temp
				scoreboard players operation @s equip_int -= #GLOBAL temp
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.luk
				scoreboard players operation @s LUK -= #GLOBAL temp
				scoreboard players operation @s equip_luk -= #GLOBAL temp
				
				execute store result score #GLOBAL temp run data get block 0 1 0 Items[0].tag.speed
				
				# reset speed attribute modifier
				replaceitem block 0 1 0 container.0 minecraft:potion
				data modify block 0 1 0 Items[0].tag set from entity @s Inventory[{Slot:103b}].tag
				execute store result block 0 1 0 Items[0].tag.AttributeModifiers[0].Amount float 0.01 run scoreboard players operation @s speed -= #GLOBAL temp
				loot replace entity @s armor.head 1 mine 0 1 0 minecraft:air{modify_player:1b}
			}
		}
		
		func equip_qulification()
		{
			# job check
			execute if entity @s[tag=!warrior,tag=!archer,tag=!magician,tag=!thief] store result score #ACCEPTED_EQUIP temp run data get entity @s SelectedItem.tag.job[0]
			execute if entity @s[tag=warrior] store result score #ACCEPTED_EQUIP temp run data get entity @s SelectedItem.tag.job[1]
			execute if entity @s[tag=archer] store result score #ACCEPTED_EQUIP temp run data get entity @s SelectedItem.tag.job[2]
			execute if entity @s[tag=magician] store result score #ACCEPTED_EQUIP temp run data get entity @s SelectedItem.tag.job[3]
			execute if entity @s[tag=thief] store result score #ACCEPTED_EQUIP temp run data get entity @s SelectedItem.tag.job[4]
			# level check
			execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.level
			execute if score @s level < #GLOBAL temp run scoreboard players set #ACCEPTED_EQUIP temp 0
			# stats check
			execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.stats[0]
			execute if score @s STR < #GLOBAL temp run scoreboard players set #ACCEPTED_EQUIP temp 0
			execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.stats[1]
			execute if score @s DEX < #GLOBAL temp run scoreboard players set #ACCEPTED_EQUIP temp 0
			execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.stats[2]
			execute if score @s INT < #GLOBAL temp run scoreboard players set #ACCEPTED_EQUIP temp 0
			execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.stats[3]
			execute if score @s LUK < #GLOBAL temp run scoreboard players set #ACCEPTED_EQUIP temp 0
			
			execute if score #ACCEPTED_EQUIP temp matches 0 run tellraw @s {"translate":"warning.equip.invalid_stat","color":"red"}
		}
		
		folder weapon()
		{
			func check()
			{
				function ARG(__PATH)equip_qulification
				execute if score #ACCEPTED_EQUIP temp matches 0 run playsound minecraft:game.dlg_notice player @s
				execute if score #ACCEPTED_EQUIP temp matches 1 run function ARG(_PATH)equip
			}
			
			func equip()
			{
				playsound minecraft:item.equip player @s
				
				# clear display weapon
				clear @s diamond_axe
				
				data modify storage item:main equip set from entity @s SelectedItem.tag
				tellraw @s {"translate":"item.notice.equip_weapon","with":[{"nbt":"equip.display.Name","storage":"item:main","interpret":true}]}
				
				# give weapon for display
				replaceitem block 0 1 0 container.0 minecraft:diamond_axe
				data modify block 0 1 0 Items[0].tag set from entity @s SelectedItem.tag
				data modify block 0 1 0 Items[0].tag.display.Lore set value ['{"translate":"item.notice.for_display"}']
				loot replace entity @s weapon.mainhand 1 mine 0 1 0 minecraft:air{modify_player:1b}
				
				# replace original weapon
				execute if score @s weapon_type matches 1.. run function ARG(_PATH)remove
				
				# equip new weapon
				replaceitem block 0 1 0 container.0 minecraft:potion
				data modify block 0 1 0 Items[0].tag set from entity @s Inventory[{Slot:103b}].tag
				data modify block 0 1 0 Items[0].tag.weapon set from storage item:main equip
				
				execute store result score @s weapon_type run data get storage item:main equip.weapon_type
				execute store result score @s weapon_attack run data get storage item:main equip.weapon_attack
				execute store result score @s weapon_mattack run data get storage item:main equip.magic_attack
				execute store result score @s weapon_speed run data get storage item:main equip.action_time
				execute store result score @s weapon_knock run data get storage item:main equip.knockback
				function ARG(__PATH)equip_numeric/apply
				
				loot replace entity @s armor.head 1 mine 0 1 0 minecraft:air{modify_player:1b}
				
				execute if score @s boost_buff_time matches 1.. run function skill:buff/boost/reset/run
			}
			
			func remove()
			{
				replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick
				data modify block 0 1 0 Items[0].tag set from entity @s Inventory[{Slot:103b}].tag.weapon
				loot give @s mine 0 1 0 minecraft:air{modify_player:1b}
				
				function ARG(__PATH)equip_numeric/remove
				scoreboard players reset @s weapon_type
			}
		}
		
		folder consume()
		{
			func check()
			{
				execute if score #USING_ITEM temp matches ..30 unless score @s action_time matches 1.. run function ARG(_PATH)weapon/check
				execute if score #USING_ITEM temp matches 100.. unless score @s invis_buff_time matches 1.. run function ARG(_PATH)potion/check
			}
			
			folder potion()
			{
				func check()
				{
					# use sound check
					execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.sound
					execute if score #GLOBAL temp matches 0 run playsound minecraft:item.potion player @s
					
					# potion type check
					execute if score #USING_ITEM temp matches ..299 run function ARG(_PATH)recover/check
					execute if score #USING_ITEM temp matches 300.. run function ARG(_PATH)buff/check
					
					# potion consume blongs here
				}
				
				folder recover()
				{
					func check()
					{
						execute if score #USING_ITEM temp matches 100..169 if score @s hp < @s hp_max run function ARG(_PATH)hp
						execute if score #USING_ITEM temp matches 130..199 if score @s mp < @s mp_max run function ARG(_PATH)mp
						execute if score #USING_ITEM temp matches 200..269 if score @s hp < @s hp_max run function ARG(_PATH)hp_rate
						execute if score #USING_ITEM temp matches 230..299 if score @s mp < @s mp_max run function ARG(_PATH)mp_rate
					}
					
					func hp()
					{
						execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.hp
						scoreboard players operation @s hp += #GLOBAL temp
						scoreboard players operation @s hp < @s hp_max
						
						function bar_display:change/hp/recovered
					}
					
					func mp()
					{
						execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.mp
						scoreboard players operation @s mp += #GLOBAL temp
						scoreboard players operation @s mp < @s mp_max
						
						function bar_display:change/mp/recovered
					}
					
					func hp_rate()
					{
						execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.hp_rate
						scoreboard players operation #GLOBAL temp *= @s hp_max
						scoreboard players operation #GLOBAL temp /= #100 const
						
						scoreboard players operation @s hp += #GLOBAL temp
						scoreboard players operation @s hp < @s hp_max
						
						function bar_display:change/hp/recovered
					}
					
					func mp_rate()
					{
						execute store result score #GLOBAL temp run data get entity @s SelectedItem.tag.mp_rate
						scoreboard players operation #GLOBAL temp *= @s mp_max
						scoreboard players operation #GLOBAL temp /= #100 const
						
						scoreboard players operation @s mp += #GLOBAL temp
						scoreboard players operation @s mp < @s mp_max
						
						function bar_display:change/mp/recovered
					}
				}
				
				folder buff()
				{
					func check()
					{
						
					}
				}
			}
			
			folder weapon()
			{
				func check()
				{
					execute store result score @s bullet_model run data get entity @s SelectedItem.tag.bullet_model
					execute store result score @s bullet_damage run data get entity @s SelectedItem.tag.Enchantments[0].lvl
					execute store result score #GLOBAL bullet_amount run data get entity @s SelectedItem.tag.amount
					tellraw @s {"translate":"item.notice.used_weapon","with":[{"nbt":"SelectedItem.tag.display.Name","entity":"@s","interpret":true},{"score":{"name":"#GLOBAL","objective":"bullet_amount"}}]}
					
					playsound minecraft:item.equip player @s
					replaceitem entity @s weapon.mainhand minecraft:air
					execute if score @s bullet_item matches 1.. run function ARG(_PATH)restore/check
					
					scoreboard players operation @s bullet_amount = #GLOBAL bullet_amount
					scoreboard players operation @s bullet_item = #USING_ITEM temp
				}
				
				folder restore()
				{
					func check()
					{
						execute if score @s bullet_item matches ..20 run function ARG(_PATH)throwing_star
						execute if score @s bullet_item matches 21..25 if score @s bullet_amount matches 1.. run function ARG(_PATH)arrow_bow
						execute if score @s bullet_item matches 26..30 if score @s bullet_amount matches 1.. run function ARG(_PATH)arrow_crossbow
						
						scoreboard players reset @s bullet_item
					}
					
					func give()
					{
						execute store result block 0 1 0 Items[0].tag.amount int 1 run scoreboard players operation #GLOBAL temp = @s bullet_amount
						data modify block 0 0 0 Text1 set value '{"translate":"item.notice.weapon.left","with":[{"score":{"name":"#GLOBAL","objective":"temp"}}]}'
						data modify block 0 1 0 Items[0].tag.display.Lore[0] set from block 0 0 0 Text1
						loot give @s mine 0 1 0 minecraft:air{modify_player:1b}
					}
					
					func throwing_star()
					{
						# nbt is stored in google cloud sheet
						execute if score @s bullet_item matches 1 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:1,amount:500,bullet_model:4000,max_amount:500,unit_price:0.8f,display:{Name:'{"translate":"item.consume.2070000"}',Lore:['{"translate":"item.notice.weapon.left","with":["500"]}']},Enchantments:[{id:"minecraft:fortune",lvl:15}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 2 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:2,amount:500,bullet_model:4001,max_amount:500,unit_price:0.7f,display:{Name:'{"translate":"item.consume.2070001"}',Lore:['{"translate":"item.notice.weapon.left","with":["500"]}']},Enchantments:[{id:"minecraft:fortune",lvl:17}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 3 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:3,amount:700,bullet_model:4002,max_amount:700,unit_price:0.6f,display:{Name:'{"translate":"item.consume.2070002"}',Lore:['{"translate":"item.notice.weapon.left","with":["700"]}']},Enchantments:[{id:"minecraft:fortune",lvl:19}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 4 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:4,amount:500,bullet_model:4003,max_amount:500,unit_price:0.6f,display:{Name:'{"translate":"item.consume.2070003"}',Lore:['{"translate":"item.notice.weapon.left","with":["500"]}']},Enchantments:[{id:"minecraft:fortune",lvl:21}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 5 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:5,amount:1000,bullet_model:4004,max_amount:1000,unit_price:0.5f,display:{Name:'{"translate":"item.consume.2070004"}',Lore:['{"translate":"item.notice.weapon.left","with":["1000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:23}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 6 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:6,amount:1000,bullet_model:4005,max_amount:1000,unit_price:0.4f,display:{Name:'{"translate":"item.consume.2070005"}',Lore:['{"translate":"item.notice.weapon.left","with":["1000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:25}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 7 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:7,amount:800,bullet_model:4006,max_amount:800,unit_price:0.3f,display:{Name:'{"translate":"item.consume.2070006"}',Lore:['{"translate":"item.notice.weapon.left","with":["800"]}']},Enchantments:[{id:"minecraft:fortune",lvl:27}],HideFlags:126,Unbreakable:1}
						
						function ARG(_PATH)give
					}
					
					func arrow_bow()
					{
						# nbt is stored in google cloud sheet
						execute if score @s bullet_item matches 21 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:21,amount:2000,bullet_model:3101,display:{Name:'{"translate":"item.consume.2060000"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 22 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:22,amount:2000,bullet_model:3102,display:{Name:'{"translate":"item.consume.2060001"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:1}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 23 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:23,amount:2000,bullet_model:3103,display:{Name:'{"translate":"item.consume.2060002"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:2}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 24 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:24,amount:2000,bullet_model:3104,display:{Name:'{"translate":"item.consume.2060003"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:4}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 25 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:25,amount:2000,bullet_model:3105,display:{Name:'{"translate":"item.consume.2060004"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:4}],HideFlags:126,Unbreakable:1}
						
						function ARG(_PATH)give
					}
					
					func arrow_crossbow()
					{
						# nbt is stored in google cloud sheet
						execute if score @s bullet_item matches 26 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:26,amount:2000,bullet_model:3201,display:{Name:'{"translate":"item.consume.2061000"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 27 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:27,amount:2000,bullet_model:3202,display:{Name:'{"translate":"item.consume.2061001"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:1}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 28 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:28,amount:2000,bullet_model:3203,display:{Name:'{"translate":"item.consume.2061002"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:2}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 29 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:29,amount:2000,bullet_model:3204,display:{Name:'{"translate":"item.consume.2061003"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:4}],HideFlags:126,Unbreakable:1}
						execute if score @s bullet_item matches 30 run replaceitem block 0 1 0 container.0 minecraft:warped_fungus_on_a_stick{CustomModelData:30,amount:2000,bullet_model:3205,display:{Name:'{"translate":"item.consume.2061004"}',Lore:['{"translate":"item.notice.weapon.left","with":["2000"]}']},Enchantments:[{id:"minecraft:fortune",lvl:4}],HideFlags:126,Unbreakable:1}
						
						function ARG(_PATH)give
					}
				}
			}
		}
	}
}
namespace number_display()
{
	func install()
	{	
		scoreboard objectives add display_number dummy
		scoreboard objectives add display_num_type dummy
	}
	
	func show()
	{
		scoreboard players operation #GLOBAL display_number = @s display_number
		scoreboard players operation #GLOBAL display_num_type = @s display_num_type
		execute summon text_display run function ARG(_PATH)set_display
	}
	
	func show_miss()
	{
		execute unless score #GLOBAL display_num_type matches 2.. run data modify entity @s text set value '{"text":"m","font":"number:red"}'
		execute if score #GLOBAL display_num_type matches 2 run data modify entity @s text set value '{"text":"m","font":"number:violet"}'
	}
	
	func show_number()
	{
		data modify storage number:main display set value []
		function ARG(_PATH)insert_digit
		
		# number:main -> display = [6, num1, 6, num2, 6, num3, ..., num6]
		# display_num_type = {X: red, 1: crit, 2: violet, 3: blue}
		execute unless score #GLOBAL display_num_type matches 1.. run data modify entity @s text set value '["",{"nbt":"display[1]","storage":"number:main","font":"number:red_head"},{"nbt":"display[2]","storage":"number:main","font":"space:default"},{"nbt":"display[3]","storage":"number:main","font":"number:red_lower"},{"nbt":"display[4]","storage":"number:main","font":"space:default"},{"nbt":"display[5]","storage":"number:main","font":"number:red"},{"nbt":"display[6]","storage":"number:main","font":"space:default"},{"nbt":"display[7]","storage":"number:main","font":"number:red_lower"},{"nbt":"display[8]","storage":"number:main","font":"space:default"},{"nbt":"display[9]","storage":"number:main","font":"number:red"},{"nbt":"display[10]","storage":"number:main","font":"space:default"},{"nbt":"display[11]","storage":"number:main","font":"number:red"}]'
		execute if score #GLOBAL display_num_type matches 1 run data modify entity @s text set value '["",{"text":"-","font":"number:crit_head"},{"text":"9","font":"space:default"},{"nbt":"display[1]","storage":"number:main","font":"number:crit_head"},{"nbt":"display[2]","storage":"number:main","font":"space:default"},{"nbt":"display[3]","storage":"number:main","font":"number:crit_lower"},{"nbt":"display[4]","storage":"number:main","font":"space:default"},{"nbt":"display[5]","storage":"number:main","font":"number:crit"},{"nbt":"display[6]","storage":"number:main","font":"space:default"},{"nbt":"display[7]","storage":"number:main","font":"number:crit_lower"},{"nbt":"display[8]","storage":"number:main","font":"space:default"},{"nbt":"display[9]","storage":"number:main","font":"number:crit"},{"nbt":"display[10]","storage":"number:main","font":"space:default"},{"nbt":"display[11]","storage":"number:main","font":"number:crit"}]'
		execute if score #GLOBAL display_num_type matches 2 run data modify entity @s text set value '["",{"nbt":"display[1]","storage":"number:main","font":"number:violet_head"},{"nbt":"display[2]","storage":"number:main","font":"space:default"},{"nbt":"display[3]","storage":"number:main","font":"number:violet_lower"},{"nbt":"display[4]","storage":"number:main","font":"space:default"},{"nbt":"display[5]","storage":"number:main","font":"number:violet"},{"nbt":"display[6]","storage":"number:main","font":"space:default"},{"nbt":"display[7]","storage":"number:main","font":"number:violet_lower"},{"nbt":"display[8]","storage":"number:main","font":"space:default"},{"nbt":"display[9]","storage":"number:main","font":"number:violet"}]'
		execute if score #GLOBAL display_num_type matches 3 run data modify entity @s text set value '["",{"nbt":"display[1]","storage":"number:main","font":"number:blue_head"},{"nbt":"display[2]","storage":"number:main","font":"space:default"},{"nbt":"display[3]","storage":"number:main","font":"number:blue_lower"},{"nbt":"display[4]","storage":"number:main","font":"space:default"},{"nbt":"display[5]","storage":"number:main","font":"number:blue"},{"nbt":"display[6]","storage":"number:main","font":"space:default"},{"nbt":"display[7]","storage":"number:main","font":"number:blue_lower"},{"nbt":"display[8]","storage":"number:main","font":"space:default"},{"nbt":"display[9]","storage":"number:main","font":"number:blue"}]'
	}
	
	func insert_digit()
	{
		scoreboard players operation #GLOBAL temp = #GLOBAL display_number
		scoreboard players operation #GLOBAL temp %= #10 const
		
		data modify storage number:main display prepend value 0
		execute store result storage number:main display[0] int 1 run scoreboard players get #GLOBAL temp
		# larger overlap on crit numbers
		execute unless score #GLOBAL display_num_type matches 1 run data modify storage number:main display prepend value 6
		execute if score #GLOBAL display_num_type matches 1 run data modify storage number:main display prepend value 7
		
		scoreboard players operation #GLOBAL display_number /= #10 const
		execute if score #GLOBAL display_number matches 1.. run function ARG(_PATH)insert_digit
	}
	
	func set_display()
	{
		data merge entity @s {Tags:["display_number"],billboard:"center",background:0}
		
		execute if score #GLOBAL display_number matches 0 run function ARG(_PATH)show_miss
		execute if score #GLOBAL display_number matches 1.. run function ARG(_PATH)show_number
		
		execute store result score #GLOBAL temp run time query gametime
		scoreboard players operation #GLOBAL temp %= #24000 const
		scoreboard players operation #GLOBAL temp %= #22 const
		# 0 ~ 3 都會被當成 255，故意跳過這個區間
		execute store result entity @s text_opacity byte 1 run scoreboard players add #GLOBAL temp 4
		
		execute if score #NUMBER_HEIGHT temp = #NUMBER_HEIGHT temp run function ARG(_PATH)set_pos
		
		execute store result score @s life run schedule function ARG(_PATH)remove 21t append
	}
	
	func set_pos()
	{
		execute store result score #GLOBAL temp run data get entity @s Pos[1] 100
		scoreboard players operation #GLOBAL temp += #NUMBER_HEIGHT temp
		execute store result entity @s Pos[1] double 0.01 run scoreboard players add #GLOBAL temp 70
		scoreboard players reset #NUMBER_HEIGHT temp
	}
	
	func remove()
	{
		execute store result score #GLOBAL temp run time query gametime
		execute as @e[type=text_display,tag=display_number] if score @s life <= #GLOBAL temp run kill @s
	}
}
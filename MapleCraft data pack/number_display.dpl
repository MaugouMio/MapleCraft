namespace number_display()
{
	func install()
	{	
		scoreboard objectives add display_number dummy
		
		forceload add 0 0
		fill -1 0 -1 1 1 1 minecraft:bedrock
		setblock 0 0 0 minecraft:acacia_wall_sign
	}
	
	func show()
	{
		data modify storage number:main display set value []
		function ARG(_PATH)insert_digit
		
		# number:main -> display = [6, num1, 6, num2, 6, num3, ..., num6]
		data modify block 0 0 0 Text1 set value '["",{"text":"F","font":"space:default"},{"nbt":"display[1]","storage":"number:main","font":"number:red_head"},{"nbt":"display[2]","storage":"number:main","font":"space:default"},{"nbt":"display[3]","storage":"number:main","font":"number:red_lower"},{"nbt":"display[4]","storage":"number:main","font":"space:default"},{"nbt":"display[5]","storage":"number:main","font":"number:red"},{"nbt":"display[6]","storage":"number:main","font":"space:default"},{"nbt":"display[7]","storage":"number:main","font":"number:red_lower"},{"nbt":"display[8]","storage":"number:main","font":"space:default"},{"nbt":"display[9]","storage":"number:main","font":"number:red"},{"text":"F","font":"space:default"}]'
		
		summon item ~ ~1 ~ {Tags:["red_number","new"],Motion:[0.0,0.03,0.0],Item:{id:"minecraft:flint_and_steel",Count:1b,tag:{CustomModelData:1}},NoGravity:1,Invulnerable:1,Age:5980,PickupDelay:-32768,CustomName:'',CustomNameVisible:1}
		execute as @e[type=item,tag=new,limit=1] run function ARG(_PATH)set_display
	}
	
	func insert_digit()
	{
		scoreboard players operation #GLOBAL display_number = @s display_number
		scoreboard players operation #GLOBAL display_number %= #10 const
		
		data modify storage number:main display prepend value 0
		execute store result storage number:main display[0] int 1 run scoreboard players get #GLOBAL display_number
		data modify storage number:main display prepend value 6
		
		scoreboard players operation @s display_number /= #10 const
		execute if score @s display_number matches 1.. run function ARG(_PATH)insert_digit
	}
	
	func set_display()
	{
		data modify entity @s CustomName set from block 0 0 0 Text1
		tag @s remove new
	}
}
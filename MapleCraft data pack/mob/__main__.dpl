```
with open(PROJ_DIR + "/../mobs.py", "r", encoding="utf8") as f:
	exec(f.read())

MOB_UUID = 0
```

import mob_skill, map_mobs

namespace mob()
{
	func install()
	{
		scoreboard objectives add spawn_alpha dummy
		scoreboard objectives add skill_delay dummy
		scoreboard objectives add anger_time dummy
		
		scoreboard objectives add mob_cd1 dummy
		scoreboard objectives add mob_cd2 dummy
		scoreboard objectives add mob_cd3 dummy
		scoreboard objectives add mob_cd4 dummy
		scoreboard objectives add mob_cd5 dummy
		
		scoreboard objectives add uuid0 dummy
		scoreboard objectives add uuid1 dummy
		scoreboard objectives add uuid2 dummy
		scoreboard objectives add uuid3 dummy
	}
	
	folder setting()
	{
		func check()
		{
			execute store result score #GLOBAL temp run scoreboard players add @s spawn_alpha 1
			execute if score @s spawn_alpha matches 1 on controller run function ARG(_PATH)set_model
			execute if score @s spawn_alpha matches ..9 on controller store result entity @s ArmorItems[3].tag.CustomPotionColor int 10 run scoreboard players get #GLOBAL temp
			execute if score @s spawn_alpha matches 10 run function ARG(_PATH)run
		}
		
		func set_model()
		{
			execute if data entity @s ArmorItems[3].tag.info.move_frame store result score @s move_frame run data get entity @s ArmorItems[3].tag.info.move_frame
			execute if data entity @s ArmorItems[3].tag.info.stand_frame store result score @s stand_frame run data get entity @s ArmorItems[3].tag.info.stand_frame
			execute store result score @s base_model run data get entity @s ArmorItems[3].tag.CustomModelData
			
			data remove entity @s ArmorItems[3].tag.info
		}
		
		func run()
		{
			function ARG(_PATH)set_stats
			execute on passengers run function ARG(_PATH)set_pair
			
			scoreboard players set @s spawn_alpha 10
			tag @s remove new_summon
			tag @s remove new_enemy
			tag @s add enemy
		}

		func set_pair()
		{
			execute if entity @s[type=zombified_piglin] run function ARG(_PATH)piglin_set
			execute if entity @s[type=magma_cube,tag=!no_body_attack] run data modify entity @s NoAI set value 0
			scoreboard players operation @s number = #MOB_ID number
			tag @s add enemy
		}
		
		func set_stats()
		{
			execute store result score @s level run data get entity @s ArmorItems[3].tag.info.level
			
			execute store result score @s hp_max run data get entity @s ArmorItems[3].tag.info.hp_max
			scoreboard players operation @s hp = @s hp_max
			execute store result score @s mp_max run data get entity @s ArmorItems[3].tag.info.mp_max
			scoreboard players operation @s mp = @s mp_max
			
			execute store result score @s base_accuracy run data get entity @s ArmorItems[3].tag.info.accuracy
			
			execute store result score @s avoidability run data get entity @s ArmorItems[3].tag.info.avoidability
			execute if data entity @s ArmorItems[3].tag.info.magic_attack store result score @s weapon_mattack run data get entity @s ArmorItems[3].tag.info.magic_attack
			execute store result score @s weapon_defense run data get entity @s ArmorItems[3].tag.info.weapon_defense
			execute store result score @s magic_defense run data get entity @s ArmorItems[3].tag.info.magic_defense
			execute store result score @s kb run data get entity @s ArmorItems[3].tag.info.kb
			execute store result score @s speed run data get entity @s ArmorItems[3].tag.info.speed
			execute store result score @s exp run data get entity @s ArmorItems[3].tag.info.exp
			execute store result score @s hurt_sound run data get entity @s ArmorItems[3].tag.info.hurt_sound
			execute store result score @s die_sound run data get entity @s ArmorItems[3].tag.info.die_sound
			
			execute if data entity @s ArmorItems[3].tag.info.skill_id store result score @s skill_id run data get entity @s ArmorItems[3].tag.info.skill_id
			
			data remove entity @s ArmorItems[3].tag.info
			
			# scoreboard players set @s status 0
			scoreboard players set @s hit_distance 0
			# scoreboard players set @s[tag=!no_jump] rand_jump 60
			execute store result score @s number run scoreboard players add #MOB_ID number 1
		}
		
		func piglin_set()
		{
			# 召喚的怪物不會經過淡入流程，這邊要再做一次模型設定
			execute unless score @s base_model matches 1.. run function ARG(_PATH)set_model
			data modify entity @s ArmorItems[3].tag.CustomPotionColor set value -1
		}
	}
	
	func main()
	{
		execute as @e[type=ocelot,tag=new_enemy] run function ARG(_PATH)setting/check
		execute as @e[type=ocelot,tag=enemy,scores={skill_id=1..}] run function ARG(_PATH)skills/update
	}
	
	folder loop()
	{
		func schedule_1s()
		{
			execute as @e[type=ocelot] if score @s anger_time matches 1.. run function ARG(_PATH)anger_countdown/run
		}
		
		folder anger_countdown()
		{
			func run()
			{
				scoreboard players remove @s anger_time 1
				execute if score @s anger_time matches 0 run function ARG(_PATH)reset
			}
			
			func reset()
			{
				execute on controller run attribute @s minecraft:generic.follow_range base set 0.0
				scoreboard players reset @s anger_time
			}
		}
		
		func schedule_10s()
		{
			function random:rand_rate
			execute as @e[type=marker,tag=mob_spawner] at @s run data modify block ~ ~ ~ auto set value 1b
			
			execute as @e[type=ocelot] run data modify entity @s Age set value -2147483648
		}
	}
	
	func write_attacker_data()
	{
		scoreboard players operation #ATTACKER_ACC temp = @s base_accuracy
		scoreboard players operation #ATTACKER_LEVEL temp = @s level
	}
	
	func damage()
	{
		attribute @s minecraft:generic.knockback_resistance base set 0.0
		
		# 繩子上額外做偏移，減少被鷹架影響動能
		execute if block ~ ~ ~ minecraft:scaffolding facing entity 00000000-0000-0006-0000-000000000006 feet rotated ~ 0 run tp @s ^ ^ ^-0.6
		damage @s 0.00001 minecraft:generic by 00000000-0000-0006-0000-000000000006
		
		attribute @s minecraft:generic.knockback_resistance base set 1.0
	}
	
	folder map() from map_mobs();
	folder skills() from mob_skill();
	
	func test()
	{
		```
		# 殭屍菇菇
		print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("2230101"))
		# 鱷魚 KPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300001"))
		# 青蛇 KPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300000"))
		# 風獨眼獸 KPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300002"))
		# 超級綠水靈
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300003"))
		# 綠水靈
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("0210100"))
		# 綠菇菇
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("1110100"))
		# 刺菇菇
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("2110200"))
		# 小石球 新手訓練
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300018"))
		# 白色發條鼠 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300005"))
		# 黑色發條鼠 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300006"))
		# 機械章魚 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300007"))
		# 次元之哀目
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300008"))
		# 次元之怒目
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300014"))
		# 積木泥人王 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300013"))
		# 積木泥人 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300009"))
		# 泥人領導者 LDPQ
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300010"))
		# 妖魔 LDPQ
		print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300015"))
		# 進化妖魔 LDPQ
		print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300016"))
		# 妖魔隊長 LDPQ
		print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300017"))
		# 機械鯨魚王
		# print("execute rotated ~ 0 positioned ^ ^ ^5 align y run", spawn_mob("9300012"))
		```
	}
}